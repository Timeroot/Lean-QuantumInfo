/-
Copyright (c) 2025 Alex Meiburg. All rights reserved.
Released under MIT license as described in the file LICENSE.
Authors: Alex Meiburg
-/
import Mathlib.Analysis.SpecialFunctions.Log.Basic
import Mathlib.Order.CompletePartialOrder

--Can this be rewritten more generally? For `finiteness` to work, I don't know how.
--PR'ed in #33105
@[aesop (rule_sets := [finiteness]) unsafe apply]
theorem ite_eq_top {Œ± : Type*} [Top Œ±] (h : Prop) [Decidable h] {x y : Œ±} (hx : x ‚â† ‚ä§) (hy : y ‚â† ‚ä§) :
    (if h then x else y) ‚â† ‚ä§ := by
  split <;> assumption

section subtype_val_iSup

variable {Œπ Œ± : Type*} [i : Nonempty Œπ] [ConditionallyCompleteLattice Œ±]
  {f : Œπ ‚Üí Œ±} {a b : Œ±} [Fact (a ‚â§ b)]

/- This isn't marked as `simp` because rewriting from a sup over a `CompleteLattice` into a
`ConditionallyCompleteLattice` would, pretty often, be undesirable. -/
theorem subtype_val_iSup (h : ‚àÄ i, f i ‚àà Set.Icc a b) :
    (‚®Ü i, (‚ü®f i, h i‚ü© : ‚Üë(Set.Icc a b))).val = ‚®Ü i, f i := by
  simp only [iSup, sSup, Set.range_eq_empty_iff, not_isEmpty_of_nonempty, reduceDIte]
  congr 1; ext1
  simp

theorem subtype_val_iSup' (h : ‚àÄ i, f i ‚àà Set.Icc a b) :
    ‚®Ü i, (‚ü®f i, h i‚ü© : ‚Üë(Set.Icc a b)) =
      ‚ü®‚®Ü i, f i, ‚ü®(h i.some).1.trans (le_ciSup ‚ü®b, by intro; grind‚ü© _), ciSup_le (h ¬∑|>.2)‚ü©‚ü© := by
  rw [Subtype.eq_iff, subtype_val_iSup]

/- This isn't marked as `simp` because rewriting from a sup over a `CompleteLattice` into a
`ConditionallyCompleteLattice` would, pretty often, be undesirable. -/
theorem subtype_val_iInf (h : ‚àÄ i, f i ‚àà Set.Icc a b) :
    (‚®Ö i, (‚ü®f i, h i‚ü© : ‚Üë(Set.Icc a b))).val = ‚®Ö i, f i := by
  simp only [iInf, sInf, Set.range_eq_empty_iff, not_isEmpty_of_nonempty, reduceDIte]
  congr 1; ext1
  simp

theorem subtype_val_iInf' (h : ‚àÄ i, f i ‚àà Set.Icc a b) :
    ‚®Ö i, (‚ü®f i, h i‚ü© : ‚Üë(Set.Icc a b)) =
      ‚ü®‚®Ö i, f i, ‚ü®le_ciInf (h ¬∑|>.1), (ciInf_le ‚ü®a, by intro; grind‚ü© _).trans (h i.some).2‚ü©‚ü© := by
  rw [Subtype.eq_iff, subtype_val_iInf]

end subtype_val_iSup

--PR'ed in #33106
@[simp]
theorem Real.log_comp_exp : log ‚àò exp = _root_.id := by
  ext
  simp

open scoped ENNReal Topology in
/-- Analogous to `bdd_le_mul_tendsto_zero`, for `ENNReal` (which otherwise lacks a continuous
multiplication function). The product of a sequence that tends to zero with any bounded sequence
also tends to zero. -/
protected lemma ENNReal.bdd_le_mul_tendsto_zero
  {Œ± : Type*} {l : Filter Œ±} {f g : Œ± ‚Üí ‚Ñù‚â•0‚àû} {b : ‚Ñù‚â•0‚àû} (hb : b ‚â† ‚ä§)
  (hf : l.Tendsto f (ùìù 0)) (hg : ‚àÄ·∂† (x : Œ±) in l, g x ‚â§ b) :
    l.Tendsto (fun x ‚Ü¶ f x * g x) (ùìù 0) := by
  rw [ENNReal.tendsto_nhds_zero] at hf ‚ä¢
  intro Œµ hŒµpos
  by_cases hb_pos : 0 < b
  ¬∑ filter_upwards [hf (Œµ / b) (by simp [hb, hŒµpos.ne']), hg] with x hx‚ÇÅ hx‚ÇÇ
    grw [hx‚ÇÅ, hx‚ÇÇ, ENNReal.div_mul_cancel hb_pos.ne' hb]
  ¬∑ filter_upwards [hg] with x hx
    grind [not_lt, nonpos_iff_eq_zero, mul_zero, zero_le]

--PULLOUT: Belongs in Mathlib/Algebra/Order/Group/Pointwise/CompleteLattice.lean
-- (after appropriately generalizing to MulPosMono)
open scoped Pointwise in
theorem csInf_mul_nonneg {s t : Set ‚Ñù}
  (hs‚ÇÄ : s.Nonempty) (hs‚ÇÅ : ‚àÄ x ‚àà s, 0 ‚â§ x) (ht‚ÇÄ : t.Nonempty) (ht‚ÇÅ : ‚àÄ x ‚àà t, 0 ‚â§ x) :
    sInf (s * t) = sInf s * sInf t := by
  apply le_antisymm
  ¬∑ set a := sInf s
    set b := sInf t
    have h_eps : ‚àÄ Œµ > 0, ‚àÉ x ‚àà s, x < a + Œµ ‚àß ‚àÉ y ‚àà t, y < b + Œµ := by
      intro Œµ Œµ_pos
      obtain ‚ü®x, hx‚ÇÅ, hx‚ÇÇ‚ü© := exists_lt_of_csInf_lt hs‚ÇÄ (lt_add_of_pos_right a Œµ_pos)
      obtain ‚ü®y, hy‚ÇÅ, hy‚ÇÇ‚ü© := exists_lt_of_csInf_lt ht‚ÇÄ (lt_add_of_pos_right b Œµ_pos)
      exact ‚ü®x, hx‚ÇÅ, hx‚ÇÇ, y, hy‚ÇÅ, hy‚ÇÇ‚ü©
    have h_prod_eps : ‚àÄ Œµ > 0, ‚àÉ x ‚àà s, ‚àÉ y ‚àà t, x * y < (a + Œµ) * (b + Œµ) := by
      intro Œµ hŒµ
      obtain ‚ü®x, hx‚ÇÅ, hx‚ÇÇ, y, hy‚ÇÅ, hy‚ÇÇ‚ü© := h_eps Œµ hŒµ
      exact ‚ü®x, hx‚ÇÅ, y, hy‚ÇÅ, by nlinarith [hs‚ÇÅ x hx‚ÇÅ, ht‚ÇÅ y hy‚ÇÅ]‚ü©
    have h_lim : Filter.Tendsto (fun Œµ => (a + Œµ) * (b + Œµ)) (nhdsWithin 0 (Set.Ioi 0)) (nhds (a * b)) := by
      exact tendsto_nhdsWithin_of_tendsto_nhds (Continuous.tendsto' (by continuity) _ _ (by norm_num))
    apply le_of_tendsto_of_tendsto tendsto_const_nhds h_lim
    filter_upwards [self_mem_nhdsWithin] with Œµ hŒµ
    specialize h_prod_eps Œµ hŒµ
    choose x hx y hy using h_prod_eps
    refine le_trans ?_ hy.right.le
    refine csInf_le ‚ü®0, ?_‚ü© ?_
    ¬∑ rintro x ‚ü®u, hu, v, hv, rfl‚ü©
      exact mul_nonneg (hs‚ÇÅ u hu) (ht‚ÇÅ v hv)
    ¬∑ exact ‚ü®_, hx, _, hy.left, rfl‚ü©
  ¬∑ apply le_csInf (hs‚ÇÄ.mul ht‚ÇÄ)
    rintro _ ‚ü®x, hx, y, hy, rfl‚ü©
    apply mul_le_mul
    ¬∑ exact csInf_le ‚ü®0, hs‚ÇÅ‚ü© hx
    ¬∑ exact csInf_le ‚ü®0, ht‚ÇÅ‚ü© hy
    ¬∑ exact Real.sInf_nonneg ht‚ÇÅ
    ¬∑ exact hs‚ÇÅ x hx
