/-
Copyright (c) 2025 Alex Meiburg. All rights reserved.
Released under MIT license as described in the file LICENSE.
Authors: Alex Meiburg
-/
import QuantumInfo.ForMathlib.HermitianMat.Reindex

/-! # Trace of Hermitian Matrices

While the trace of a Hermitian matrix is, in informal math, typically just "the same as" a trace of
a matrix that happens to be Hermitian - it is a real number, not a complex number. Or more generally,
it is a self-adjoint element of the base `StarAddMonoid`.

Working directly with `Matrix.trace` then means that there would be constant casts between rings,
chasing imaginary parts and inequalities and so on. By defining `HermitianMat.trace` as its own
operation, we encapsulate the mess and give a clean interface.

The `IsMaximalSelfAdjoint` class is used so that (for example) for matrices over â„¤ or â„,
`HermitianMat.trace` works as well and is in fact defeq to `Matrix.trace`. For â„‚ or `RCLike`,
it uses the real part.
-/

namespace HermitianMat

variable {R n m Î± : Type*} [Star R] [TrivialStar R] [Fintype n] [Fintype m]

section star
variable [AddGroup Î±] [StarAddMonoid Î±] [CommSemiring R] [Semiring Î±] [Algebra R Î±] [IsMaximalSelfAdjoint R Î±]

/-- The trace of the matrix. This requires a `IsMaximalSelfAdjoint R Î±` instance, and then maps from
  `HermitianMat n Î±` to `R`. This means that the trace of (say) a `HermitianMat n â„¤` gives values in â„¤,
  but that the trace of a `HermitianMat n â„‚` gives values in â„. The fact that traces are "automatically"
  real reduces coercions down the line. -/
def trace (A : HermitianMat n Î±) : R :=
  IsMaximalSelfAdjoint.selfadjMap (A.toMat.trace)

/-- `HermitianMat.trace` reduces to `Matrix.trace` in the algebra.-/
theorem trace_eq_trace (A : HermitianMat n Î±) : algebraMap R Î± A.trace = Matrix.trace A.toMat := by
  rw [HermitianMat.trace, Matrix.trace, map_sum, map_sum]
  congr! 1
  exact IsMaximalSelfAdjoint.selfadj_algebra (Matrix.IsHermitian.apply A.H _ _)

variable [StarModule R Î±] in
@[simp]
theorem trace_smul (A : HermitianMat n Î±) (r : R) : (r â€¢ A).trace = r * A.trace := by
  simp [trace, IsMaximalSelfAdjoint.selfadj_smul]

end star
section semiring
variable [CommSemiring R] [Ring Î±] [StarAddMonoid Î±] [Algebra R Î±] [IsMaximalSelfAdjoint R Î±]

@[simp]
theorem trace_zero : (0 : HermitianMat n Î±).trace = 0 := by
  simp [trace]

@[simp]
theorem trace_add (A B : HermitianMat n Î±) : (A + B).trace = A.trace + B.trace := by
  simp [trace]

end semiring
section ring

variable [CommRing R] [Ring Î±] [StarAddMonoid Î±] [Algebra R Î±] [IsMaximalSelfAdjoint R Î±]
@[simp]
theorem trace_neg (A : HermitianMat n Î±) : (-A).trace = -A.trace := by
  simp [trace]

@[simp]
theorem trace_sub (A B : HermitianMat n Î±) : (A - B).trace = A.trace - B.trace := by
  simp [trace]

end ring
section starring

variable [CommRing R] [CommRing Î±] [StarRing Î±] [Algebra R Î±] [IsMaximalSelfAdjoint R Î±]

--PULLOUT
theorem _root_.Matrix.IsHermitian.isSelfAdjoint_trace {A : Matrix n n Î±} (hA : A.IsHermitian) :
    IsSelfAdjoint A.trace := by
  simp [Matrix.trace, IsSelfAdjoint, â† Matrix.star_apply, show star A = A from hA]

variable (A : HermitianMat m Î±) (B : HermitianMat n Î±)

@[simp]
theorem trace_kronecker [FaithfulSMul R Î±] : (A âŠ—â‚– B).trace = A.trace * B.trace := by
  apply FaithfulSMul.algebraMap_injective R Î±
  simp only [trace, kronecker_coe]
  rw [Matrix.trace_kronecker A.toMat B.toMat]
  simp only [map_mul]
  have hA := A.H.isSelfAdjoint_trace
  have hB := B.H.isSelfAdjoint_trace
  open IsMaximalSelfAdjoint in
  rw [selfadj_algebra hA, selfadj_algebra hB, selfadj_algebra (hA.mul hB)]

end starring

section trivialstar

variable [Star Î±] [TrivialStar Î±] [CommSemiring Î±]

/-- `HermitianMat.trace` reduces to `Matrix.trace` when the elements are a `TrivialStar`. -/
@[simp]
theorem trace_eq_trace_trivial (A : HermitianMat n â„) : A.trace = Matrix.trace A.toMat := by
  rw [â† trace_eq_trace]
  rfl

end trivialstar

section RCLike

variable {n m ğ•œ : Type*} [Fintype n] [Fintype m] [RCLike ğ•œ]

--strictly speaking this works over any division ring, not just â„ + RCLike
instance FiniteDimensional : FiniteDimensional â„ (HermitianMat n ğ•œ) :=
  FiniteDimensional.finiteDimensional_submodule (selfAdjoint.submodule â„ (Matrix n n ğ•œ))

theorem trace_eq_re_trace (A : HermitianMat n ğ•œ) : A.trace = RCLike.re A.toMat.trace := by
  rfl

/-- `HermitianMat.trace` reduces to `Matrix.trace` when the elements are `RCLike`. -/
@[simp]
theorem trace_eq_trace_rc (A : HermitianMat n ğ•œ) : â†‘A.trace = A.toMat.trace := by
  rw [trace, Matrix.trace, map_sum, RCLike.ofReal_sum]
  congr 1
  exact Matrix.IsHermitian.coe_re_diag A.H

theorem trace_diagonal {T : Type*} [Fintype T] [DecidableEq T] (f : T â†’ â„) :
    (diagonal ğ•œ f).trace = âˆ‘ i, f i := by
  rw [trace_eq_re_trace]
  simp [HermitianMat.diagonal, Matrix.trace]

theorem sum_eigenvalues_eq_trace [DecidableEq n] (A : HermitianMat n ğ•œ) :
    âˆ‘ i, A.H.eigenvalues i = A.trace := by
  convert congrArg RCLike.re A.H.sum_eigenvalues_eq_trace
  rw [RCLike.ofReal_re]

--Proving that traces are 0 or 1 is common enough that we have a convenience lemma here for turning
--statements about HermitianMat traces into Matrix traces.
theorem trace_eq_zero_iff (A : HermitianMat n ğ•œ) : A.trace = 0 â†” A.toMat.trace = 0 := by
  rw [â† trace_eq_trace_rc]
  use mod_cast id, mod_cast id

theorem trace_eq_one_iff (A : HermitianMat n ğ•œ) : A.trace = 1 â†” A.toMat.trace = 1 := by
  rw [â† trace_eq_trace_rc]
  use mod_cast id, mod_cast id

@[simp]
theorem trace_reindex (A : HermitianMat n â„‚) (e : n â‰ƒ m) :
    (A.reindex e).trace = A.trace := by
  simp [reindex, trace_eq_re_trace]

end RCLike
