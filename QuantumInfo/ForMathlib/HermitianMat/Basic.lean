/-
Copyright (c) 2025 Alex Meiburg. All rights reserved.
Released under MIT license as described in the file LICENSE.
Authors: Alex Meiburg
-/
import QuantumInfo.ForMathlib.Matrix
import QuantumInfo.ForMathlib.IsMaximalSelfAdjoint
import QuantumInfo.ForMathlib.ContinuousLinearMap

import Mathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.ExpLog
import Mathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.Rpow.Basic
import Mathlib.LinearAlgebra.Matrix.HermitianFunctionalCalculus
import Mathlib.Analysis.Matrix

/-- The type of Hermitian matrices, as a `Subtype`. Equivalent to a `Matrix n n Œ±` bundled
with the fact that `Matrix.IsHermitian`. -/
abbrev HermitianMat (n : Type*) (Œ± : Type*) [AddGroup Œ±] [StarAddMonoid Œ±] :=
  (selfAdjoint (Matrix n n Œ±) : Type (max u_1 u_2))

namespace HermitianMat

variable {Œ± : Type*} {m n : Type*}

section addgroup

variable [AddGroup Œ±] [StarAddMonoid Œ±]

theorem eq_IsHermitian : HermitianMat n Œ±  = { m : Matrix n n Œ± // m.IsHermitian} := by
  rfl

@[coe, reducible] def toMat : HermitianMat n Œ± ‚Üí Matrix n n Œ± :=
  Subtype.val

instance : Coe (HermitianMat n Œ±) (Matrix n n Œ±) := ‚ü®toMat‚ü©

@[simp]
theorem val_eq_coe (A : HermitianMat n Œ±) : A.val = A := by
  rfl

@[simp]
theorem mk_toMat (x : Matrix n n Œ±) (h) : HermitianMat.toMat (Subtype.mk x h) = x := by
  rfl

/-- Alias for HermitianMat.property or HermitianMat.2, this gets the fact that the value
  is actually `IsHermitian`.-/
theorem H (A : HermitianMat n Œ±) : A.toMat.IsHermitian :=
  A.2

@[ext] protected theorem ext {A B : HermitianMat n Œ±} : A.toMat = B.toMat ‚Üí A = B :=
  Subtype.eq

instance instFun : FunLike (HermitianMat n Œ±) n (n ‚Üí Œ±) where
  coe M := (M : Matrix n n Œ±)
  coe_injective' _ _ h := HermitianMat.ext h

instance instStar : Star (HermitianMat n Œ±) :=
  ‚ü®(¬∑)‚ü©

instance instTrivialStar : TrivialStar (HermitianMat n Œ±) :=
  ‚ü®(refl ¬∑)‚ü©

@[simp]
theorem conjTranspose_toMat (A : HermitianMat n Œ±) :
    A.toMat.conjTranspose = A :=
  A.H

@[simp]
theorem smul_toMat (A : HermitianMat n ‚ÑÇ) (c : ‚Ñù) :
  ((Complex.ofReal c) ‚Ä¢ A.toMat) = (c ‚Ä¢ A).toMat := by
  rfl

end addgroup
section commring

variable [CommRing Œ±] [StarRing Œ±] [DecidableEq n] [Fintype n]

noncomputable instance instInv : Inv (HermitianMat n Œ±) :=
  ‚ü®fun x ‚Ü¶ ‚ü®x‚Åª¬π, Matrix.IsHermitian.inv x.H‚ü©‚ü©

noncomputable instance instZPow : Pow (HermitianMat n Œ±) ‚Ñ§ :=
  ‚ü®fun x z ‚Ü¶ ‚ü®x^z, Matrix.IsHermitian.zpow x.H z‚ü©‚ü©

/-
--There is already a `One` instance when `n` is a `Fintype` (it comes through the fact that we have a
-- (`Ring`) but in principle we shouldn't need that (only DecidableEq!). But, the fact that trying
-- `simp` in `coe_one` below causes a defeq timeout is worrying. So, we keep this commented out, we'll
-- use `Fintype` everywhere we want a `1`.
variable {n : Type*} [DecidableEq n]

instance : One (HermitianMat n Œ±) :=
  ‚ü®1, by
    simp [selfAdjoint.mem_iff, ‚Üê Matrix.ext_iff, Matrix.one_apply, apply_ite (Œ≤ := Œ±), eq_comm]‚ü©

@[simp]
theorem coe_one : (1 : HermitianMat n Œ±).toMat = 1 := by
  rfl
-/

end commring
section rclike

variable [RCLike Œ±]

@[simp]
theorem im_eq_zero (A : HermitianMat n Œ±) (x : n) :
    RCLike.im (A x x) = 0 := by
  simpa [CharZero.eq_neg_self_iff] using congrArg (RCLike.im <| ¬∑ x x) A.H.symm

--Repeat it explicitly for ‚ÑÇ so that simp can find it
@[simp]
theorem Complex_im_eq_zero (A : HermitianMat n ‚ÑÇ) (x : n) :
    (A x x).im = 0 :=
  A.im_eq_zero x

variable [Fintype n] [DecidableEq n]

end rclike

section conj

variable [CommRing Œ±] [StarRing Œ±] [Fintype n]
variable (A : HermitianMat n Œ±)

/-- The Hermitian matrix given by conjugating by a (possibly rectangular) Matrix. If we required `B` to be
square, this would apply to any `Semigroup`+`StarMul` (as proved by `IsSelfAdjoint.conjugate`). But this lets
us conjugate to other sizes too, as is done in e.g. Kraus operators. That is, it's a _heterogeneous_ conjguation.
-/
def conj {m} (B : Matrix m n Œ±) : HermitianMat n Œ± ‚Üí+ HermitianMat m Œ± where
  toFun A :=
    ‚ü®B * A.toMat * B.conjTranspose, by
    ext
    simp only [Matrix.star_apply, Matrix.mul_apply, Matrix.conjTranspose_apply, Finset.sum_mul,
      star_sum, star_mul', star_star, show ‚àÄ (a b : n), star (A.toMat b a) = A.toMat a b from congrFun‚ÇÇ A.property]
    rw [Finset.sum_comm]
    congr! 2
    ring‚ü©
  map_add' _ _ := by ext1; simp [Matrix.mul_add, Matrix.add_mul]
  map_zero' := by simp

theorem conj_apply (B : Matrix m n Œ±) (A : HermitianMat n Œ±) :
    conj B A = ‚ü®B * A.toMat * B.conjTranspose, (conj B A).2‚ü© := by
  rfl

@[simp]
theorem conj_apply_toMat (B : Matrix m n Œ±) (A : HermitianMat n Œ±) :
    (conj B A).toMat = B * A.toMat * B.conjTranspose := by
  rfl

theorem conj_conj {m l} [Fintype m] (B : Matrix m n Œ±) (C : Matrix l m Œ±) :
    (A.conj B).conj C = A.conj (C * B) := by
  ext1
  simp [Matrix.conjTranspose_mul, Matrix.mul_assoc]

variable (B : HermitianMat n Œ±)

@[simp]
theorem conj_zero [DecidableEq n] : A.conj (0 : Matrix n n Œ±) = 0 := by
  simp [conj_apply]

@[simp]
theorem conj_one [DecidableEq n] : A.conj (1 : Matrix n n Œ±) = A := by
  simp [conj_apply]

variable (R : Type*) [Star R] [TrivialStar R] [CommSemiring R] [Algebra R Œ±] [StarModule R Œ±]

/-- `HermitianMat.conj` as an `R`-linear map, where `R` is the ring of relevant reals. -/
def conjLinear {m} (B : Matrix m n Œ±) : HermitianMat n Œ± ‚Üí‚Çó[R] HermitianMat m Œ± where
  toAddHom := conj B
  map_smul' _ _ := by
    ext1
    --Squeezing this simp because otherwise we run out of heartbeats fast
    simp only [AddHom.toFun_eq_coe, AddHom.coe_coe, conj_apply_toMat,
      selfAdjoint.val_smul, val_eq_coe, Matrix.mul_smul, Matrix.smul_mul, RingHom.id_apply]

@[simp]
theorem conjLinear_apply (B : Matrix m n Œ±) : conjLinear R B A = conj B A  := by
  rfl

end conj

section eigenspace

variable {ùïú} [RCLike ùïú] [Fintype n] [DecidableEq n] (A : HermitianMat n ùïú)

instance [i : Nonempty n] : FaithfulSMul ‚Ñù (HermitianMat n ùïú) where
  eq_of_smul_eq_smul h := by
    simpa [RCLike.smul_re] using congr(RCLike.re ($(h 1).val i.some i.some))

/-- The continuous linear map associated with a Hermitian matrix. -/
def lin : EuclideanSpace ùïú n ‚ÜíL[ùïú] EuclideanSpace ùïú n where
  toLinearMap := A.toMat.toEuclideanLin
  cont := LinearMap.continuous_of_finiteDimensional _

@[simp]
theorem isSymmetric : A.lin.IsSymmetric :=
  Matrix.isHermitian_iff_isSymmetric.mp A.H

@[simp]
theorem lin_zero : (0 : HermitianMat n ùïú).lin = 0 := by
  simp [lin]; rfl

@[simp]
theorem lin_one : (1 : HermitianMat n ùïú).lin = 1 := by
  simp [lin]; rfl

noncomputable def eigenspace (Œº : ùïú) : Submodule ùïú (EuclideanSpace ùïú n) :=
  Module.End.eigenspace A.lin Œº

/-- The kernel of a Hermitian matrix `A` as a submodule of Euclidean space, defined by
`LinearMap.ker A.toMat.toEuclideanLin`. Equivalently, the zero-eigenspace. -/
def ker : Submodule ùïú (EuclideanSpace ùïú n) :=
  LinearMap.ker A.lin

theorem mem_ker_iff_mulVec_zero (x : EuclideanSpace ùïú n) : x ‚àà A.ker ‚Üî A.toMat.mulVec x = 0 := by
  simp [ker, LinearMap.mem_ker, lin, Matrix.toEuclideanLin_apply]
  norm_cast

/-- The kernel of a Hermitian matrix is its zero eigenspace. -/
theorem ker_eq_eigenspace_zero : A.ker = A.eigenspace 0 := by
  ext
  simp [ker, eigenspace]

@[simp]
theorem ker_zero : (0 : HermitianMat n ùïú).ker = ‚ä§ := by
  simp [ker]

@[simp]
theorem ker_one : (1 : HermitianMat n ùïú).ker = ‚ä• := by
  simp [ker]; rfl

theorem ker_pos_smul {c : ‚Ñù} (hc : 0 < c) : (c ‚Ä¢ A).ker = A.ker := by
  ext x
  simp [mem_ker_iff_mulVec_zero, Matrix.smul_mulVec, hc.ne']

/-- The support of a Hermitian matrix `A` as a submodule of Euclidean space, defined by
`LinearMap.range A.toMat.toEuclideanLin`. Equivalently, the sum of all nonzero eigenspaces. -/
def support : Submodule ùïú (EuclideanSpace ùïú n) :=
  LinearMap.range A.lin

/-- The support of a Hermitian matrix is the sum of its nonzero eigenspaces. -/
theorem support_eq_sup_eigenspace_nonzero : A.support = ‚®Ü Œº ‚â† 0, A.eigenspace Œº := by
  exact A.lin.support_eq_sup_eigenspace_nonzero A.isSymmetric

@[simp]
theorem support_zero : (0 : HermitianMat n ùïú).support = ‚ä• := by
  simp [support]

@[simp]
theorem support_one : (1 : HermitianMat n ùïú).support = ‚ä§ := by
  simpa [support] using LinearMap.ker_eq_bot_iff_range_eq_top.mp rfl

@[simp]
theorem ker_orthogonal_eq_support : A.ker·óÆ = A.support := by
  rw [ker, support]
  convert ContinuousLinearMap.orthogonal_ker A.lin
  simp

@[simp]
theorem support_orthogonal_eq_range : A.support·óÆ = A.ker := by
  rw [ker, support]
  convert ContinuousLinearMap.orthogonal_range A.lin
  simp

end eigenspace

section diagonal

--TODO: Generalize this more types than ‚Ñù/‚ÑÇ
def diagonal [DecidableEq n] (f : n ‚Üí ‚Ñù) : HermitianMat n ‚ÑÇ :=
  ‚ü®Matrix.diagonal (f ¬∑),
    by simp [selfAdjoint.mem_iff, Matrix.star_eq_conjTranspose, Matrix.diagonal_conjTranspose]‚ü©

theorem diagonal_conj_diagonal [Fintype n] [DecidableEq n] (f g : n ‚Üí ‚Ñù) :
    (diagonal f).conj (diagonal g) =
    diagonal (fun i ‚Ü¶ f i * (g i)^2) := by
  simp [diagonal, conj]
  intro
  ring

end diagonal

section kronecker
open Kronecker

variable [CommRing Œ±] [StarRing Œ±]

/-- The kronecker product of two HermitianMats, see `Matrix.kroneckerMap`. -/
@[simps]
def kronecker (A : HermitianMat m Œ±) (B : HermitianMat n Œ±) : HermitianMat (m √ó n) Œ± where
  val := A.toMat ‚äó‚Çñ B.toMat
  property := Matrix.kroneckerMap_IsHermitian A.H B.H

@[inherit_doc HermitianMat.kronecker]
scoped[HermitianMat] infixl:100 " ‚äó‚Çñ " => HermitianMat.kronecker

@[simp]
theorem zero_kronecker (A : HermitianMat m Œ±) : (0 : HermitianMat n Œ±) ‚äó‚Çñ A = 0 := by
  ext1; simp

@[simp]
theorem kronecker_zero (A : HermitianMat m Œ±) : A ‚äó‚Çñ (0 : HermitianMat n Œ±) = 0 := by
  ext1; simp

variable [DecidableEq m] [DecidableEq n] [Fintype m] [Fintype n] in
@[simp]
theorem kronecker_one_one : (1 : HermitianMat m Œ±) ‚äó‚Çñ (1 : HermitianMat n Œ±) = 1 := by
  ext1; simp

variable (A B : HermitianMat m Œ±) (C : HermitianMat n Œ±) in
theorem add_kronecker : (A + B) ‚äó‚Çñ C = A ‚äó‚Çñ C + B ‚äó‚Çñ C := by
  ext1; simp [Matrix.add_kronecker]

variable (A : HermitianMat m Œ±) (B C : HermitianMat n Œ±) in
theorem kronecker_add : A ‚äó‚Çñ (B + C) = A ‚äó‚Çñ B + A ‚äó‚Çñ C := by
  ext1; simp [Matrix.kronecker_add]

end kronecker

open scoped Pointwise in
theorem spectrum_prod [Fintype m] [DecidableEq m] [Fintype n] [DecidableEq n]
  {A : HermitianMat m ‚ÑÇ} {B : HermitianMat n ‚ÑÇ} :
    spectrum ‚Ñù (A ‚äó‚Çñ B).toMat = spectrum ‚Ñù A.toMat * spectrum ‚Ñù B.toMat :=
  Matrix.spectrum_prod A.H B.H

--Shortcut instance
noncomputable instance : AddCommMonoid (HermitianMat d ‚ÑÇ) :=
  inferInstance
