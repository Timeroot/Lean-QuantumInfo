import QuantumInfo.ForMathlib.HermitianMat.Order
import Mathlib.Analysis.Convex.Contractible
import Mathlib.Topology.Instances.Real.Lemmas

/-! # Inner product of Hermitian Matrices

For general matrices there are multiple reasonable notions of "inner product" (Hilbert‚ÄìSchmidt inner product,
Frobenius inner product), and so Mathlib avoids giving a canonical `InnerProductSpace` instance. But for the
particular case of Hermitian matrices, these all coincide, so we can put a canonical `InnerProductSpace`
instance.

This _does_ however induce a `Norm` on `HermitianMat` as well, the Frobenius norm, and this is less obviously
a uniquely correct choice. It is something that one essentially has to live with, with the way that Mathlib
currently structures the instances. (Thankfully, all norms induce the same _topology and bornology_ on
finite-dimensional matrices.)

Some care to be taken so that the topology induced by the InnerProductSpace is defeq with the Subtype
topology that HermitianMat inherits from the topology on Matrix. This can be done via
`InnerProductSpace.ofCoreOfTopology`.

-/

namespace HermitianMat

variable {R n Œ± : Type*} [Star R] [TrivialStar R] [Fintype n]

variable [Ring Œ±] [StarAddMonoid Œ±] [CommSemiring R] [Algebra R Œ±] [IsMaximalSelfAdjoint R Œ±] in
/-- The Hermitian inner product, `Tr[AB]`. This is equal to `Matrix.trace (A * B)`, but gives real
  values when the matrices are complex, using `IsMaximalSelfAdjoint`. -/
def inner (A B : HermitianMat n Œ±) : R :=
  IsMaximalSelfAdjoint.selfadjMap ((A.toMat * B.toMat).trace)

section semiring

variable [CommSemiring R] [Ring Œ±] [StarAddMonoid Œ±] [Algebra R Œ±] [IsMaximalSelfAdjoint R Œ±]
variable (A B C : HermitianMat n Œ±)

theorem inner_left_distrib : A.inner (B + C) = A.inner B + A.inner C := by
  simp [inner, left_distrib]

theorem inner_right_distrib : (A + B).inner C = A.inner C + B.inner C := by
  simp [inner, right_distrib]

@[simp]
theorem inner_zero : A.inner 0 = 0 := by
  simp [inner]

@[simp]
theorem zero_inner : HermitianMat.inner 0 A = 0 := by
  simp [inner]

end semiring

section ring

variable [CommRing R] [Ring Œ±] [StarAddMonoid Œ±] [Algebra R Œ±] [IsMaximalSelfAdjoint R Œ±]
variable (A B C : HermitianMat n Œ±)

@[simp]
theorem inner_left_neg : (-A).inner B = -A.inner B := by
  simp [inner]

@[simp]
theorem inner_right_neg : A.inner (-B) = -A.inner B := by
  simp [inner]

theorem inner_left_sub : A.inner (B - C) = A.inner B - A.inner C := by
  simp [inner, mul_sub]

theorem inner_right_sub : (A - B).inner C = A.inner C - B.inner C := by
  simp [inner, sub_mul]

variable [StarModule R Œ±]

@[simp]
theorem smul_inner (r : R) : (r ‚Ä¢ A).inner B = r * A.inner B := by
  simp [inner, IsMaximalSelfAdjoint.selfadj_smul]

@[simp]
theorem inner_smul (r : R) : A.inner (r ‚Ä¢ B) = r * A.inner B := by
  simp [inner, IsMaximalSelfAdjoint.selfadj_smul]

/-- The Hermitian inner product as bilinear form. -/
def inner_BilinForm : LinearMap.BilinForm R (HermitianMat n Œ±) := {
      toFun A := {
        toFun := A.inner
        map_add' := A.inner_left_distrib
        map_smul' r B := inner_smul A B r
      }
      map_add' := by intros; ext1; apply inner_right_distrib
      map_smul' := by intros; ext1; apply smul_inner
    }

@[simp]
theorem inner_BilinForm_coe_apply : ‚áë(inner_BilinForm A) = A.inner :=
  rfl

@[simp]
theorem inner_BilinForm_apply : inner_BilinForm A B = A.inner B :=
  rfl

end ring
section starring

variable [CommSemiring R] [Ring Œ±] [StarRing Œ±] [Algebra R Œ±] [IsMaximalSelfAdjoint R Œ±] [DecidableEq n]
variable (A B : HermitianMat n Œ±)

@[simp]
theorem inner_one : A.inner 1 = A.trace := by
  simp only [inner, selfAdjoint.val_one,  mul_one, trace]

@[simp]
theorem one_inner : HermitianMat.inner 1 A = A.trace := by
  simp only [inner, one_mul, selfAdjoint.val_one, trace]

end starring
section commring

variable [CommSemiring R] [CommRing Œ±] [StarRing Œ±] [Algebra R Œ±] [IsMaximalSelfAdjoint R Œ±]
variable (A B : HermitianMat n Œ±)

/-- The inner product for Hermtian matrices is equal to the trace of the product. -/
theorem inner_eq_trace_mul : algebraMap R Œ± (A.inner B) = (A.toMat * B.toMat).trace := by
  apply IsMaximalSelfAdjoint.selfadj_algebra
  rw [IsSelfAdjoint, Matrix.trace]
  simp_rw [star_sum, Matrix.diag_apply, Matrix.mul_apply, star_sum, star_mul, mul_comm]
  rw [Finset.sum_comm]
  congr! <;> apply congrFun‚ÇÇ (H _)

theorem inner_comm : A.inner B = B.inner A := by
  rw [inner, inner, Matrix.trace_mul_comm]

end commring

section trivialstar
variable [CommRing Œ±] [StarRing Œ±] [TrivialStar Œ±]

/-- `HermitianMat.inner` reduces to `Matrix.trace (A * B)` when the elements are a `TrivialStar`. -/
theorem inner_eq_trace_trivial (A B : HermitianMat n Œ±) : A.inner B = Matrix.trace (A.toMat * B.toMat) := by
  rw [‚Üê inner_eq_trace_mul]
  rfl

end trivialstar

section RCLike
open ComplexOrder
variable {n ùïú : Type*} [Fintype n] [RCLike ùïú] (A B C : HermitianMat n ùïú)

theorem inner_eq_re_trace : A.inner B = RCLike.re (Matrix.trace (A.toMat * B.toMat)) := by
  rfl

theorem inner_eq_trace_rc : A.inner B = Matrix.trace (A.toMat * B.toMat) := by
  change RCLike.ofReal (RCLike.re _) = _
  rw [‚Üê RCLike.conj_eq_iff_re]
  convert (Matrix.trace_conjTranspose (A.toMat * B.toMat)).symm using 1
  rw [Matrix.conjTranspose_mul, A.H, B.H, Matrix.trace_mul_comm]

theorem inner_self_nonneg: 0 ‚â§ A.inner A := by
  simp_rw [inner_eq_re_trace, Matrix.trace, Matrix.diag, Matrix.mul_apply, map_sum]
  refine Finset.sum_nonneg fun i _ ‚Ü¶ Finset.sum_nonneg fun j _ ‚Ü¶ ?_
  rw [‚Üê congrFun‚ÇÇ A.H, Matrix.conjTranspose_apply]
  refine And.left <| RCLike.nonneg_iff.mp ?_
  open ComplexOrder in
  exact star_mul_self_nonneg (A.toMat j i)

variable {A B C}

theorem inner_mul_nonneg (h : 0 ‚â§ A.toMat * B.toMat) : 0 ‚â§ A.inner B := by
  rw [Matrix.PosSemidef.zero_le_iff_posSemidef] at h
  exact (RCLike.nonneg_iff.mp h.trace_nonneg).left

/-- The inner product for PSD matrices is nonnegative. -/
theorem inner_ge_zero (hA : 0 ‚â§ A) (hB : 0 ‚â§ B) : 0 ‚â§ A.inner B := by
  rw [zero_le_iff] at hA hB
  open Classical in
  rw [inner_eq_re_trace, ‚Üê hA.sqrt_mul_self, Matrix.trace_mul_cycle, Matrix.trace_mul_cycle]
  nth_rewrite 1 [‚Üê hA.posSemidef_sqrt.left]
  exact (RCLike.nonneg_iff.mp (hB.conjTranspose_mul_mul_same _).trace_nonneg).left

theorem inner_mono (hA : 0 ‚â§ A) : B ‚â§ C ‚Üí A.inner B ‚â§ A.inner C := fun hBC ‚Ü¶ by
  classical have hTr : 0 ‚â§ A.inner (C - B) := inner_ge_zero hA (zero_le_iff.mpr hBC)
  rw [inner_left_sub] at hTr
  linarith

theorem inner_mono' (hA : 0 ‚â§ A) : B ‚â§ C ‚Üí B.inner A ‚â§ C.inner A := fun hBC ‚Ü¶ by
  rw [inner_comm B A, inner_comm C A]
  exact inner_mono hA hBC

/-- The inner product for PSD matrices is at most the product of their traces. -/
theorem inner_le_mul_trace (hA : 0 ‚â§ A) (hB : 0 ‚â§ B) : A.inner B ‚â§ A.trace * B.trace := by
  classical convert inner_mono hA (le_trace_smul_one hB)
  simp [mul_comm]

--TODO cleanup
private theorem inner_zero_iff_aux_lemma [DecidableEq n] (hA‚ÇÅ : A.val.PosSemidef) (hB‚ÇÅ : B.val.PosSemidef) :
  RCLike.re (A.val * B.val).trace = 0 ‚Üî
    LinearMap.range (Matrix.toEuclideanLin A.val) ‚â§
      LinearMap.ker (Matrix.toEuclideanLin B.val) := by
  --Thanks Aristotle
  have h_trace_zero : (RCLike.re ((A.val * B.val).trace)) = 0 ‚Üî (A.val * B.val) = 0 := by
    -- Since $A$ and $B$ are positive semidefinite, we can write them as $A = C^* C$ and $B = D^* D$ for some matrices $C$ and $D$.
    obtain ‚ü®C, hC‚ü© : ‚àÉ C : Matrix n n ùïú, A.val = C.conjTranspose * C := by
      exact Matrix.posSemidef_iff_eq_conjTranspose_mul_self.mp hA‚ÇÅ
    obtain ‚ü®D, hD‚ü© : ‚àÉ D : Matrix n n ùïú, B.val = D.conjTranspose * D := by
      exact Matrix.posSemidef_iff_eq_conjTranspose_mul_self.mp hB‚ÇÅ
    have h_trace_zero_iff : (RCLike.re ((A.val * B.val).trace)) = 0 ‚Üî (D * C.conjTranspose) = 0 := by
      -- Since $\operatorname{Tr}((DC)^* DC) = \sum_{i,j} |(DC)_{ij}|^2$, and this sum is zero if and only if each term is zero, we have $\operatorname{Tr}((DC)^* DC) = 0$ if and only if $DC = 0$.
      have h_trace_zero_iff : (RCLike.re ((D * C.conjTranspose).conjTranspose * (D * C.conjTranspose)).trace) = 0 ‚Üî (D * C.conjTranspose) = 0 := by
        have h_trace_zero_iff : ‚àÄ (M : Matrix n n ùïú), (RCLike.re (M.conjTranspose * M).trace) = 0 ‚Üî M = 0 := by
          simp [ Matrix.trace, Matrix.mul_apply ];
          intro M
          simp_all only
          obtain ‚ü®val, property‚ü© := A
          obtain ‚ü®val_1, property_1‚ü© := B
          subst hD hC
          apply Iff.intro
          ¬∑ intro a
            rw [ Finset.sum_eq_zero_iff_of_nonneg fun i _ => Finset.sum_nonneg fun j _ => add_nonneg ( mul_self_nonneg _ ) ( mul_self_nonneg _ )] at a
            ext i j
            specialize a j
            rw [ Finset.sum_eq_zero_iff_of_nonneg fun _ _ => add_nonneg ( mul_self_nonneg _ ) ( mul_self_nonneg _ ) ] at a
            simp_all only [Finset.mem_univ, forall_const, Matrix.zero_apply]
            exact RCLike.ext ( by norm_num; nlinarith only [ a i ] ) ( by norm_num; nlinarith only [ a i ] );
          ¬∑ intro a
            subst a
            simp_all only [Matrix.zero_apply, map_zero, mul_zero, add_zero, Finset.sum_const_zero]
        exact h_trace_zero_iff _;
      convert h_trace_zero_iff using 3 ; simp [ hC, hD, Matrix.mul_assoc ];
      rw [ ‚Üê Matrix.trace_mul_comm ] ; simp [ Matrix.mul_assoc ];
    simp_all only
    obtain ‚ü®val, property‚ü© := A
    obtain ‚ü®val_1, property_1‚ü© := B
    subst hD hC
    apply Iff.intro
    ¬∑ intro a
      simp_all only [iff_true]
      simp ( config := { decide := Bool.true } ) [ ‚Üê Matrix.mul_assoc, ‚Üê Matrix.conjTranspose_inj, a ];
    ¬∑ intro a
      simp_all only [Matrix.trace_zero, map_zero, true_iff]
  have h_range_ker : (LinearMap.range (Matrix.toEuclideanLin A.val)) ‚â§ (LinearMap.ker (Matrix.toEuclideanLin B.val)) ‚Üí (A.val * B.val) = 0 := by
    intro h_range_ker
    have hAB_zero : ‚àÄ v, (Matrix.toEuclideanLin B.val) ((Matrix.toEuclideanLin A.val) v) = 0 := by
      exact fun v => h_range_ker ( LinearMap.mem_range_self _ v )
    have h_herm : A.val * B.val = (B.val * A.val).conjTranspose := by
      simp [Matrix.conjTranspose_mul]
    have hBA_zero : (B.val * A.val) = 0 := by
      ext i j
      specialize hAB_zero (Pi.single j 1)
      convert congr_fun hAB_zero i using 1
      simp [Matrix.toEuclideanLin, dotProduct, Matrix.mulVec, Matrix.mul_apply, Pi.single_apply]
    rw [h_herm, hBA_zero, Matrix.conjTranspose_zero]
  simp_all only
  obtain ‚ü®val, property‚ü© := A
  obtain ‚ü®val_1, property_1‚ü© := B
  simp_all only
  apply Iff.intro
  ¬∑ rintro a _ ‚ü®y, rfl‚ü©
    have h_comm : val_1 * val = 0 := by
      rw [‚Üê Matrix.conjTranspose_inj]
      have h_conj_transpose : val.conjTranspose = val ‚àß val_1.conjTranspose = val_1 := by
        aesop
      simp [h_conj_transpose, Matrix.conjTranspose_mul, a]
    simp only [LinearMap.mem_ker]
    convert congr_arg (fun x => Matrix.mulVec x y) h_comm using 1
    ¬∑ simp [Matrix.toEuclideanLin_apply, Matrix.mulVec_mulVec]
      rfl
    ¬∑ simp
  ¬∑ grind

/-- The inner product of two PSD matrices is zero iff they have disjoint support, i.e., each lives entirely
in the other's kernel. -/
theorem inner_zero_iff [DecidableEq n] (hA‚ÇÅ : 0 ‚â§ A) (hB‚ÇÅ : 0 ‚â§ B)
    : A.inner B = 0 ‚Üî A.support ‚â§ B.ker := by
  rw [zero_le_iff] at hA‚ÇÅ hB‚ÇÅ
  dsimp [support, ker, lin]
  rw [inner_eq_re_trace]
  change selfAdjoint (Matrix n n ùïú) at A B
  exact inner_zero_iff_aux_lemma hA‚ÇÅ hB‚ÇÅ

variable {d d‚ÇÇ : Type*} (A B : HermitianMat d ùïú) [Fintype d‚ÇÇ] [Fintype d]

@[simp]
theorem reindex_inner (e : d ‚âÉ d‚ÇÇ) (B : HermitianMat d‚ÇÇ ùïú) :
    (A.reindex e).inner B = A.inner (B.reindex e.symm) := by
  dsimp [inner]
  congr
  rw (occs := [3,4]) [‚Üê e.symm_symm]
  rw [‚Üê Matrix.submatrix_id_mul_right]
  rw (occs := [2]) [Matrix.trace_mul_comm]
  rw [Matrix.submatrix_id_mul_right, Matrix.trace_mul_comm, Equiv.symm_symm]

end RCLike

section topology
/-!
Theorems about `HermitianMat`s that have to do with the topological structure. Pretty much everything here will
assume these are matrices over ‚ÑÇ, but changes to upgrade this to other types are welcome.
-/
open ComplexOrder

variable {d : Type*} [Fintype d] {ùïú : Type*} [RCLike ùïú]

--Using `#guard_msgs(drop info) in #synth` to check that certain instances already exist here

#guard_msgs(drop info) in
#synth ContinuousAdd (HermitianMat d ‚ÑÇ)

instance : ContinuousSMul ‚Ñù (HermitianMat d ùïú) where
  continuous_smul := by
    rw [continuous_induced_rng]
    exact continuous_smul.comp <| continuous_fst.prodMk (by fun_prop)

#guard_msgs(drop info) in
#synth ContractibleSpace (HermitianMat d ‚ÑÇ)

@[fun_prop] --fun_prop can actually prove this, should I leave this on or not?
theorem inner_bilinForm_Continuous (A : HermitianMat d ùïú) : Continuous ‚áë(HermitianMat.inner_BilinForm A) :=
  LinearMap.continuous_of_finiteDimensional _

@[fun_prop]
theorem inner_continuous : Continuous ((HermitianMat.inner (n := d) (Œ± := ùïú)).uncurry) := by
  rw [funext‚ÇÇ inner_eq_re_trace]
  fun_prop

end topology

section innerproductspace

variable {d d‚ÇÇ : Type*} [Fintype d] [Fintype d‚ÇÇ] {ùïú : Type*} [RCLike ùïú]

/-- We define the Hermitian inner product as our "canonical" inner product, which does induce a norm.
This disagrees slightly with Mathlib convention on the `Matrix` type, which avoids asserting one norm
as there are several reasonable ones; for Hermitian matrices, though, this seem to be the right choice. -/
noncomputable def InnerProductCore : InnerProductSpace.Core ‚Ñù (HermitianMat d ùïú) :=
   {
    inner A B := A.inner B
    conj_inner_symm := fun x y ‚Ü¶ by
      simpa using inner_comm y x
    re_inner_nonneg := inner_self_nonneg
    add_left := by simp [inner, add_mul]
    smul_left x y r := by simp
    definite x h := by
      replace h : ‚àë j, ‚àë i, (RCLike.re (x i j) ^ 2 + RCLike.im (x i j) ^ 2) = 0 := by
        convert h
        simp only [inner_eq_re_trace, Matrix.trace, Matrix.diag_apply, Matrix.mul_apply, map_sum,
          RCLike.mul_re, sub_eq_add_neg]
        congr! 2 with i _ j
        simp [‚Üê congrFun‚ÇÇ x.H i j, pow_two]
        rfl
      ext i j
      rw [Fintype.sum_eq_zero_iff_of_nonneg (fun i ‚Ü¶ by positivity)] at h
      replace h := congrFun h j
      rw [Pi.zero_apply, Fintype.sum_eq_zero_iff_of_nonneg (fun i ‚Ü¶ by positivity)] at h
      replace h := congrFun h i
      rw [Pi.zero_apply] at h
      rw [add_eq_zero_iff_of_nonneg (by positivity) (by positivity), sq_eq_zero_iff, sq_eq_zero_iff] at h
      apply RCLike.ext (h.left.trans RCLike.zero_re.symm) (h.right.trans (map_zero _).symm)
  }

open Matrix.Norms.Frobenius in
/-- The `HermitianMat` type inherits the Frobenius necessarily, since it's going to need the
Hermitian inner product, and in Mathlib an `InnerProductSpace` always carries the corresponding
norm. -/
noncomputable instance instNormedGroup : NormedAddCommGroup (HermitianMat d ùïú) :=
  AddSubgroupClass.normedAddCommGroup _

theorem norm_eq_frobenius (A : HermitianMat d ùïú) :
    ‚ÄñA‚Äñ = (‚àë i : d, ‚àë j : d, ‚ÄñA i j‚Äñ ^ 2) ^ (1 / 2 : ‚Ñù) := by
  convert ‚Üê Matrix.frobenius_norm_def A.toMat
  exact Real.rpow_ofNat _ 2

--PULLOUT
omit [Fintype d] in
@[simp]
theorem toMat_apply {A : HermitianMat d ùïú} {i j : d} : A.toMat i j = A i j := by
  rfl

theorem norm_eq_sqrt_inner_self (A : HermitianMat d ùïú) : ‚ÄñA‚Äñ = ‚àö(A.inner A) := by
  rw [norm_eq_frobenius, ‚Üê Real.sqrt_eq_rpow]
  congr
  simp_rw [inner_eq_re_trace, Matrix.trace, Matrix.diag, Matrix.mul_apply]
  simp only [map_sum]
  congr! with i _ j _
  simp only [RCLike.norm_sq_eq_def, RCLike.mul_re, sub_eq_add_neg,
    neg_mul_eq_mul_neg]
  congr 2 <;> (rw [‚Üê A.H]; simp)

noncomputable instance instNormedSpace : NormedSpace ‚Ñù (HermitianMat d ùïú) where
  norm_smul_le r x := by
    rw [norm_eq_sqrt_inner_self, norm_eq_sqrt_inner_self]
    simp only [inner_smul, smul_inner, ‚Üê mul_assoc, Real.norm_eq_abs]
    rw [Real.sqrt_mul' _ (inner_self_nonneg x), Real.sqrt_mul_self_eq_abs]

noncomputable instance instInnerProductSpace : InnerProductSpace ‚Ñù (HermitianMat d ùïú) :=
   letI : Inner ‚Ñù (HermitianMat d ùïú) := InnerProductCore.toInner;
   letI : NormedSpace ‚Ñù (HermitianMat d ùïú) := instNormedSpace;
  { InnerProductCore with
    norm_sq_eq_re_inner := fun x => by
      rw [norm_eq_sqrt_inner_self, Real.sq_sqrt (inner_self_nonneg x), RCLike.re_to_real]
      rfl
  }

theorem inner_eq (A B : HermitianMat d ùïú) : Inner.inner ‚Ñù A B = A.inner B := by
  rfl

open scoped RealInnerProductSpace

instance : CompleteSpace (HermitianMat d ùïú) :=
  inferInstance

--Shortcut instances
noncomputable instance : NormedAddCommGroup (HermitianMat d ‚Ñù) :=
  inferInstance

noncomputable instance : NormedAddCommGroup (HermitianMat d ‚ÑÇ) :=
  inferInstance

--TODO: PULLOUT, PR
open ComplexOrder in
def _root_.RCLike.instOrderClosed : OrderClosedTopology ùïú where
  isClosed_le' := by
    conv => enter [1, 1, p]; rw [RCLike.le_iff_re_im]
    simp_rw [Set.setOf_and]
    refine IsClosed.inter (isClosed_le ?_ ?_) (isClosed_eq ?_ ?_) <;> continuity

scoped[ComplexOrder] attribute [instance] RCLike.instOrderClosed

variable (A B : HermitianMat d ùïú)

--TODO: Eventually deprecated HermitianMat.inner and switch to this primed version everywhere.
/-- The inner product for PSD matrices is nonnegative. -/
theorem inner_ge_zero' (hA : 0 ‚â§ A) (hB : 0 ‚â§ B) : 0 ‚â§ ‚ü™A, B‚ü´ :=
  inner_ge_zero hA hB

variable {A B} in
theorem dist_le_of_mem_Icc (x : HermitianMat d ùïú) (hA : A ‚â§ x) (hB : x ‚â§ B) :
    ‚Äñx - A‚Äñ ‚â§ ‚ÄñB - A‚Äñ := by
  classical
  conv => enter [2, 1]; equals (B - x) + (x - A) => abel
  rw [‚Üê sq_le_sq‚ÇÄ (norm_nonneg _) (norm_nonneg _)]
  rw [norm_add_pow_two_real, le_add_iff_nonneg_left]
  suffices 0 ‚â§ ‚ü™B - x, x - A‚ü´ by positivity
  apply inner_ge_zero' <;> rwa [sub_nonneg]

omit [Fintype n] in
theorem Matrix.IsHermitian_isClosed : IsClosed { A : Matrix n n ùïú | A.IsHermitian } := by
  conv =>
    enter [1, 1, A]
    rw [Matrix.IsHermitian, ‚Üê sub_eq_zero]
  convert isClosed_singleton.preimage (f := fun (x : Matrix n n ùïú) ‚Ü¶ (x.conjTranspose - x))
    (by fun_prop) using 1

open ComplexOrder

theorem Matrix.PosSemiDef_isClosed : IsClosed { A : Matrix n n ùïú | A.PosSemidef } := by
  refine IsHermitian_isClosed.inter ?_
  suffices IsClosed (‚ãÇ x : n ‚Üí ùïú, { A : Matrix n n ùïú | 0 ‚â§ star x ‚¨ù·µ• A.mulVec x }) by
    rwa [‚Üê Set.setOf_forall] at this
  exact isClosed_iInter fun _ ‚Ü¶ (isClosed_Ici (a := 0)).preimage (by fun_prop)

theorem isClosed_nonneg : IsClosed { A : HermitianMat n ùïú | 0 ‚â§ A } := by
  simp_rw [zero_le_iff]
  exact Matrix.PosSemiDef_isClosed.preimage_val

--TODO: The PosDef matrices are open *within* the HermitianMat space (not in the ambient space of matrices.)

instance : OrderClosedTopology (HermitianMat d ùïú) where
  isClosed_le' := by
    classical
    convert IsClosed.preimage (X := (HermitianMat d ùïú √ó HermitianMat d ùïú))
      (f := fun xy ‚Ü¶ (xy.2 - xy.1)) (by fun_prop) isClosed_nonneg
    ext ‚ü®x, y‚ü©
    simp only [Set.mem_setOf_eq, Set.mem_preimage, ‚Üê sub_nonneg (b := x)]

/-- Equivalently: the matrices `X` such that `X - A` is PSD and `B - X` is PSD, form a compact set. -/
instance : CompactIccSpace (HermitianMat d ùïú) where
  isCompact_Icc := by
    intros A B
    apply Metric.isCompact_of_isClosed_isBounded isClosed_Icc
    rw [Metric.isBounded_iff]
    use 2 * ‚ÄñB - A‚Äñ
    rintro x ‚ü®hxA, hxB‚ü© y ‚ü®hyA, hyB‚ü©
    grw [dist_triangle_right (z := A), dist_eq_norm, dist_eq_norm]
    grw [dist_le_of_mem_Icc x hxA hxB, dist_le_of_mem_Icc y hyA hyB]
    rw [two_mul]

/-- The PSD matrices that are `‚â§ 1` are a compact set. More generally, this is true of any closed interval,
but stating that is a bit different because of how numerals are treated. The `0` and `1` here are already
directly matrices, putting in an `(a : ‚Ñù) ‚Ä¢ 1 ‚â§ m ‚àß m ‚â§ (b : ‚Ñù) ‚Ä¢ 1` involves casts. But that theorem should follow
easily from this. More generally `A ‚â§ m ‚àß m ‚â§ B` is compact.
-/
theorem unitInterval_IsCompact [DecidableEq d] :
    IsCompact {m : HermitianMat d ùïú | 0 ‚â§ m ‚àß m ‚â§ 1} :=
  CompactIccSpace.isCompact_Icc

end innerproductspace
