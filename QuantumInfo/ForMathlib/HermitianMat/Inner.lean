/-
Copyright (c) 2025 Alex Meiburg. All rights reserved.
Released under MIT license as described in the file LICENSE.
Authors: Alex Meiburg
-/
import QuantumInfo.ForMathlib.HermitianMat.Order
import Mathlib.Analysis.Convex.Contractible
import Mathlib.Topology.Instances.Real.Lemmas

/-! # Inner product of Hermitian Matrices

For general matrices there are multiple reasonable notions of "inner product" (Hilbertâ€“Schmidt inner product,
Frobenius inner product), and so Mathlib avoids giving a canonical `InnerProductSpace` instance. But for the
particular case of Hermitian matrices, these all coincide, so we can put a canonical `InnerProductSpace`
instance.

This _does_ however induce a `Norm` on `HermitianMat` as well, the Frobenius norm, and this is less obviously
a uniquely correct choice. It is something that one essentially has to live with, with the way that Mathlib
currently structures the instances. (Thankfully, all norms induce the same _topology and bornology_ on
finite-dimensional matrices.)

Some care to be taken so that the topology induced by the InnerProductSpace is defeq with the Subtype
topology that HermitianMat inherits from the topology on Matrix. This can be done via
`InnerProductSpace.ofCoreOfTopology`.

-/

namespace HermitianMat

variable {R n Î± : Type*} [Star R] [TrivialStar R] [Fintype n]
open scoped InnerProductSpace RealInnerProductSpace
open IsMaximalSelfAdjoint

section defs

variable [Ring Î±] [StarAddMonoid Î±] [CommSemiring R] [Algebra R Î±] [IsMaximalSelfAdjoint R Î±]

/-- The Hermitian inner product, `Tr[AB]`. This is equal to `Matrix.trace (A * B)`, but gives real
  values when the matrices are complex, using `IsMaximalSelfAdjoint`. -/
instance : Inner R (HermitianMat n Î±) where
  inner A B := selfadjMap (A.mat * B.mat).trace

theorem inner_def (A B : HermitianMat n Î±) :
    âŸªA, BâŸ«_R = selfadjMap (A.mat * B.mat).trace := by
  rfl

end defs
section semiring

--We necessarily re-state and re-prove many of the theorems from InnerProductSpace/Basic.lean,
--because our inner product happens outside of just an `InnerProductSpace` instance.

variable [CommSemiring R] [Ring Î±] [StarAddMonoid Î±] [Algebra R Î±] [IsMaximalSelfAdjoint R Î±]
variable (A B C : HermitianMat n Î±)

protected theorem inner_add_right : âŸªA, B + CâŸ«_R = âŸªA, BâŸ«_R + âŸªA, CâŸ«_R := by
  simp [inner_def, left_distrib]

protected theorem inner_add_left : âŸªA + B, CâŸ«_R = âŸªA, CâŸ«_R + âŸªB, CâŸ«_R := by
  simp [inner_def, right_distrib]

@[simp]
protected theorem inner_zero_right : âŸªA, 0âŸ«_R = 0 := by
  simp [inner_def]

@[simp]
protected theorem inner_zero_left : âŸª0, AâŸ«_R = 0 := by
  simp [inner_def]

end semiring
section ring

variable [CommRing R] [Ring Î±] [StarAddMonoid Î±] [Algebra R Î±] [IsMaximalSelfAdjoint R Î±]
variable (A B C : HermitianMat n Î±)

@[simp]
protected theorem inner_neg_left : âŸª-A, BâŸ«_R = -âŸªA, BâŸ«_R := by
  simp [inner_def]

@[simp]
protected theorem inner_neg_right : âŸªA, -BâŸ«_R = -âŸªA, BâŸ«_R := by
  simp [inner_def]

protected theorem inner_sub_left : âŸªA, B - CâŸ«_R = âŸªA, BâŸ«_R - âŸªA, CâŸ«_R := by
  simp [inner_def, mul_sub]

protected theorem inner_sub_right : âŸªA - B, CâŸ«_R = âŸªA, CâŸ«_R - âŸªB, CâŸ«_R := by
  simp [inner_def, sub_mul]

variable [StarModule R Î±]

@[simp]
protected theorem inner_smul_left (r : R) : âŸªr â€¢ A, BâŸ«_R = r * âŸªA, BâŸ«_R := by
  simp [inner_def, selfadj_smul]

@[simp]
protected theorem inner_smul_right (r : R) : âŸªA, r â€¢ BâŸ«_R = r * âŸªA, BâŸ«_R := by
  simp [inner_def, selfadj_smul]

/-- The Hermitian inner product as bilinear form. Compare with `innerâ‚—` (in the root namespace)
which requires an `InnerProductSpace` instance. -/
protected def innerâ‚— : LinearMap.BilinForm R (HermitianMat n Î±) where
  toFun A := {
    toFun := (âŸªA, Â·âŸ«_R)
    map_add' := A.inner_add_right
    map_smul' r B := by simp
  }
  map_add' A B := by ext1; apply A.inner_add_left B
  map_smul' A B := by ext1; simp

end ring
section starring

variable [CommSemiring R] [Ring Î±] [StarRing Î±] [Algebra R Î±] [IsMaximalSelfAdjoint R Î±] [DecidableEq n]
variable (A B : HermitianMat n Î±)

@[simp]
theorem inner_one : âŸªA, 1âŸ«_R = A.trace := by
  simp only [inner_def, mat_one,  mul_one, trace]

@[simp]
theorem one_inner : âŸª1, AâŸ«_R = A.trace := by
  simp only [inner_def, one_mul, mat_one, trace]

end starring
section commring

variable [CommSemiring R] [CommRing Î±] [StarRing Î±] [Algebra R Î±] [IsMaximalSelfAdjoint R Î±]
variable (A B : HermitianMat n Î±)

/-- The inner product for Hermtian matrices is equal to the trace of the product. -/
theorem inner_eq_trace_mul : algebraMap R Î± âŸªA, BâŸ«_R = (A.mat * B.mat).trace := by
  apply IsMaximalSelfAdjoint.selfadj_algebra
  rw [IsSelfAdjoint, Matrix.trace]
  simp_rw [star_sum, Matrix.diag_apply, Matrix.mul_apply, star_sum, star_mul, mul_comm]
  rw [Finset.sum_comm]
  congr! <;> apply congrFunâ‚‚ (H _)

theorem inner_comm : âŸªA, BâŸ«_R = âŸªB, AâŸ«_R := by
  rw [inner_def, inner_def, Matrix.trace_mul_comm]

end commring

section trivialstar
variable [CommRing Î±] [StarRing Î±] [TrivialStar Î±]
variable (A B : HermitianMat n Î±)

/-- `HermitianMat.inner` reduces to `Matrix.trace (A * B)` when the elements are a `TrivialStar`. -/
theorem inner_eq_trace_trivial : âŸªA, BâŸ«_Î± = (A.mat * B.mat).trace := by
  rw [â† inner_eq_trace_mul]
  rfl

end trivialstar

section RCLike

open ComplexOrder

variable {n ğ•œ : Type*} [Fintype n] [RCLike ğ•œ] (A B C : HermitianMat n ğ•œ)

theorem inner_eq_re_trace : âŸªA, BâŸ« = RCLike.re (A.mat * B.mat).trace := by
  rfl

theorem inner_eq_trace_rc : âŸªA, BâŸ« = (A.mat * B.mat).trace := by
  rw [inner_eq_re_trace, â† RCLike.conj_eq_iff_re]
  convert (Matrix.trace_conjTranspose (A.mat * B.mat)).symm using 1
  rw [Matrix.conjTranspose_mul, A.H, B.H, Matrix.trace_mul_comm]

theorem inner_self_nonneg: 0 â‰¤ âŸªA, AâŸ« := by
  simp_rw [inner_eq_re_trace, Matrix.trace, Matrix.diag, Matrix.mul_apply, map_sum]
  refine Finset.sum_nonneg fun i _ â†¦ Finset.sum_nonneg fun j _ â†¦ ?_
  rw [â† congrFunâ‚‚ A.H, Matrix.conjTranspose_apply]
  refine And.left <| RCLike.nonneg_iff.mp ?_
  open ComplexOrder in
  exact star_mul_self_nonneg (A.mat j i)

variable {A B C}

open MatrixOrder in
theorem inner_mul_nonneg (h : 0 â‰¤ A.mat * B.mat) : 0 â‰¤ âŸªA, BâŸ« := by
  rw [Matrix.nonneg_iff_posSemidef] at h
  exact (RCLike.nonneg_iff.mp h.trace_nonneg).left

/-- The inner product for PSD matrices is nonnegative. -/
theorem inner_ge_zero (hA : 0 â‰¤ A) (hB : 0 â‰¤ B) : 0 â‰¤ âŸªA, BâŸ« := by
  rw [zero_le_iff] at hB
  open MatrixOrder in
  open Classical in
  rw [inner_eq_re_trace, â† CFC.sqrt_mul_sqrt_self A.mat hA, Matrix.trace_mul_cycle, Matrix.trace_mul_cycle]
  nth_rewrite 1 [â† (Matrix.nonneg_iff_posSemidef.mp (CFC.sqrt_nonneg A.mat)).left]
  exact (RCLike.nonneg_iff.mp (hB.conjTranspose_mul_mul_same _).trace_nonneg).left

theorem inner_mono (hA : 0 â‰¤ A) : B â‰¤ C â†’ âŸªA, BâŸ« â‰¤ âŸªA, CâŸ« := by
  intro hBC
  classical have hTr : 0 â‰¤ âŸªA, C - BâŸ« := inner_ge_zero hA (zero_le_iff.mpr hBC)
  simpa [inner_def, mul_sub] using hTr

theorem inner_mono' (hA : 0 â‰¤ A) : B â‰¤ C â†’ âŸªB, AâŸ« â‰¤ âŸªC, AâŸ« := by
  intro hBC
  rw [inner_comm B A, inner_comm C A]
  exact inner_mono hA hBC

/-- The inner product for PSD matrices is at most the product of their traces. -/
theorem inner_le_mul_trace (hA : 0 â‰¤ A) (hB : 0 â‰¤ B) : âŸªA, BâŸ« â‰¤ A.trace * B.trace := by
  classical convert inner_mono hA (le_trace_smul_one hB)
  simp [mul_comm]

--TODO cleanup
private theorem inner_zero_iff_aux_lemma [DecidableEq n] (hAâ‚ : A.mat.PosSemidef) (hBâ‚ : B.mat.PosSemidef) :
  RCLike.re (A.val * B.val).trace = 0 â†”
    LinearMap.range (Matrix.toEuclideanLin A.val) â‰¤
      LinearMap.ker (Matrix.toEuclideanLin B.val) := by
  open MatrixOrder in
  --Thanks Aristotle
  have h_trace_zero : (RCLike.re ((A.val * B.val).trace)) = 0 â†” (A.val * B.val) = 0 := by
    -- Since $A$ and $B$ are positive semidefinite, we can write them as $A = C^* C$ and $B = D^* D$ for some matrices $C$ and $D$.
    obtain âŸ¨C, hCâŸ© : âˆƒ C : Matrix n n ğ•œ, A.val = C.conjTranspose * C := by
      rw [â† Matrix.nonneg_iff_posSemidef] at hAâ‚
      exact CStarAlgebra.nonneg_iff_eq_star_mul_self.mp hAâ‚
    obtain âŸ¨D, hDâŸ© : âˆƒ D : Matrix n n ğ•œ, B.val = D.conjTranspose * D := by
      erw [â† Matrix.nonneg_iff_posSemidef] at hBâ‚
      exact CStarAlgebra.nonneg_iff_eq_star_mul_self.mp hBâ‚
    have h_trace_zero_iff : (RCLike.re ((A.val * B.val).trace)) = 0 â†” (D * C.conjTranspose) = 0 := by
      -- Since $\operatorname{Tr}((DC)^* DC) = \sum_{i,j} |(DC)_{ij}|^2$, and this sum is zero if and only if each term is zero, we have $\operatorname{Tr}((DC)^* DC) = 0$ if and only if $DC = 0$.
      have h_trace_zero_iff : (RCLike.re ((D * C.conjTranspose).conjTranspose * (D * C.conjTranspose)).trace) = 0 â†” (D * C.conjTranspose) = 0 := by
        have h_trace_zero_iff : âˆ€ (M : Matrix n n ğ•œ), (RCLike.re (M.conjTranspose * M).trace) = 0 â†” M = 0 := by
          simp [ Matrix.trace, Matrix.mul_apply ];
          intro M
          -- simp_all only
          obtain âŸ¨val, propertyâŸ© := A
          obtain âŸ¨val_1, property_1âŸ© := B
          subst hD hC
          apply Iff.intro
          Â· intro a
            rw [ Finset.sum_eq_zero_iff_of_nonneg fun i _ => Finset.sum_nonneg fun j _ => add_nonneg ( mul_self_nonneg _ ) ( mul_self_nonneg _ )] at a
            ext i j
            specialize a j
            rw [ Finset.sum_eq_zero_iff_of_nonneg fun _ _ => add_nonneg ( mul_self_nonneg _ ) ( mul_self_nonneg _ ) ] at a
            simp_all only [Finset.mem_univ, forall_const, Matrix.zero_apply]
            exact RCLike.ext ( by norm_num; nlinarith only [ a i ] ) ( by norm_num; nlinarith only [ a i ] );
          Â· intro a
            subst a
            simp_all only [Matrix.zero_apply, map_zero, mul_zero, add_zero, Finset.sum_const_zero]
        exact h_trace_zero_iff _;
      convert h_trace_zero_iff using 3
      simp [ hC, hD, Matrix.mul_assoc ];
      rw [ â† Matrix.trace_mul_comm ]
      have h_trace_cyclic : Matrix.trace (D.conjTranspose * D * C.conjTranspose * C) = Matrix.trace (C * D.conjTranspose * D * C.conjTranspose) := by
        rw [ â† Matrix.trace_mul_comm ]
        simp [ Matrix.mul_assoc ] ;
      simp_all [ Matrix.mul_assoc ]
    simp_all only
    obtain âŸ¨val, propertyâŸ© := A
    obtain âŸ¨val_1, property_1âŸ© := B
    subst hD hC
    apply Iff.intro
    Â· intro a
      simp_all only [iff_true]
      simp [ â† Matrix.mul_assoc, â† Matrix.conjTranspose_inj, a ];
    Â· intro a
      simp_all only [Matrix.trace_zero, map_zero, true_iff]
  have h_range_ker : (LinearMap.range (Matrix.toEuclideanLin A.val)) â‰¤ (LinearMap.ker (Matrix.toEuclideanLin B.val)) â†’ (A.val * B.val) = 0 := by
    intro h_range_ker
    have hAB_zero : âˆ€ v, (Matrix.toEuclideanLin B.val) ((Matrix.toEuclideanLin A.val) v) = 0 := by
      exact fun v => h_range_ker ( LinearMap.mem_range_self _ v )
    have h_herm : A.val * B.val = (B.val * A.val).conjTranspose := by
      simp [Matrix.conjTranspose_mul]
    have hBA_zero : (B.val * A.val) = 0 := by
      ext i j
      specialize hAB_zero (Pi.single j 1)
      convert congr_fun hAB_zero i using 1
      simp [Matrix.toEuclideanLin, dotProduct, Matrix.mulVec, Matrix.mul_apply, Pi.single_apply]
    rw [h_herm, hBA_zero, Matrix.conjTranspose_zero]
  simp_all only
  obtain âŸ¨val, propertyâŸ© := A
  obtain âŸ¨val_1, property_1âŸ© := B
  simp_all only
  apply Iff.intro
  Â· rintro a _ âŸ¨y, rflâŸ©
    have h_comm : val_1 * val = 0 := by
      rw [â† Matrix.conjTranspose_inj]
      have h_conj_transpose : val.conjTranspose = val âˆ§ val_1.conjTranspose = val_1 := by
        aesop
      simp [h_conj_transpose, Matrix.conjTranspose_mul, a]
    simp only [LinearMap.mem_ker]
    convert congr_arg (fun x => Matrix.mulVec x y) h_comm using 1
    Â· simp [Matrix.toEuclideanLin_apply, Matrix.mulVec_mulVec]
      rfl
    Â· simp
  Â· grind

/-- The inner product of two PSD matrices is zero iff they have disjoint support, i.e., each lives entirely
in the other's kernel. -/
theorem inner_zero_iff [DecidableEq n] (hAâ‚ : 0 â‰¤ A) (hBâ‚ : 0 â‰¤ B)
    : âŸªA, BâŸ« = 0 â†” A.support â‰¤ B.ker := by
  rw [zero_le_iff] at hAâ‚ hBâ‚
  rw [inner_eq_re_trace]
  exact inner_zero_iff_aux_lemma hAâ‚ hBâ‚

variable {d dâ‚‚ : Type*} (A B : HermitianMat d ğ•œ) [Fintype dâ‚‚] [Fintype d]

@[simp]
theorem reindex_inner (e : d â‰ƒ dâ‚‚) (B : HermitianMat dâ‚‚ ğ•œ) :
    âŸªA.reindex e, BâŸ« = âŸªA, B.reindex e.symmâŸ« := by
  simp only [inner_def, RCLike_selfadjMap, mat_reindex, Matrix.reindex_apply, Equiv.symm_symm]
  congr
  rw (occs := [3,4]) [â† e.symm_symm]
  rw [â† Matrix.submatrix_id_mul_right]
  rw (occs := [2]) [Matrix.trace_mul_comm]
  rw [Matrix.submatrix_id_mul_right, Matrix.trace_mul_comm, Equiv.symm_symm]

end RCLike

section topology
/-!
Theorems about `HermitianMat`s that have to do with the topological structure. Pretty much everything here will
assume these are matrices over â„‚, but changes to upgrade this to other types are welcome.
-/
open ComplexOrder

variable {d : Type*} [Fintype d] {ğ•œ : Type*} [RCLike ğ•œ]

--Check that it synthesizes ok
#guard_msgs(drop info) in
#synth ContractibleSpace (HermitianMat d â„‚)

@[fun_prop]
theorem inner_continuous : Continuous (Inner.inner â„ (E := HermitianMat d ğ•œ)) := by
  rw [funextâ‚‚ inner_eq_re_trace]
  fun_prop

@[fun_prop] --fun_prop can actually prove this, should I leave this on or not?
theorem inner_bilinForm_Continuous (A : HermitianMat d ğ•œ) : Continuous â‡‘(HermitianMat.innerâ‚— A) :=
  LinearMap.continuous_of_finiteDimensional _

end topology

section innerproductspace

variable {d dâ‚‚ : Type*} [Fintype d] [Fintype dâ‚‚] {ğ•œ : Type*} [RCLike ğ•œ]

/-- We define the Hermitian inner product as our "canonical" inner product, which does induce a norm.
This disagrees slightly with Mathlib convention on the `Matrix` type, which avoids asserting one norm
as there are several reasonable ones; for Hermitian matrices, though, this seem to be the right choice. -/
noncomputable def InnerProductCore : InnerProductSpace.Core â„ (HermitianMat d ğ•œ) :=
   {
    inner A B := âŸªA, BâŸ«
    conj_inner_symm := fun x y â†¦ by
      simpa using inner_comm y x
    re_inner_nonneg := inner_self_nonneg
    add_left := by simp [inner, add_mul]
    smul_left x y r := by simp
    definite x h := by
      replace h : âˆ‘ j, âˆ‘ i, (RCLike.re (x i j) ^ 2 + RCLike.im (x i j) ^ 2) = 0 := by
        convert h
        simp only [inner_eq_re_trace, Matrix.trace, Matrix.diag_apply, Matrix.mul_apply, map_sum,
          RCLike.mul_re, sub_eq_add_neg]
        congr! 2 with i _ j
        simp only [Matrix.conjTranspose_apply, â† congrFunâ‚‚ x.H i j]
        simp [pow_two]
      ext i j
      rw [Fintype.sum_eq_zero_iff_of_nonneg (fun i â†¦ by positivity)] at h
      replace h := congrFun h j
      rw [Pi.zero_apply, Fintype.sum_eq_zero_iff_of_nonneg (fun i â†¦ by positivity)] at h
      replace h := congrFun h i
      rw [Pi.zero_apply] at h
      rw [add_eq_zero_iff_of_nonneg (by positivity) (by positivity), sq_eq_zero_iff, sq_eq_zero_iff] at h
      apply RCLike.ext (h.left.trans RCLike.zero_re.symm) (h.right.trans (map_zero _).symm)
  }

open Matrix.Norms.Frobenius in
/-- The `HermitianMat` type inherits the Frobenius necessarily, since it's going to need the
Hermitian inner product, and in Mathlib an `InnerProductSpace` always carries the corresponding
norm. -/
noncomputable instance instNormedGroup : NormedAddCommGroup (HermitianMat d ğ•œ) :=
  AddSubgroupClass.normedAddCommGroup _

theorem norm_eq_frobenius (A : HermitianMat d ğ•œ) :
    â€–Aâ€– = (âˆ‘ i : d, âˆ‘ j : d, â€–A i jâ€– ^ 2) ^ (1 / 2 : â„) := by
  convert â† Matrix.frobenius_norm_def A.mat
  exact Real.rpow_ofNat _ 2

theorem norm_eq_sqrt_inner_self (A : HermitianMat d ğ•œ) : â€–Aâ€– = âˆš(âŸªA, AâŸ«) := by
  rw [norm_eq_frobenius, â† Real.sqrt_eq_rpow]
  congr
  simp_rw [inner_eq_re_trace, Matrix.trace, Matrix.diag, Matrix.mul_apply]
  simp only [map_sum]
  congr! with i _ j _
  simp only [RCLike.norm_sq_eq_def, RCLike.mul_re, sub_eq_add_neg,
    neg_mul_eq_mul_neg]
  congr 2 <;> (rw [â† A.H]; simp)

noncomputable instance instNormedSpace : NormedSpace â„ (HermitianMat d ğ•œ) where
  norm_smul_le r x := by
    simp [norm_eq_sqrt_inner_self, â† mul_assoc, Real.sqrt_mul',
      inner_self_nonneg, Real.sqrt_mul_self_eq_abs]

noncomputable instance instInnerProductSpace : InnerProductSpace â„ (HermitianMat d ğ•œ) :=
   letI : Inner â„ (HermitianMat d ğ•œ) := InnerProductCore.toInner;
   letI : NormedSpace â„ (HermitianMat d ğ•œ) := instNormedSpace;
  { InnerProductCore with
    norm_sq_eq_re_inner := fun x => by
      rw [norm_eq_sqrt_inner_self, Real.sq_sqrt (inner_self_nonneg x), RCLike.re_to_real]
  }

instance : CompleteSpace (HermitianMat d ğ•œ) :=
  inferInstance

--Shortcut instances
noncomputable instance : NormedAddCommGroup (HermitianMat d â„) :=
  inferInstance

noncomputable instance : NormedAddCommGroup (HermitianMat d â„‚) :=
  inferInstance

--PR'ed in #35056
open ComplexOrder in
def _root_.RCLike.instOrderClosed : OrderClosedTopology ğ•œ where
  isClosed_le' := by
    conv => enter [1, 1, p]; rw [RCLike.le_iff_re_im]
    simp_rw [Set.setOf_and]
    refine IsClosed.inter (isClosed_le ?_ ?_) (isClosed_eq ?_ ?_) <;> continuity

scoped[ComplexOrder] attribute [instance] RCLike.instOrderClosed

variable (A B : HermitianMat d ğ•œ)

variable {A B} in
theorem dist_le_of_mem_Icc (x : HermitianMat d ğ•œ) (hA : A â‰¤ x) (hB : x â‰¤ B) :
    â€–x - Aâ€– â‰¤ â€–B - Aâ€– := by
  classical
  conv => enter [2, 1]; equals (B - x) + (x - A) => abel
  rw [â† sq_le_sqâ‚€ (norm_nonneg _) (norm_nonneg _)]
  rw [norm_add_pow_two_real, le_add_iff_nonneg_left]
  suffices 0 â‰¤ âŸªB - x, x - AâŸ« by positivity
  apply inner_ge_zero <;> rwa [sub_nonneg]

omit [Fintype n] in
theorem Matrix.IsHermitian_isClosed : IsClosed { A : Matrix n n ğ•œ | A.IsHermitian } := by
  conv =>
    enter [1, 1, A]
    rw [Matrix.IsHermitian, â† sub_eq_zero]
  convert isClosed_singleton.preimage (f := fun (x : Matrix n n ğ•œ) â†¦ (x.conjTranspose - x))
    (by fun_prop) using 1

open ComplexOrder

theorem Matrix.PosSemiDef_isClosed : IsClosed { A : Matrix n n ğ•œ | A.PosSemidef } := by
  refine IsHermitian_isClosed.inter ?_
  suffices IsClosed (â‹‚ x : n â†’ ğ•œ, { A : Matrix n n ğ•œ | 0 â‰¤ star x â¬áµ¥ A.mulVec x }) by
    rwa [â† Set.setOf_forall] at this
  exact isClosed_iInter fun _ â†¦ (isClosed_Ici (a := 0)).preimage (by fun_prop)

theorem isClosed_nonneg : IsClosed { A : HermitianMat n ğ•œ | 0 â‰¤ A } := by
  simp_rw [zero_le_iff]
  exact Matrix.PosSemiDef_isClosed.preimage_val

--TODO: The PosDef matrices are open *within* the HermitianMat space (not in the ambient space of matrices.)

instance : OrderClosedTopology (HermitianMat d ğ•œ) where
  isClosed_le' := by
    classical
    convert IsClosed.preimage (X := (HermitianMat d ğ•œ Ã— HermitianMat d ğ•œ))
      (f := fun xy â†¦ (xy.2 - xy.1)) (by fun_prop) isClosed_nonneg
    ext âŸ¨x, yâŸ©
    simp only [Set.mem_setOf_eq, Set.mem_preimage, â† sub_nonneg (b := x)]

/-- Equivalently: the matrices `X` such that `X - A` is PSD and `B - X` is PSD, form a compact set. -/
instance : CompactIccSpace (HermitianMat d ğ•œ) where
  isCompact_Icc := by
    intros A B
    apply Metric.isCompact_of_isClosed_isBounded isClosed_Icc
    rw [Metric.isBounded_iff]
    use 2 * â€–B - Aâ€–
    rintro x âŸ¨hxA, hxBâŸ© y âŸ¨hyA, hyBâŸ©
    grw [dist_triangle_right (z := A), dist_eq_norm, dist_eq_norm]
    grw [dist_le_of_mem_Icc x hxA hxB, dist_le_of_mem_Icc y hyA hyB]
    rw [two_mul]

variable [DecidableEq d]

/-- The PSD matrices that are `â‰¤ 1` are a compact set. More generally, this is true of any closed interval,
but stating that is a bit different because of how numerals are treated. The `0` and `1` here are already
directly matrices, putting in an `(a : â„) â€¢ 1 â‰¤ m âˆ§ m â‰¤ (b : â„) â€¢ 1` involves casts. But that theorem should follow
easily from this. More generally `A â‰¤ m âˆ§ m â‰¤ B` is compact.
-/
theorem unitInterval_IsCompact : IsCompact {m : HermitianMat d ğ•œ | 0 â‰¤ m âˆ§ m â‰¤ 1} :=
  CompactIccSpace.isCompact_Icc

@[simp]
theorem norm_one : â€–(1 : HermitianMat d ğ•œ)â€– = âˆš(Fintype.card d : â„) := by
  simp [norm_eq_sqrt_real_inner, inner_def]

theorem norm_eq_trace_sq : â€–Aâ€– ^ 2 = (A.mat ^ 2).trace := by
  rw [norm_eq_frobenius, â† RCLike.ofReal_pow, â† Real.rpow_two, â† Real.rpow_mul (by positivity)]
  simp only [one_div, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, inv_mul_cancelâ‚€, Real.rpow_one]
  simp only [sq A.mat, map_sum, map_pow, Matrix.trace, Matrix.diag_apply, Matrix.mul_apply, mat_apply]
  congr! with i _ j _
  rw [â† star_star (A j i), â† A.mat_apply (i := j)]
  rw [â† A.mat.conjTranspose_apply j i, A.H, eq_comm]
  exact RCLike.mul_conj (A.mat i j)

end innerproductspace
