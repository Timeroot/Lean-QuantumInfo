/-
Copyright (c) 2025 Leonardo A Lessa. All rights reserved.
Released under MIT license as described in the file LICENSE.
Authors: Leonardo A Lessa, Alex Meiburg
-/
import QuantumInfo.ForMathlib.HermitianMat.CFC

import Mathlib.Analysis.CStarAlgebra.Classes
import Mathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.PosPart.Basic

/-!
The positive parts, or equivalently the projections, of Hermitian matrices onto each other.
-/

noncomputable section
namespace HermitianMat

variable {n : Type*} [Fintype n] [DecidableEq n]
variable {ğ•œ : Type*} [RCLike ğ•œ]
variable {Î¹ : Type*} [Fintype Î¹] (S : Submodule ğ•œ (EuclideanSpace ğ•œ n))

variable (A B : HermitianMat n ğ•œ)

open scoped InnerProductSpace

/--
Given a Submodule (EuclideanSpace ...) to HermitianMat, this gives the projector onto that subspace,
i.e. a matrix that squares to itself, preserves vectors in the submodule, and zeroes out anything
in the orthogonal complement of that submodule.
-/
noncomputable def projector (S : Submodule ğ•œ (EuclideanSpace ğ•œ n)) : HermitianMat n ğ•œ :=
  let P := S.subtypeL.comp S.orthogonalProjection
  âŸ¨P.toMatrix (EuclideanSpace.basisFun n ğ•œ).toBasis (EuclideanSpace.basisFun n ğ•œ).toBasis, by
    ext i j
    simpa [EuclideanSpace.inner_single_right, EuclideanSpace.inner_single_left] using
      S.inner_starProjection_left_eq_right (EuclideanSpace.single i 1) (EuclideanSpace.single j 1)âŸ©

theorem projector_add_orthogonal : projector S + projector Sá—® = 1 := by
  have h_decomp v : v = S.subtype (S.orthogonalProjection v) + Sá—®.subtype (Sá—®.orthogonalProjection v) := by
    simp
  ext i j : 2
  specialize h_decomp (Pi.single j 1)
  convert congr_arg (fun x => x i) h_decomp.symm using 1
  simp [Matrix.one_apply, Pi.single_apply]

@[simp]
theorem trace_projector : (projector S).trace = (Module.finrank ğ•œ S : â„) := by
  suffices h_trace : ((S.subtype âˆ˜â‚— S.orthogonalProjection).toMatrix (EuclideanSpace.basisFun n ğ•œ).toBasis (EuclideanSpace.basisFun n ğ•œ).toBasis).trace = Module.finrank ğ•œ S by
    simp [projector, trace_eq_re_trace, h_trace]
  suffices h_trace : ((S.subtype âˆ˜â‚— S.orthogonalProjection).toMatrix (EuclideanSpace.basisFun n ğ•œ).toBasis (EuclideanSpace.basisFun n ğ•œ).toBasis).trace = (LinearMap.id.toMatrix (Module.finBasis ğ•œ S) (Module.finBasis ğ•œ S)).trace by
    simp [h_trace]
  rw [LinearMap.toMatrix_comp _ (Module.finBasis ğ•œ â†¥S), Matrix.trace_mul_comm, â† LinearMap.toMatrix_comp]
  congr 2
  ext1
  simp [Submodule.orthogonalProjection_mem_subspace_eq_self]

/--
The `HermitianMat.projector` for the `HermitianMat.support` submodule.
-/
noncomputable def supportProj (A : HermitianMat n ğ•œ) : HermitianMat n ğ•œ := projector A.support

/--
The `HermitianMat.projector` for the `HermitianMat.ker` submodule.
-/
noncomputable def kerProj (A : HermitianMat n ğ•œ) : HermitianMat n ğ•œ := projector A.ker

@[simp]
theorem kerProj_add_supportProj : A.kerProj + A.supportProj = 1 := by
  rw [â† projector_add_orthogonal A.ker, ker_orthogonal_eq_support, kerProj, supportProj]

@[simp]
theorem kerProj_of_nonSingular [NonSingular A] : A.kerProj = 0 := by
  simp only [kerProj, nonSingular_ker_bot, HermitianMat.ext_iff]
  simp [projector]

@[simp]
theorem supportProj_of_nonSingular [NonSingular A] : A.supportProj = 1 := by
  simpa using A.kerProj_add_supportProj

/--
The projector onto a submodule S is the sum of the outer products of the vectors in an orthonormal basis of S.
-/
theorem projector_eq_sum_rankOne (b : OrthonormalBasis Î¹ ğ•œ S) :
    (projector S).mat = âˆ‘ i, Matrix.vecMulVec (S.subtype (b i)) (star (S.subtype (b i))) := by
  ext i j : 1
  have h_proj v : S.orthogonalProjection v = âˆ‘ i, âŸª(b i).val, vâŸ«_ğ•œ â€¢ (b i).val := by
    simpa using congr($((OrthonormalBasis.sum_repr' b  (S.orthogonalProjection v)).symm).val)
  specialize h_proj (Pi.single j 1)
  simp only [Matrix.vecMulVec, Matrix.sum_apply]
  refine (congr_fun h_proj i).trans ?_
  rw [Finset.sum_apply]
  simp [Pi.single_apply, inner, mul_comm]

/--
The projector onto the support of A is the sum of the projections onto the eigenvectors with non-zero eigenvalues.
-/
lemma projector_support_eq_sum : A.supportProj.mat =
    âˆ‘ i, (if A.H.eigenvalues i = 0 then 0 else 1) â€¢
      Matrix.vecMulVec (A.H.eigenvectorBasis i) (star (A.H.eigenvectorBasis i)) := by
  --TODO Cleanup
  -- The support of A is the span of the eigenvectors corresponding to non-zero eigenvalues.
  have h_support : A.support = Submodule.span ğ•œ (Set.range (fun i => if A.H.eigenvalues i = 0 then 0 else A.H.eigenvectorBasis i)) := by
    ext x;
    constructor;
    Â· intro hx
      obtain âŸ¨y, hyâŸ© : âˆƒ y : EuclideanSpace ğ•œ n, x = A.mat.mulVec y := by
        obtain âŸ¨ y, rfl âŸ© := hx;
        exact âŸ¨ y, rfl âŸ©;
      -- Since $y$ is in the span of the eigenvectors of $A$, we can write $y$ as a linear combination of the eigenvectors.
      obtain âŸ¨c, hcâŸ© : âˆƒ c : n â†’ ğ•œ, y = âˆ‘ i, c i â€¢ A.H.eigenvectorBasis i := by
        have := A.H.eigenvectorBasis.sum_repr y;
        exact âŸ¨ _, this.symm âŸ©;
      -- Since $A$ is Hermitian, we can write $A.mulVec (c i â€¢ eigenvectorBasis i)$ as $c i â€¢ eigenvalues i â€¢ eigenvectorBasis i$.
      have h_mulVec : âˆ€ i, A.mat.mulVec (c i â€¢ A.H.eigenvectorBasis i) = c i â€¢ A.H.eigenvalues i â€¢ A.H.eigenvectorBasis i := by
        intro i
        have h_mulVec : A.mat.mulVec (A.H.eigenvectorBasis i) = A.H.eigenvalues i â€¢ A.H.eigenvectorBasis i := by
          convert A.H.mulVec_eigenvectorBasis i;
        rw [ â† h_mulVec, Matrix.mulVec_smul ];
      simp_all [ Submodule.mem_span_range_iff_exists_fun ];
      use fun i => c i * A.H.eigenvalues i;
      rw [ Matrix.mulVec_sum ];
      exact Finset.sum_congr rfl fun i _ => by split_ifs <;> simp_all [ MulAction.mul_smul ] ;
    Â· intro hx;
      refine' Submodule.span_le.mpr _ hx;
      rintro _ âŸ¨ i, rfl âŸ© ; by_cases hi : A.H.eigenvalues i = 0 <;> simp +decide [ hi ];
      have := A.H.mulVec_eigenvectorBasis i;
      refine' âŸ¨ _, _ âŸ©;
      exact ( A.H.eigenvalues i ) â»Â¹ â€¢ WithLp.ofLp ( A.H.eigenvectorBasis i );
      convert congr_arg ( fun x => ( A.H.eigenvalues i ) â»Â¹ â€¢ x ) this using 1 <;> simp +decide [ hi, smul_smul ];
      Â· congr! 1;
      Â· exact rfl;
  rw [ supportProj, h_support ];
  convert projector_eq_sum_rankOne _ _ using 1;
  rotate_left;
  exact { i : n // A.H.eigenvalues i â‰  0 };
  exact inferInstance;
  refine' OrthonormalBasis.mk _ _;
  use fun i => âŸ¨ A.H.eigenvectorBasis i, Submodule.subset_span âŸ¨ i, by simp +decide [ i.2 ] âŸ© âŸ©;
  all_goals simp +decide [ orthonormal_iff_ite ];
  Â· intro i hi j hj; split_ifs with h <;> simp_all
  Â· rw [ Submodule.eq_top_iff' ];
    rintro âŸ¨ x, hx âŸ©;
    rw [ Submodule.mem_span ] at hx âŠ¢;
    intro p hp;
    convert hx ( p.map ( Submodule.subtype _ ) ) _;
    Â· simp +decide [ Submodule.mem_map ];
      exact âŸ¨ fun h => âŸ¨ by assumption, h âŸ©, fun âŸ¨ _, h âŸ© => h âŸ©;
    Â· rintro _ âŸ¨ i, rfl âŸ©;
      by_cases hi : A.H.eigenvalues i = 0 <;> simp +decide [ hi ];
      exact âŸ¨ Submodule.subset_span âŸ¨ i, by aesop âŸ©, hp âŸ¨ âŸ¨ i, hi âŸ©, rfl âŸ© âŸ©;
  Â· rw [ â† Finset.sum_subset ( Finset.subset_univ ( Finset.univ.filter fun i => A.H.eigenvalues i â‰  0 ) ) ];
    Â· refine' Finset.sum_bij ( fun i hi => âŸ¨ i, by aesop âŸ© ) _ _ _ _ <;> aesop;
    Â· aesop

/-
`HermitianMat.supportProj` as a cfc.
-/
theorem supportProj_eq_cfc : A.supportProj = A.cfc (if Â· = 0 then 0 else 1) := by
  apply HermitianMat.ext;
  rw [HermitianMat.cfc_toMat_eq_sum_smul_proj];
  convert projector_support_eq_sum A using 1;
  refine' Finset.sum_congr rfl fun i _ => _;
  ext x y
  simp [ Matrix.vecMulVec, Matrix.mul_apply ] ;
  simp [ Matrix.single ];
  simp [ Finset.sum_ite, Finset.filter_eq, Finset.filter_and ];
  rw [ Finset.sum_eq_single i ] <;> aesop

/-- Projector onto the non-negative eigenspace of `B - A`. Accessible by the notation
`{A â‰¤â‚š B}`, which is scoped to `HermitianMat`. This is the unique maximum operator `P`
such that `P^2 = P` and `P * A * P â‰¤ P * B * P` in the Loewner order. -/
def proj_le (A B : HermitianMat n ğ•œ) : HermitianMat n ğ•œ :=
  (B - A).cfc (fun x â†¦ if 0 â‰¤ x then 1 else 0)

/-- Projector onto the positive eigenspace of `B - A`. Accessible by the notation
`{A <â‚š B}`, which is scoped to `HermitianMat`. Compare with `proj_le`. -/
noncomputable def proj_lt (A B : HermitianMat n ğ•œ) : HermitianMat n ğ•œ :=
  (B - A).cfc (fun x â†¦ if 0 < x then 1 else 0)

-- Note this is in the opposite direction as in the Stein's Lemma paper, which uses `â‰¥â‚š`
-- as the default ordering. We offer the `â‰¥â‚š` notation which is the same with the arguments
-- flipped, similar to how `GT.gt` is defeq to `LT.lt` with arguments flipped.
-- We put the â‰¥â‚š first, since both can delaborate and we want to show the â‰¤â‚š one.
scoped notation "{" A " â‰¥â‚š " B "}" => proj_le B A
scoped notation "{" A " â‰¤â‚š " B "}" => proj_le A B

scoped notation "{" A " >â‚š " B "}" => proj_lt B A
scoped notation "{" A " <â‚š " B "}" => proj_lt A B

theorem proj_le_def : {A â‰¤â‚š B} = (B - A).cfc (fun x â†¦ if 0 â‰¤ x then 1 else 0) := by
  rfl

theorem proj_lt_def : {A <â‚š B} = (B - A).cfc (fun x â†¦ if 0 < x then 1 else 0) := by
  rfl

theorem proj_le_sq : {A â‰¤â‚š B}^2 = {A â‰¤â‚š B} := by
  rw [proj_le_def]
  --TODO: Should do a `HermitianMat.cfc_pow`.
  ext1
  simp only [cfc, mat_pow, mat_mk]
  rw [â† cfc_pow _ 2 (hf := by cfc_cont_tac)]
  simp

theorem proj_lt_sq : {A <â‚š B}^2 = {A <â‚š B} := by
  rw [proj_lt_def]
  --TODO: Should do a `HermitianMat.cfc_pow`.
  ext1
  simp only [cfc, mat_pow, mat_mk]
  rw [â† cfc_pow _ 2 (hf := by cfc_cont_tac)]
  simp

theorem proj_zero_le_cfc : {0 â‰¤â‚š A} = cfc A (fun x â†¦ if 0 â‰¤ x then 1 else 0) := by
  simp only [proj_le, sub_zero]

theorem proj_zero_lt_cfc : {0 <â‚š A} = cfc A (fun x â†¦ if 0 < x then 1 else 0) := by
  simp only [proj_lt, sub_zero]

theorem proj_le_zero_cfc : {A â‰¤â‚š 0} = cfc A (fun x â†¦ if x â‰¤ 0 then 1 else 0) := by
  simp only [proj_le, zero_sub]
  --TODO: Should do a `HermitianMat.cfc_comp_neg`?
  nth_rw 1 [â† cfc_id A]
  rw [â† cfc_neg, â† cfc_comp]
  congr! 2 with x
  simp

theorem proj_lt_zero_cfc : {A <â‚š 0} = cfc A (fun x â†¦ if x < 0 then 1 else 0) := by
  simp only [proj_lt, zero_sub]
  --TODO: Should do a `HermitianMat.cfc_comp_neg`?
  nth_rw 1 [â† cfc_id A]
  rw [â† cfc_neg, â† cfc_comp]
  congr! 2 with x
  simp

theorem proj_le_nonneg : 0 â‰¤ {A â‰¤â‚š B} := by
  rw [proj_le, zero_le_cfc]
  intro i
  apply ite_nonneg <;> norm_num

theorem proj_lt_nonneg : 0 â‰¤ {A <â‚š B} := by
  rw [proj_lt, zero_le_cfc]
  intro i
  apply ite_nonneg <;> norm_num

theorem proj_le_le_one : {A â‰¤â‚š B} â‰¤ 1 := by
  --The whole `rw` line is a defeq, i.e. `change _root_.cfc _ (B - A).mat â‰¤ 1` works too.
  --TODO better API.
  open MatrixOrder in
  rw [â† Subtype.coe_le_coe, val_eq_coe, selfAdjoint.val_one]
  apply cfc_le_one (f := fun x â†¦ if 0 â‰¤ x then 1 else 0)
  intros; split <;> norm_num

open MatrixOrder in
theorem proj_le_mul_nonneg : 0 â‰¤ {A â‰¤â‚š B}.mat * (B - A).mat := by
  rw [proj_le]
  nth_rewrite 2 [â† cfc_id (B - A)]
  rw [â† mat_cfc_mul]
  apply cfc_nonneg
  aesop

open MatrixOrder in
theorem proj_le_mul_le : {A â‰¤â‚š B}.mat * A.mat â‰¤ {A â‰¤â‚š B}.mat * B.mat := by
  rw [â† sub_nonneg, â† mul_sub_left_distrib]
  exact proj_le_mul_nonneg A B

@[simp]
theorem proj_le_add_lt : {A <â‚š B} + {B â‰¤â‚š A} = 1 := by
  rw [proj_le, proj_lt]
  rw [â† neg_sub A B]
  nth_rw 1 [â† cfc_id (A - B)]
  rw[â† cfc_neg, â† cfc_comp, â† cfc_add]
  convert cfc_const (A - B) 1 with x
  Â· simp; grind
  Â· simp

theorem conj_lt_add_conj_le : A.conj {A <â‚š 0} + A.conj {0 â‰¤â‚š A} = A := by
  rw (occs := [2, 4, 5]) [â† cfc_id A]
  rw [proj_lt_zero_cfc, proj_zero_le_cfc, cfc_conj, cfc_conj, â† cfc_add]
  congr; ext
  simp; grind

/-
The projection onto the support can be split into the projection onto positive
and negative eigenvalues.
-/
theorem supportProj_eq_proj_lt_add_proj_lt (A : HermitianMat n ğ•œ) :
    A.supportProj = {A <â‚š 0} + {0 <â‚š A} := by
  rw [supportProj_eq_cfc, proj_lt_zero_cfc, proj_zero_lt_cfc, â† cfc_add A]
  congr 1
  grind only [Pi.add_apply]

/-- The positive part of a Hermitian matrix: the projection onto its positive eigenvalues. -/
def proj_pos : HermitianMat n ğ•œ :=
  A.cfc (fun x â†¦ x âŠ” 0)

/-- The negative part of a Hermitian matrix: the projection onto its negative eigenvalues. -/
def proj_neg : HermitianMat n ğ•œ :=
  A.cfc (fun x â†¦ -x âŠ” 0)

instance : PosPart (HermitianMat n ğ•œ) where
  posPart := proj_pos

instance : NegPart (HermitianMat n ğ•œ) where
  negPart := proj_neg

theorem posPart_eq_cfc_max : Aâº = A.cfc (fun x â†¦ x âŠ” 0) := by
  rfl

theorem negPart_eq_cfc_min : Aâ» = A.cfc (fun x â†¦ -x âŠ” 0) := by
  rfl

theorem posPart_eq_cfc_ite : Aâº = A.cfc (fun x â†¦ if 0 â‰¤ x then x else 0) := by
  simp only [â† max_def', posPart_eq_cfc_max]

theorem negPart_eq_cfc_ite : Aâ» = A.cfc (fun x â†¦ if x â‰¤ 0 then -x else 0) := by
  simp only [negPart_eq_cfc_min, max_def]
  congr; ext
  split <;> split <;> grind

/-- There is an existing (very slow) `PosPart` instance on `Matrix n n ğ•œ`, this shows
that this is equal. -/
theorem posPart_eq_posPart_toMat : Aâº = A.matâº := by
  rw [CFC.posPart_def, cfcâ‚™_eq_cfc]
  rfl

/-- There is an existing (very slow) `PosPart` instance on `Matrix n n ğ•œ`, this shows
that this is equal. -/
theorem negPart_eq_negPart_toMat : Aâ» = A.matâ» := by
  rw [CFC.negPart_def, cfcâ‚™_eq_cfc]
  rfl

/-- The positive part can be equivalently described as the nonnegative part. -/
theorem posPart_eq_cfc_lt : Aâº = A.cfc (fun x â†¦ if 0 < x then x else 0) := by
  rw [posPart_eq_cfc_ite]
  congr with x
  rcases lt_trichotomy x 0 <;> grind

/-- The negative part can be equivalently described as the nonpositive part. -/
theorem negPart_eq_cfc_lt : Aâ» = A.cfc (fun x â†¦ if x < 0 then -x else 0) := by
  rw [negPart_eq_cfc_ite]
  congr with x
  rcases lt_trichotomy x 0 <;> grind

theorem posPart_add_negPart : Aâº - Aâ» = A := by
  rw [posPart_eq_cfc_ite, negPart_eq_cfc_lt, â† cfc_sub]
  convert cfc_id A
  ext; dsimp; grind

theorem zero_le_posPart : 0 â‰¤ Aâº := by
  rw [posPart_eq_cfc_ite, zero_le_cfc]
  intro; split <;> order

theorem negPart_le_zero : 0 â‰¤ Aâ» := by
  rw [negPart_eq_cfc_ite, zero_le_cfc]
  intro; split <;> grind

theorem posPart_le : A â‰¤ Aâº := by
  nth_rw 1 [â† cfc_id A]
  rw [posPart_eq_cfc_ite, â† sub_nonneg, â† cfc_sub, zero_le_cfc]
  intro; simp; split <;> order

theorem posPart_mul_negPart : Aâº.mat * Aâ».mat = 0 := by
  rw [posPart_eq_cfc_ite, negPart_eq_cfc_ite, â† mat_cfc_mul]
  convert congrArg mat (cfc_const A 0)
  Â· grind [Pi.mul_apply, mul_eq_zero]
  Â· simp

open RealInnerProductSpace

theorem proj_le_inner_nonneg  : 0 â‰¤ âŸª{A â‰¤â‚š B}, (B - A)âŸ« :=
  --This inner is equal to `(B - A)âº.trace`, could be better way to describe it
  inner_mul_nonneg (proj_le_mul_nonneg A B)

theorem proj_le_inner_le : âŸª{A â‰¤â‚š B}, AâŸ« â‰¤ âŸª{A â‰¤â‚š B}, BâŸ« := by
  rw [â† sub_nonneg, â† inner_sub_right]
  exact proj_le_inner_nonneg A B

open RealInnerProductSpace in
theorem inner_proj_le_nonneg : 0 â‰¤ âŸª{A â‰¤â‚š B}, (B - A)âŸ« :=
  proj_le_inner_nonneg A B

open RealInnerProductSpace in
theorem inner_proj_le_le : âŸª{A â‰¤â‚š B}, AâŸ« â‰¤ âŸª{A â‰¤â‚š B}, BâŸ« :=
  proj_le_inner_le A B

--TODO: When we upgrade `cfc_continuous` from ğ•œ to â„‚, we upgrade these too.
@[fun_prop]
theorem posPart_Continuous : Continuous (Â·âº : HermitianMat n â„‚ â†’ _) := by
  simp_rw [posPart_eq_cfc_max]
  fun_prop

@[fun_prop]
theorem negPart_Continuous : Continuous (Â·â» : HermitianMat n â„‚ â†’ _) := by
  simp_rw [negPart_eq_cfc_min]
  fun_prop

proof_wanted posPart_le_zero_iff : Aâº â‰¤ 0 â†” A â‰¤ 0

proof_wanted posPart_eq_zero_iff : Aâº = 0 â†” A â‰¤ 0
/- := by
   rw [â† posPart_le_zero_iff]
   have := zero_le_posPart A
   constructor <;> order
-/

--Many missing lemmas: see `Mathlib.Algebra.Order.Group.PosPart` for examples
-- (They don't apply here since it's not a Lattice, and there's no well-defined `max` in
--   the Loewner order.)
-- PosPart is Monotone (so `A â‰¤ B` implies `Aâº â‰¤ Bâº`), as is NegPart
-- PosPart and NegPart commute with nonnegative scalar muliptlication
-- `Aâº â‰¤ 0 â†” Aâº = 0 â†” A = 0`
-- `0 â‰¤ A â†’ Aâº = A`
-- `0 < A â†’ 0 < Aâº` (this is not the PosDef version, this is `â‰¤ && â‰ `)
-- `A.PosDef â†’ Aâº.PosDef`
-- versions of those ^^ for negPart
-- simp: 0âº = 0, 0â» = 0, 1âº = 1, 1â» = 0
--   (-A)âº = Aâ», (-A)â» = Aâº
--  Aâºâº = Aâº, Aâºâ» = 0

-- variable {d : Type*} [Fintype d] [DecidableEq d] (A B : HermitianMat d â„‚)

theorem one_sub_proj_le : 1 - {B â‰¤â‚š A} = {A <â‚š B} := by
  rw [sub_eq_iff_eq_add, proj_le_add_lt]

open MatrixOrder ComplexOrder

theorem proj_lt_mul_nonneg : 0 â‰¤ {A <â‚š B}.mat * (B - A).mat := by
  rw [proj_lt]
  nth_rewrite 2 [â† cfc_id (B - A)]
  rw [â† mat_cfc_mul]
  apply cfc_nonneg
  intros
  simp only [Pi.mul_apply, id_eq, ite_mul, one_mul, zero_mul]
  split <;> order

theorem proj_lt_mul_lt : {A <â‚š B}.mat * A.mat â‰¤ {A <â‚š B}.mat * B.mat := by
  rw [â† sub_nonneg, â† mul_sub_left_distrib]
  exact A.proj_lt_mul_nonneg B

theorem inner_negPart_nonpos : âŸªA, Aâ»âŸ« â‰¤ 0 := by
  rw [â† neg_le_neg_iff, neg_zero, â† inner_neg_right]
  apply inner_mul_nonneg
  nth_rw 1 [â† A.cfc_id]
  rw [negPart_eq_cfc_ite]
  rw [â† cfc_neg]
  rw [â† mat_cfc_mul]
  change 0 â‰¤ A.cfc _
  rw [zero_le_cfc]
  intro i
  dsimp
  split_ifs with h
  Â· rw [neg_neg]
    exact mul_self_nonneg _
  Â· simp

@[simp]
theorem posPart_inner_negPart_zero : âŸªAâº, Aâ»âŸ« = 0 := by
  have hi := inner_eq_trace_rc Aâº Aâ»
  rw [posPart_mul_negPart, Matrix.trace_zero] at hi
  simpa only [map_eq_zero] using hi

theorem inner_negPart_zero_iff : âŸªA, Aâ»âŸ« = 0 â†” 0 â‰¤ A := by
  constructor
  Â· intro h
    nth_rw 1 [â† posPart_add_negPart A] at h
    rw [inner_sub_left, sub_eq_zero, posPart_inner_negPart_zero, eq_comm, inner_self_eq_zero] at h
    rw [â† zero_smul â„ 1, â† cfc_const A, negPart_eq_cfc_ite] at h --TODO cfc_zero
    rw [cfc_eq_cfc_iff_eqOn, A.H.spectrum_real_eq_range_eigenvalues] at h
    simp only [Set.eqOn_range] at h
    replace h (i) := congrFun h i
    simp only [Function.comp_apply, ite_eq_right_iff, neg_eq_zero] at h
    rw [zero_le_iff, A.H.posSemidef_iff_eigenvalues_nonneg]
    intro i
    contrapose! h
    use i, h.le, h.ne
  Â· intro h
    apply le_antisymm
    Â· exact inner_negPart_nonpos A
    Â· exact inner_ge_zero h (negPart_le_zero A)

theorem inner_negPart_neg_iff : âŸªA, Aâ»âŸ« < 0 â†” Â¬0 â‰¤ A := by
  simp [â† inner_negPart_zero_iff, lt_iff_le_and_ne, inner_negPart_nonpos A]

/-- The self-duality of the PSD cone: a matrix is PSD iff its inner product with all
nonnegative matrices is non-negative. -/
theorem zero_le_iff_inner_pos (A : HermitianMat n ğ•œ) :
    0 â‰¤ A â†” âˆ€ B, 0 â‰¤ B â†’ 0 â‰¤ âŸªA, BâŸ« := by
  use fun h _ â†¦ inner_ge_zero h
  intro h
  contrapose! h
  classical
  use Aâ», negPart_le_zero A
  rwa [inner_negPart_neg_iff]
