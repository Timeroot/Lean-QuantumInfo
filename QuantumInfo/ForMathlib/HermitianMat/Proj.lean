/-
Copyright (c) 2025 Leonardo A Lessa. All rights reserved.
Released under MIT license as described in the file LICENSE.
Authors: Leonardo A Lessa, Alex Meiburg
-/
import QuantumInfo.ForMathlib.HermitianMat.CFC

import Mathlib.Analysis.CStarAlgebra.Classes
import Mathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.PosPart.Basic

/-!
The positive parts, or equivalently the projections, of Hermitian matrices onto each other.
-/

noncomputable section
namespace HermitianMat

variable {n : Type*} [Fintype n] [DecidableEq n]
variable {ğ•œ : Type*} [RCLike ğ•œ]

/-- Projector onto the non-negative eigenspace of `B - A`. Accessible by the notation
`{A â‰¤â‚š B}`, which is scoped to `HermitianMat`. This is the unique maximum operator `P`
such that `P^2 = P` and `P * A * P â‰¤ P * B * P` in the Loewner order. -/
def proj_le (A B : HermitianMat n ğ•œ) : HermitianMat n ğ•œ :=
  (B - A).cfc (fun x â†¦ if 0 â‰¤ x then 1 else 0)

/-- Projector onto the positive eigenspace of `B - A`. Accessible by the notation
`{A <â‚š B}`, which is scoped to `HermitianMat`. Compare with `proj_le`. -/
noncomputable def proj_lt (A B : HermitianMat n ğ•œ) : HermitianMat n ğ•œ :=
  (B - A).cfc (fun x â†¦ if 0 < x then 1 else 0)

-- Note this is in the opposite direction as in the Stein's Lemma paper, which uses `â‰¥â‚š`
-- as the default ordering. We offer the `â‰¥â‚š` notation which is the same with the arguments
-- flipped, similar to how `GT.gt` is defeq to `LT.lt` with arguments flipped.
-- We put the â‰¥â‚š first, since both can delaborate and we want to show the â‰¤â‚š one.
scoped notation "{" A " â‰¥â‚š " B "}" => proj_le B A
scoped notation "{" A " â‰¤â‚š " B "}" => proj_le A B

scoped notation "{" A " >â‚š " B "}" => proj_lt B A
scoped notation "{" A " <â‚š " B "}" => proj_lt A B

variable (A B : HermitianMat n ğ•œ)

theorem proj_le_def : {A â‰¤â‚š B} = (B - A).cfc (fun x â†¦ if 0 â‰¤ x then 1 else 0) := by
  rfl

theorem proj_lt_def : {A <â‚š B} = (B - A).cfc (fun x â†¦ if 0 < x then 1 else 0) := by
  rfl

theorem proj_le_sq : {A â‰¤â‚š B}^2 = {A â‰¤â‚š B} := by
  rw [proj_le_def]
  --TODO: Should do a `HermitianMat.cfc_pow`.
  ext1
  simp only [cfc, mat_pow, mat_mk]
  rw [â† cfc_pow _ 2 (hf := by cfc_cont_tac)]
  simp

theorem proj_lt_sq : {A <â‚š B}^2 = {A <â‚š B} := by
  rw [proj_lt_def]
  --TODO: Should do a `HermitianMat.cfc_pow`.
  ext1
  simp only [cfc, mat_pow, mat_mk]
  rw [â† cfc_pow _ 2 (hf := by cfc_cont_tac)]
  simp

theorem proj_zero_le_cfc : {0 â‰¤â‚š A} = cfc A (fun x â†¦ if 0 â‰¤ x then 1 else 0) := by
  simp only [proj_le, sub_zero]

theorem proj_zero_lt_cfc : {0 <â‚š A} = cfc A (fun x â†¦ if 0 < x then 1 else 0) := by
  simp only [proj_lt, sub_zero]

theorem proj_le_zero_cfc : {A â‰¤â‚š 0} = cfc A (fun x â†¦ if x â‰¤ 0 then 1 else 0) := by
  simp only [proj_le, zero_sub]
  --TODO: Should do a `HermitianMat.cfc_comp_neg`?
  nth_rw 1 [â† cfc_id A]
  rw [â† cfc_neg, â† cfc_comp]
  congr! 2 with x
  simp

theorem proj_lt_zero_cfc : {A <â‚š 0} = cfc A (fun x â†¦ if x < 0 then 1 else 0) := by
  simp only [proj_lt, zero_sub]
  --TODO: Should do a `HermitianMat.cfc_comp_neg`?
  nth_rw 1 [â† cfc_id A]
  rw [â† cfc_neg, â† cfc_comp]
  congr! 2 with x
  simp

theorem proj_le_nonneg : 0 â‰¤ {A â‰¤â‚š B} := by
  rw [proj_le, zero_le_cfc]
  intro i
  apply ite_nonneg <;> norm_num

theorem proj_lt_nonneg : 0 â‰¤ {A <â‚š B} := by
  rw [proj_lt, zero_le_cfc]
  intro i
  apply ite_nonneg <;> norm_num

theorem proj_le_le_one : {A â‰¤â‚š B} â‰¤ 1 := by
  --The whole `rw` line is a defeq, i.e. `change _root_.cfc _ (B - A).mat â‰¤ 1` works too.
  --TODO better API.
  open MatrixOrder in
  rw [â† Subtype.coe_le_coe, val_eq_coe, selfAdjoint.val_one]
  apply cfc_le_one (f := fun x â†¦ if 0 â‰¤ x then 1 else 0)
  intros; split <;> norm_num

open MatrixOrder in
theorem proj_le_mul_nonneg : 0 â‰¤ {A â‰¤â‚š B}.mat * (B - A).mat := by
  rw [proj_le]
  nth_rewrite 2 [â† cfc_id (B - A)]
  rw [â† mat_cfc_mul]
  apply cfc_nonneg
  aesop

open MatrixOrder in
theorem proj_le_mul_le : {A â‰¤â‚š B}.mat * A.mat â‰¤ {A â‰¤â‚š B}.mat * B.mat := by
  rw [â† sub_nonneg, â† mul_sub_left_distrib]
  exact proj_le_mul_nonneg A B

@[simp]
theorem proj_le_add_lt : {A <â‚š B} + {B â‰¤â‚š A} = 1 := by
  rw [proj_le, proj_lt]
  rw [â† neg_sub A B]
  nth_rw 1 [â† cfc_id (A - B)]
  rw[â† cfc_neg, â† cfc_comp, â† cfc_add]
  convert cfc_const (A - B) 1 with x
  Â· simp; grind
  Â· simp

theorem conj_lt_add_conj_le : A.conj {A <â‚š 0} + A.conj {0 â‰¤â‚š A} = A := by
  rw (occs := [2, 4, 5]) [â† cfc_id A]
  rw [proj_lt_zero_cfc, proj_zero_le_cfc, cfc_conj, cfc_conj, â† cfc_add]
  congr; ext
  simp; grind

/-- The positive part of a Hermitian matrix: the projection onto its positive eigenvalues. -/
def proj_pos : HermitianMat n ğ•œ :=
  A.cfc (fun x â†¦ x âŠ” 0)

/-- The negative part of a Hermitian matrix: the projection onto its negative eigenvalues. -/
def proj_neg : HermitianMat n ğ•œ :=
  A.cfc (fun x â†¦ -x âŠ” 0)

instance : PosPart (HermitianMat n ğ•œ) where
  posPart := proj_pos

instance : NegPart (HermitianMat n ğ•œ) where
  negPart := proj_neg

theorem posPart_eq_cfc_max : Aâº = A.cfc (fun x â†¦ x âŠ” 0) := by
  rfl

theorem negPart_eq_cfc_min : Aâ» = A.cfc (fun x â†¦ -x âŠ” 0) := by
  rfl

theorem posPart_eq_cfc_ite : Aâº = A.cfc (fun x â†¦ if 0 â‰¤ x then x else 0) := by
  simp only [â† max_def', posPart_eq_cfc_max]

theorem negPart_eq_cfc_ite : Aâ» = A.cfc (fun x â†¦ if x â‰¤ 0 then -x else 0) := by
  simp only [negPart_eq_cfc_min, max_def]
  congr; ext
  split <;> split <;> grind

/-- There is an existing (very slow) `PosPart` instance on `Matrix n n ğ•œ`, this shows
that this is equal. -/
theorem posPart_eq_posPart_toMat : Aâº = A.matâº := by
  rw [CFC.posPart_def, cfcâ‚™_eq_cfc]
  rfl

/-- There is an existing (very slow) `PosPart` instance on `Matrix n n ğ•œ`, this shows
that this is equal. -/
theorem negPart_eq_negPart_toMat : Aâ» = A.matâ» := by
  rw [CFC.negPart_def, cfcâ‚™_eq_cfc]
  rfl

/-- The positive part can be equivalently described as the nonnegative part. -/
theorem posPart_eq_cfc_lt : Aâº = A.cfc (fun x â†¦ if 0 < x then x else 0) := by
  rw [posPart_eq_cfc_ite]
  congr with x
  rcases lt_trichotomy x 0 <;> grind

/-- The negative part can be equivalently described as the nonpositive part. -/
theorem negPart_eq_cfc_lt : Aâ» = A.cfc (fun x â†¦ if x < 0 then -x else 0) := by
  rw [negPart_eq_cfc_ite]
  congr with x
  rcases lt_trichotomy x 0 <;> grind

theorem posPart_add_negPart : Aâº - Aâ» = A := by
  rw [posPart_eq_cfc_ite, negPart_eq_cfc_lt, â† cfc_sub]
  convert cfc_id A
  ext; dsimp; grind

theorem zero_le_posPart : 0 â‰¤ Aâº := by
  rw [posPart_eq_cfc_ite, zero_le_cfc]
  intro; split <;> order

theorem negPart_le_zero : 0 â‰¤ Aâ» := by
  rw [negPart_eq_cfc_ite, zero_le_cfc]
  intro; split <;> grind

theorem posPart_le : A â‰¤ Aâº := by
  nth_rw 1 [â† cfc_id A]
  rw [posPart_eq_cfc_ite, â† sub_nonneg, â† cfc_sub, zero_le_cfc]
  intro; simp; split <;> order

theorem posPart_mul_negPart : Aâº.mat * Aâ».mat = 0 := by
  rw [posPart_eq_cfc_ite, negPart_eq_cfc_ite, â† mat_cfc_mul]
  convert congrArg mat (cfc_const A 0)
  Â· grind [Pi.mul_apply, mul_eq_zero]
  Â· simp

open RealInnerProductSpace

theorem proj_le_inner_nonneg  : 0 â‰¤ âŸª{A â‰¤â‚š B}, (B - A)âŸ« :=
  --This inner is equal to `(B - A)âº.trace`, could be better way to describe it
  inner_mul_nonneg (proj_le_mul_nonneg A B)

theorem proj_le_inner_le : âŸª{A â‰¤â‚š B}, AâŸ« â‰¤ âŸª{A â‰¤â‚š B}, BâŸ« := by
  rw [â† sub_nonneg, â† inner_sub_right]
  exact proj_le_inner_nonneg A B

open RealInnerProductSpace in
theorem inner_proj_le_nonneg : 0 â‰¤ âŸª{A â‰¤â‚š B}, (B - A)âŸ« :=
  proj_le_inner_nonneg A B

open RealInnerProductSpace in
theorem inner_proj_le_le : âŸª{A â‰¤â‚š B}, AâŸ« â‰¤ âŸª{A â‰¤â‚š B}, BâŸ« :=
  proj_le_inner_le A B

--TODO: When we upgrade `cfc_continuous` from ğ•œ to â„‚, we upgrade these too.
@[fun_prop]
theorem posPart_Continuous : Continuous (Â·âº : HermitianMat n â„‚ â†’ _) := by
  simp_rw [posPart_eq_cfc_max]
  fun_prop

@[fun_prop]
theorem negPart_Continuous : Continuous (Â·â» : HermitianMat n â„‚ â†’ _) := by
  simp_rw [negPart_eq_cfc_min]
  fun_prop

proof_wanted posPart_le_zero_iff : Aâº â‰¤ 0 â†” A â‰¤ 0

proof_wanted posPart_eq_zero_iff : Aâº = 0 â†” A â‰¤ 0
/- := by
   rw [â† posPart_le_zero_iff]
   have := zero_le_posPart A
   constructor <;> order
-/

--Many missing lemmas: see `Mathlib.Algebra.Order.Group.PosPart` for examples
-- (They don't apply here since it's not a Lattice, and there's no well-defined `max` in
--   the Loewner order.)
-- PosPart is Monotone (so `A â‰¤ B` implies `Aâº â‰¤ Bâº`), as is NegPart
-- PosPart and NegPart commute with nonnegative scalar muliptlication
-- `Aâº â‰¤ 0 â†” Aâº = 0 â†” A = 0`
-- `0 â‰¤ A â†’ Aâº = A`
-- `0 < A â†’ 0 < Aâº` (this is not the PosDef version, this is `â‰¤ && â‰ `)
-- `A.PosDef â†’ Aâº.PosDef`
-- versions of those ^^ for negPart
-- simp: 0âº = 0, 0â» = 0, 1âº = 1, 1â» = 0
--   (-A)âº = Aâ», (-A)â» = Aâº
--  Aâºâº = Aâº, Aâºâ» = 0

-- variable {d : Type*} [Fintype d] [DecidableEq d] (A B : HermitianMat d â„‚)

theorem one_sub_proj_le : 1 - {B â‰¤â‚š A} = {A <â‚š B} := by
  rw [sub_eq_iff_eq_add, proj_le_add_lt]

open MatrixOrder ComplexOrder

theorem proj_lt_mul_nonneg : 0 â‰¤ {A <â‚š B}.mat * (B - A).mat := by
  rw [proj_lt]
  nth_rewrite 2 [â† cfc_id (B - A)]
  rw [â† mat_cfc_mul]
  apply cfc_nonneg
  intros
  simp only [Pi.mul_apply, id_eq, ite_mul, one_mul, zero_mul]
  split <;> order

theorem proj_lt_mul_lt : {A <â‚š B}.mat * A.mat â‰¤ {A <â‚š B}.mat * B.mat := by
  rw [â† sub_nonneg, â† mul_sub_left_distrib]
  exact A.proj_lt_mul_nonneg B

theorem inner_negPart_nonpos : âŸªA, Aâ»âŸ« â‰¤ 0 := by
  rw [â† neg_le_neg_iff, neg_zero, â† inner_neg_right]
  apply inner_mul_nonneg
  nth_rw 1 [â† A.cfc_id]
  rw [negPart_eq_cfc_ite]
  rw [â† cfc_neg]
  rw [â† mat_cfc_mul]
  change 0 â‰¤ A.cfc _
  rw [zero_le_cfc]
  intro i
  dsimp
  split_ifs with h
  Â· rw [neg_neg]
    exact mul_self_nonneg _
  Â· simp

@[simp]
theorem posPart_inner_negPart_zero : âŸªAâº, Aâ»âŸ« = 0 := by
  have hi := inner_eq_trace_rc Aâº Aâ»
  rw [posPart_mul_negPart, Matrix.trace_zero] at hi
  simpa only [map_eq_zero] using hi

theorem inner_negPart_zero_iff : âŸªA, Aâ»âŸ« = 0 â†” 0 â‰¤ A := by
  constructor
  Â· intro h
    nth_rw 1 [â† posPart_add_negPart A] at h
    rw [inner_sub_left, sub_eq_zero, posPart_inner_negPart_zero, eq_comm, inner_self_eq_zero] at h
    rw [â† zero_smul â„ 1, â† cfc_const A, negPart_eq_cfc_ite] at h --TODO cfc_zero
    rw [cfc_eq_cfc_iff_eqOn, A.H.spectrum_real_eq_range_eigenvalues] at h
    simp only [Set.eqOn_range] at h
    replace h (i) := congrFun h i
    simp only [Function.comp_apply, ite_eq_right_iff, neg_eq_zero] at h
    rw [zero_le_iff, A.H.posSemidef_iff_eigenvalues_nonneg]
    intro i
    contrapose! h
    use i, h.le, h.ne
  Â· intro h
    apply le_antisymm
    Â· exact inner_negPart_nonpos A
    Â· exact inner_ge_zero h (negPart_le_zero A)

theorem inner_negPart_neg_iff : âŸªA, Aâ»âŸ« < 0 â†” Â¬0 â‰¤ A := by
  simp [â† inner_negPart_zero_iff, lt_iff_le_and_ne, inner_negPart_nonpos A]

/-- The self-duality of the PSD cone: a matrix is PSD iff its inner product with all
nonnegative matrices is non-negative. -/
theorem zero_le_iff_inner_pos (A : HermitianMat n ğ•œ) :
    0 â‰¤ A â†” âˆ€ B, 0 â‰¤ B â†’ 0 â‰¤ âŸªA, BâŸ« := by
  use fun h _ â†¦ inner_ge_zero h
  intro h
  contrapose! h
  classical
  use Aâ», negPart_le_zero A
  rwa [inner_negPart_neg_iff]
