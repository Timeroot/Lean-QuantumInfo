/-
Copyright (c) 2025 Leonardo A Lessa. All rights reserved.
Released under MIT license as described in the file LICENSE.
Authors: Leonardo A Lessa, Alex Meiburg
-/
import QuantumInfo.ForMathlib.HermitianMat.CFC

import Mathlib.Analysis.CStarAlgebra.Classes
import Mathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.PosPart.Basic

/-!
The positive parts, or equivalently the projections, of Hermitian matrices onto each other.
-/

noncomputable section
namespace HermitianMat

variable {n : Type*} [Fintype n] [DecidableEq n]
variable {ùïú : Type*} [RCLike ùïú]

/-- Projector onto the non-negative eigenspace of `B - A`. Accessible by the notation
`{A ‚â§‚Çö B}`, which is scoped to `HermitianMat`. This is the unique maximum operator `P`
such that `P^2 = P` and `P * A * P ‚â§ P * B * P` in the Loewner order. -/
def proj_le (A B : HermitianMat n ùïú) : HermitianMat n ùïú :=
  (B - A).cfc (fun x ‚Ü¶ if 0 ‚â§ x then 1 else 0)

/-- Projector onto the positive eigenspace of `B - A`. Accessible by the notation
`{A <‚Çö B}`, which is scoped to `HermitianMat`. Compare with `proj_le`. -/
noncomputable def proj_lt (A B : HermitianMat n ùïú) : HermitianMat n ùïú :=
  (B - A).cfc (fun x ‚Ü¶ if 0 < x then 1 else 0)

-- Note this is in the opposite direction as in the Stein's Lemma paper, which uses `‚â•‚Çö`
-- as the default ordering. We offer the `‚â•‚Çö` notation which is the same with the arguments
-- flipped, similar to how `GT.gt` is defeq to `LT.lt` with arguments flipped.
-- We put the ‚â•‚Çö first, since both can delaborate and we want to show the ‚â§‚Çö one.
scoped notation "{" A " ‚â•‚Çö " B "}" => proj_le B A
scoped notation "{" A " ‚â§‚Çö " B "}" => proj_le A B

scoped notation "{" A " >‚Çö " B "}" => proj_lt B A
scoped notation "{" A " <‚Çö " B "}" => proj_lt A B

variable (A B : HermitianMat n ùïú)

theorem proj_le_def : {A ‚â§‚Çö B} = (B - A).cfc (fun x ‚Ü¶ if 0 ‚â§ x then 1 else 0) := by
  rfl

theorem proj_lt_def : {A <‚Çö B} = (B - A).cfc (fun x ‚Ü¶ if 0 < x then 1 else 0) := by
  rfl

theorem proj_le_sq : {A ‚â§‚Çö B}^2 = {A ‚â§‚Çö B} := by
  rw [proj_le_def]
  --TODO: Should do a `HermitianMat.cfc_pow`.
  ext1
  simp only [selfAdjoint.val_pow, cfc]
  rw [‚Üê cfc_pow _ 2 (hf := _)]
  ¬∑ simp
  ¬∑ simp only [continuousOn_iff_continuous_restrict, continuous_of_discreteTopology]

theorem proj_lt_sq : {A <‚Çö B}^2 = {A <‚Çö B} := by
  rw [proj_lt_def]
  --TODO: Should do a `HermitianMat.cfc_pow`.
  ext1
  simp only [selfAdjoint.val_pow, cfc]
  rw [‚Üê cfc_pow _ 2 (hf := _)]
  ¬∑ simp
  ¬∑ simp only [continuousOn_iff_continuous_restrict, continuous_of_discreteTopology]

theorem proj_zero_le_cfc : {0 ‚â§‚Çö A} = cfc A (fun x ‚Ü¶ if 0 ‚â§ x then 1 else 0) := by
  simp only [proj_le, sub_zero]

theorem proj_zero_lt_cfc : {0 <‚Çö A} = cfc A (fun x ‚Ü¶ if 0 < x then 1 else 0) := by
  simp only [proj_lt, sub_zero]

theorem proj_le_zero_cfc : {A ‚â§‚Çö 0} = cfc A (fun x ‚Ü¶ if x ‚â§ 0 then 1 else 0) := by
  simp only [proj_le, zero_sub]
  --TODO: Should do a `HermitianMat.cfc_comp_neg`.
  nth_rw 1 [‚Üê cfc_id A]
  rw [‚Üê cfc_neg, ‚Üê cfc_comp]
  congr! 2 with x
  simp

theorem proj_lt_zero_cfc : {A <‚Çö 0} = cfc A (fun x ‚Ü¶ if x < 0 then 1 else 0) := by
  simp only [proj_lt, zero_sub]
  --TODO: Should do a `HermitianMat.cfc_comp_neg`.
  nth_rw 1 [‚Üê cfc_id A]
  rw [‚Üê cfc_neg, ‚Üê cfc_comp]
  congr! 2 with x
  simp

theorem proj_le_nonneg : 0 ‚â§ {A ‚â§‚Çö B} := by
  rw [proj_le, zero_le_cfc]
  intro i
  apply ite_nonneg <;> norm_num

theorem proj_lt_nonneg : 0 ‚â§ {A <‚Çö B} := by
  rw [proj_lt, zero_le_cfc]
  intro i
  apply ite_nonneg <;> norm_num

theorem proj_le_le_one : {A ‚â§‚Çö B} ‚â§ 1 := by
  --The whole `rw` line is a defeq, i.e. `change _root_.cfc _ (B - A).toMat ‚â§ 1` works too.
  --TODO better API.
  rw [‚Üê Subtype.coe_le_coe, val_eq_coe, selfAdjoint.val_one]
  apply cfc_le_one (f := fun x ‚Ü¶ if 0 ‚â§ x then 1 else 0)
  intros; split <;> norm_num

theorem proj_le_mul_nonneg : 0 ‚â§ {A ‚â§‚Çö B}.toMat * (B - A).toMat := by
  rw [proj_le]
  nth_rewrite 2 [‚Üê cfc_id (B - A)]
  rw [‚Üê coe_cfc_mul]
  apply cfc_nonneg
  aesop

theorem proj_le_mul_le : {A ‚â§‚Çö B}.toMat * A.toMat ‚â§ {A ‚â§‚Çö B}.toMat * B.toMat := by
  rw [‚Üê sub_nonneg, ‚Üê mul_sub_left_distrib]
  exact proj_le_mul_nonneg A B

@[simp]
theorem proj_le_add_lt : {A <‚Çö B} + {B ‚â§‚Çö A} = 1 := by
  rw [proj_le, proj_lt]
  rw [‚Üê neg_sub A B]
  nth_rw 1 [‚Üê cfc_id (A - B)]
  rw[‚Üê cfc_neg, ‚Üê cfc_comp, ‚Üê cfc_add]
  convert cfc_const (A - B) 1 with x
  ¬∑ simp; grind
  ¬∑ simp

theorem conj_lt_add_conj_le : A.conj {A <‚Çö 0} + A.conj {0 ‚â§‚Çö A} = A := by
  rw (occs := [2, 4, 5]) [‚Üê cfc_id A]
  rw [proj_lt_zero_cfc, proj_zero_le_cfc, cfc_conj, cfc_conj, ‚Üê cfc_add]
  congr; ext
  simp; grind

/-- The positive part of a Hermitian matrix: the projection onto its positive eigenvalues. -/
def proj_pos : HermitianMat n ùïú :=
  A.cfc (fun x ‚Ü¶ x ‚äî 0)

/-- The negative part of a Hermitian matrix: the projection onto its negative eigenvalues. -/
def proj_neg : HermitianMat n ùïú :=
  A.cfc (fun x ‚Ü¶ -x ‚äî 0)

instance : PosPart (HermitianMat n ùïú) where
  posPart := proj_pos

instance : NegPart (HermitianMat n ùïú) where
  negPart := proj_neg

theorem posPart_eq_cfc_max : A‚Å∫ = A.cfc (fun x ‚Ü¶ x ‚äî 0) := by
  rfl

theorem negPart_eq_cfc_min : A‚Åª = A.cfc (fun x ‚Ü¶ -x ‚äî 0) := by
  rfl

theorem posPart_eq_cfc_ite : A‚Å∫ = A.cfc (fun x ‚Ü¶ if 0 ‚â§ x then x else 0) := by
  simp only [‚Üê max_def', posPart_eq_cfc_max]

theorem negPart_eq_cfc_ite : A‚Åª = A.cfc (fun x ‚Ü¶ if x ‚â§ 0 then -x else 0) := by
  simp only [negPart_eq_cfc_min, max_def]
  congr; ext
  split <;> split <;> grind

/-- There is an existing (very slow) `PosPart` instance on `Matrix n n ùïú`, this shows
that this is equal. -/
theorem posPart_eq_posPart_toMat : A‚Å∫ = A.toMat‚Å∫ := by
  rw [CFC.posPart_def, cfc‚Çô_eq_cfc]
  rfl

/-- There is an existing (very slow) `PosPart` instance on `Matrix n n ùïú`, this shows
that this is equal. -/
theorem negPart_eq_negPart_toMat : A‚Åª = A.toMat‚Åª := by
  rw [CFC.negPart_def, cfc‚Çô_eq_cfc]
  rfl

/-- The positive part can be equivalently described as the nonnegative part. -/
theorem posPart_eq_cfc_lt : A‚Å∫ = A.cfc (fun x ‚Ü¶ if 0 < x then x else 0) := by
  rw [posPart_eq_cfc_ite]
  congr with x
  rcases lt_trichotomy x 0 <;> grind

/-- The negative part can be equivalently described as the nonpositive part. -/
theorem negPart_eq_cfc_lt : A‚Åª = A.cfc (fun x ‚Ü¶ if x < 0 then -x else 0) := by
  rw [negPart_eq_cfc_ite]
  congr with x
  rcases lt_trichotomy x 0 <;> grind

theorem posPart_add_negPart : A‚Å∫ - A‚Åª = A := by
  rw [posPart_eq_cfc_ite, negPart_eq_cfc_lt, ‚Üê cfc_sub]
  convert cfc_id A
  ext; dsimp; grind

theorem zero_le_posPart : 0 ‚â§ A‚Å∫ := by
  rw [posPart_eq_cfc_ite, zero_le_cfc]
  intro; split <;> order

theorem negPart_le_zero : 0 ‚â§ A‚Åª := by
  rw [negPart_eq_cfc_ite, zero_le_cfc]
  intro; split <;> grind

theorem posPart_le : A ‚â§ A‚Å∫ := by
  nth_rw 1 [‚Üê cfc_id A]
  rw [posPart_eq_cfc_ite, ‚Üê sub_nonneg, ‚Üê cfc_sub, zero_le_cfc]
  intro; simp; split <;> order

theorem posPart_mul_negPart : A‚Å∫.toMat * A‚Åª.toMat = 0 := by
  rw [posPart_eq_cfc_ite, negPart_eq_cfc_ite, ‚Üê coe_cfc_mul]
  convert congrArg toMat (cfc_const A 0)
  ¬∑ simp; order
  ¬∑ simp

theorem proj_le_inner_nonneg  : 0 ‚â§ {A ‚â§‚Çö B}.inner (B - A) :=
  --This inner is equal to `(B - A)‚Å∫.trace`, could be better way to describe it
  HermitianMat.inner_mul_nonneg (proj_le_mul_nonneg A B)

theorem proj_le_inner_le : {A ‚â§‚Çö B}.inner A ‚â§ {A ‚â§‚Çö B}.inner B := by
  rw [‚Üê sub_nonneg, ‚Üê HermitianMat.inner_left_sub]
  exact proj_le_inner_nonneg A B

open RealInnerProductSpace in
theorem inner_proj_le_nonneg : 0 ‚â§ ‚ü™{A ‚â§‚Çö B}, (B - A)‚ü´ :=
  proj_le_inner_nonneg A B

open RealInnerProductSpace in
theorem inner_proj_le_le : ‚ü™{A ‚â§‚Çö B}, A‚ü´ ‚â§ ‚ü™{A ‚â§‚Çö B}, B‚ü´ :=
  proj_le_inner_le A B

--TODO: When we upgrade `cfc_continuous` from ùïú to ‚ÑÇ, we upgrade these too.
@[fun_prop]
theorem posPart_Continuous : Continuous (¬∑‚Å∫ : HermitianMat n ‚ÑÇ ‚Üí _) := by
  simp_rw [posPart_eq_cfc_max]
  fun_prop

@[fun_prop]
theorem negPart_Continuous : Continuous (¬∑‚Åª : HermitianMat n ‚ÑÇ ‚Üí _) := by
  simp_rw [negPart_eq_cfc_min]
  fun_prop

proof_wanted posPart_le_zero_iff : A‚Å∫ ‚â§ 0 ‚Üî A ‚â§ 0

proof_wanted posPart_eq_zero_iff : A‚Å∫ = 0 ‚Üî A ‚â§ 0
/- := by
   rw [‚Üê posPart_le_zero_iff]
   have := zero_le_posPart A
   constructor <;> order
-/

--Many missing lemmas: see `Mathlib.Algebra.Order.Group.PosPart` for examples
-- (They don't apply here since it's not a Lattice, and there's no well-defined `max` in
--   the Loewner order.)
-- PosPart is Monotone (so `A ‚â§ B` implies `A‚Å∫ ‚â§ B‚Å∫`), as is NegPart
-- PosPart and NegPart commute with nonnegative scalar muliptlication
-- `A‚Å∫ ‚â§ 0 ‚Üî A‚Å∫ = 0 ‚Üî A = 0`
-- `0 ‚â§ A ‚Üí A‚Å∫ = A`
-- `0 < A ‚Üí 0 < A‚Å∫` (this is not the PosDef version, this is `‚â§ && ‚â†`)
-- `A.PosDef ‚Üí A‚Å∫.PosDef`
-- versions of those ^^ for negPart
-- simp: 0‚Å∫ = 0, 0‚Åª = 0, 1‚Å∫ = 1, 1‚Åª = 0
--   (-A)‚Å∫ = A‚Åª, (-A)‚Åª = A‚Å∫
--  A‚Å∫‚Å∫ = A‚Å∫, A‚Å∫‚Åª = 0
