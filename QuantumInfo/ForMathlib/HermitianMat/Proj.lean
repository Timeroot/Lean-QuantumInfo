import QuantumInfo.ForMathlib.HermitianMat.CFC
import QuantumInfo.ForMathlib.HermitianMat.Inner

/-!
The positive parts, or equivalently the projections, of Hermitian matrices onto each other.
-/

noncomputable section
open HermitianMat

variable {n : Type*} [Fintype n] [DecidableEq n]
variable {ùïú : Type*} [RCLike ùïú]

/-- Projector onto the non-negative eigenspace of `B - A`. Accessible by the notation
`{A ‚â§‚Çö B}`, which is scoped to `HermitianMat`. This is the unique maximum operator `P`
such that `P^2 = P` and `P * A * P ‚â§ P * B * P` in the Loewner order. -/
def proj_le (A B : HermitianMat n ùïú) : HermitianMat n ùïú :=
  (B - A).cfc (fun x ‚Ü¶ if 0 ‚â§ x then 1 else 0)

-- Note this is in the opposite direction as in the Stein's Lemma paper, which uses `‚â•‚Çö`
-- as the default ordering. We offer the `‚â•‚Çö` notation which is the same with the arguments
-- flipped, similar to how `GT.gt` is defeq to `LT.lt` with arguments flipped.
scoped[HermitianMat] notation "{" A " ‚â§‚Çö " B "}" => proj_le A B
scoped[HermitianMat] notation "{" A " ‚â•‚Çö " B "}" => proj_le B A

variable (A B : HermitianMat n ùïú)

theorem proj_le_cfc : {A ‚â§‚Çö B} = cfc (fun x ‚Ü¶ if 0 ‚â§ x then (1 : ‚Ñù) else 0) (B - A).toMat := by
  simp only [proj_le, HermitianMat.cfc]

theorem proj_le_sq : {A ‚â§‚Çö B}^2 = {A ‚â§‚Çö B} := by
  ext1
  simp only [HermitianMat.val_eq_coe, selfAdjoint.val_pow, proj_le_cfc]
  rw [‚Üê cfc_pow _ 2 (hf := _)]
  ¬∑ simp
  ¬∑ simp only [continuousOn_iff_continuous_restrict, continuous_of_discreteTopology]

theorem proj_le_nonneg : 0 ‚â§ {A ‚â§‚Çö B} := by
  rw [‚Üê proj_le_sq]
  exact HermitianMat.sq_nonneg

theorem proj_le_le_one : {A ‚â§‚Çö B} ‚â§ 1 := by
  --The whole `rw` line is a defeq, i.e. `change _root_.cfc _ (B - A).toMat ‚â§ 1` works too.
  --TODO better API.
  rw [‚Üê Subtype.coe_le_coe, val_eq_coe, selfAdjoint.val_one, proj_le_cfc]
  apply cfc_le_one (f := fun x ‚Ü¶ if 0 ‚â§ x then 1 else 0)
  intros; split <;> norm_num

theorem proj_le_mul_nonneg : 0 ‚â§ {A ‚â§‚Çö B}.toMat * (B - A).toMat := by
  rw [proj_le_cfc]
  nth_rewrite 2 [‚Üê cfc_id ‚Ñù (B - A).toMat]
  rw [‚Üê cfc_mul (hf := _) (hg := _)]
  ¬∑ apply cfc_nonneg
    aesop
  ¬∑ --TODO: It's a failure that cfc_cont_tac doesn't use these theorems. We could add
    -- them to the `CStarAlgebra` aesop rule_set, but even then `fun_prop` doesn't try
    -- the discharger once if it's stuck at the root, so we'd also need cfc_cont_tac
    -- to try the aesop once itself first.
    simp only [continuousOn_iff_continuous_restrict, continuous_of_discreteTopology]
  ¬∑ simp only [continuousOn_iff_continuous_restrict, continuous_of_discreteTopology]

theorem proj_le_mul_le : {A ‚â§‚Çö B}.toMat * A.toMat ‚â§ {A ‚â§‚Çö B}.toMat * B.toMat := by
  rw [‚Üê sub_nonneg, ‚Üê mul_sub_left_distrib]
  exact proj_le_mul_nonneg A B

theorem proj_le_inner_nonneg : 0 ‚â§ {A ‚â§‚Çö B}.inner (B - A) :=
  HermitianMat.inner_mul_nonneg (proj_le_mul_nonneg A B)

theorem proj_le_inner_le : {A ‚â§‚Çö B}.inner A ‚â§ {A ‚â§‚Çö B}.inner B := by
  rw [‚Üê sub_nonneg, ‚Üê HermitianMat.inner_left_sub]
  exact proj_le_inner_nonneg A B
