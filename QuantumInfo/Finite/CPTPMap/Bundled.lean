import QuantumInfo.Finite.CPTPMap.Unbundled
import QuantumInfo.Finite.MState

import Mathlib.Topology.Order.Hom.Basic

/-! # Classes of Matrix Maps

The bundled `MatrixMap`s: `HPMap`, `UnitalMap`, `TPMap`, `PMap`, and `CPMap`.
These are defined over the bare minimum rings (`Semiring` or `RCLike`, respectively).

The combinations `PTPMap` (positive trace-preserving), `CPTPMap`, and `CPUMap`
(CP unital maps) take â„‚ as the default class.

The majority of quantum theory revolves around `CPTPMap`s, so those are explored more
thoroughly in their file CPTP.lean.
-/

--PULLOUT
open ComplexOrder in
theorem Matrix.PosSemidef.trace_pos {n ğ•œ : Type*} [Fintype n] [RCLike ğ•œ]
    {A : Matrix n n ğ•œ} (hA : A.PosSemidef) (h : A â‰  0) : 0 < A.trace := by
  classical
  apply hA.trace_nonneg.lt_of_ne'
  rw [hA.left.trace_eq_sum_eigenvalues]
  suffices âˆ‘ i, hA.left.eigenvalues i â‰  0 from mod_cast this
  rwa [ne_eq, Fintype.sum_eq_zero_iff_of_nonneg hA.eigenvalues_nonneg,
    hA.left.eigenvalues_eq_zero_iff]

--PULLOUT
open ComplexOrder in
theorem HermitianMat.trace_pos {n ğ•œ : Type*} [Fintype n] [RCLike ğ•œ]
    {A : HermitianMat n ğ•œ} (hA : 0 < A) : 0 < A.trace := by
  have hA' := hA.le
  rw [HermitianMat.zero_le_iff] at hA'
  have h_pos := Matrix.PosSemidef.trace_pos hA' (by simpa using hA.ne')
  rw [HermitianMat.trace_eq_re_trace]
  rw [RCLike.pos_iff] at h_pos
  exact h_pos.left

variable (dIn dOut R : Type*) (ğ•œ : Type := â„‚)
variable [Fintype dIn] [Fintype dOut]
variable [Semiring R] [RCLike ğ•œ]

/-- Hermitian-preserving linear maps. -/
structure HPMap extends MatrixMap dIn dOut ğ•œ where
  HP : MatrixMap.IsHermitianPreserving toLinearMap

/-- Unital linear maps. -/
structure UnitalMap [DecidableEq dIn] [DecidableEq dOut] extends MatrixMap dIn dOut R where
  unital : MatrixMap.Unital toLinearMap

/-- Trace-preserving linear maps. -/
structure TPMap extends MatrixMap dIn dOut R where
  TP : MatrixMap.IsTracePreserving toLinearMap

--Mark this as [simp] so that simp lemmas requiring `IsTracePreserving` can pick it up.
--In theory this could be making "IsTracePreserving" a typeclass ... or more realistically,
--defining a `TracePreservingClass` similar to `AddHomClass`
attribute [simp] TPMap.TP

/-- Positive linear maps. -/
structure PMap extends HPMap dIn dOut ğ•œ where
  pos : MatrixMap.IsPositive toLinearMap
  HP := pos.IsHermitianPreserving

/-- Completely positive linear maps. -/
structure CPMap [DecidableEq dIn] extends PMap dIn dOut ğ•œ where
  cp : MatrixMap.IsCompletelyPositive toLinearMap
  pos := cp.IsPositive

/-- Positive trace-preserving linear maps. These includes all channels, but aren't
  necessarily *completely* positive, see `CPTPMap`. -/
structure PTPMap extends PMap dIn dOut ğ•œ, TPMap dIn dOut ğ•œ

attribute [simp] PTPMap.TP

/-- Completely positive trace-preserving linear maps. This is the most common
  meaning of "channel", often described as "the most general physically realizable
  quantum operation". -/
structure CPTPMap [DecidableEq dIn] extends PTPMap dIn dOut (ğ•œ := ğ•œ), CPMap dIn dOut ğ•œ where

/-- Completely positive unital maps. These are important because they are the
  dual to `CPTPMap`: they are the most general way to map *observables*. -/
structure CPUMap [DecidableEq dIn] [DecidableEq dOut] extends CPMap dIn dOut ğ•œ, UnitalMap dIn dOut ğ•œ

variable {dIn dOut R} {ğ•œ : Type} [RCLike ğ•œ]

--Hermitian-presering maps: continuous linear maps on HermitianMats.
namespace HPMap
omit [Fintype dIn] [Fintype dOut]
variable {Î›â‚ Î›â‚‚ : HPMap dIn dOut ğ•œ}
variable {CÎ›â‚ CÎ›â‚‚ : HPMap dIn dOut â„‚}

abbrev map (M : HPMap dIn dOut ğ•œ) : MatrixMap dIn dOut ğ•œ := M.toLinearMap

@[ext]
theorem ext (h : Î›â‚.map = Î›â‚‚.map) : Î›â‚ = Î›â‚‚ := by
  rwa [HPMap.mk.injEq]

/-- Two maps are equal if they agree on all Hermitian inputs. -/
theorem funext_hermitian (h : âˆ€ M : HermitianMat dIn â„‚, CÎ›â‚.map M = CÎ›â‚‚.map M) :
    CÎ›â‚ = CÎ›â‚‚ := by
  ext M : 2
  have hH := h (realPart M)
  have hA := h (imaginaryPart M)
  convert congr($hH + Complex.I â€¢ $hA)
  <;> rw (occs := [1]) [â† realPart_add_I_smul_imaginaryPart M, map_add, map_smul]


/-- Two maps are equal if they agree on all positive inputs. -/
theorem funext_pos [Fintype dIn] (h : âˆ€ M : HermitianMat dIn â„‚, 0 â‰¤ M â†’ CÎ›â‚.map M = CÎ›â‚‚.map M) :
    CÎ›â‚ = CÎ›â‚‚ := by
  classical
  open scoped HermitianMat in
  apply funext_hermitian
  intro M
  have hPos := h Mâº M.zero_le_posPart
  have hNeg := h Mâ» M.negPart_le_zero --TODO: this is named incorrectly
  rw [â† M.posPart_add_negPart]
  simp [hPos, hNeg]

/-- Two maps are equal if they agree on all positive inputs with trace one -/
theorem funext_pos_trace [Fintype dIn]
  (h : âˆ€ M : HermitianMat dIn â„‚, 0 â‰¤ M â†’ M.trace = 1 â†’ CÎ›â‚.map M = CÎ›â‚‚.map M) :
    CÎ›â‚ = CÎ›â‚‚ := by
  apply funext_pos
  intro M hM'
  rcases hM'.eq_or_lt with rfl | hM
  Â· simp
  have h_tr : 0 < M.trace := M.trace_pos hM
  have := h (M.traceâ»Â¹ â€¢ M) ?_ ?_
  Â· simp only [selfAdjoint.val_smul, LinearMap.map_smul_of_tower] at this
    convert congr(M.trace â€¢ $this)
    Â· rw [smul_smul]
      field_simp
      simp
    Â· rw [smul_smul]
      field_simp
      simp
  Â· apply smul_nonneg (by positivity) hM'
  Â· simp [field]

/-- Two maps are equal if they agree on all `MState`s. -/
theorem funext_mstate [Fintype dIn] [DecidableEq dIn] {Î›â‚ Î›â‚‚ : HPMap dIn dOut â„‚}
  (h : âˆ€ Ï : MState dIn, Î›â‚.map Ï.m = Î›â‚‚.map Ï.m) :
    Î›â‚ = Î›â‚‚ :=
  funext_pos_trace fun M hM_pos hM_tr â†¦ h âŸ¨M, hM_pos, hM_trâŸ©

/-- Hermitian-preserving maps are functions from `HermitianMat`s to `HermitianMat`s. -/
instance instFunLike : FunLike (HPMap dIn dOut â„‚) (HermitianMat dIn â„‚) (HermitianMat dOut â„‚) where
  coe Î› Ï := âŸ¨Î›.map Ï.1, Î›.HP Ï.2âŸ©
  coe_injective' x y h := funext_hermitian fun M â†¦
    by simpa using congrFun h M

instance : ContinuousLinearMapClass
    (HPMap dIn dOut â„‚) â„ (HermitianMat dIn â„‚) (HermitianMat dOut â„‚) where
  map_add f x y := HermitianMat.ext <| LinearMap.map_add f.toLinearMap x y
  map_smulâ‚›â‚— f c x := HermitianMat.ext <| by simp [instFunLike]
  map_continuous f := .subtype_mk (by fun_prop) _

end HPMap

--Positive-preserving maps: continuous linear order-preserving maps on HermitianMats.
namespace PMap

@[ext]
theorem ext {Î›â‚ Î›â‚‚ : PMap dIn dOut ğ•œ} (h : Î›â‚.map = Î›â‚‚.map) : Î›â‚ = Î›â‚‚ := by
  rw [PMap.mk.injEq]
  exact HPMap.ext h

theorem injective_toHPMap : (PMap.toHPMap (dIn := dIn) (dOut := dOut) (ğ•œ := ğ•œ)).Injective :=
  fun _ _ â†¦ (mk.injEq _ _ _ _).mpr

/-- Positive maps are functions from `HermitianMat`s to `HermitianMat`s. -/
instance instFunLike : FunLike (PMap dIn dOut â„‚) (HermitianMat dIn â„‚) (HermitianMat dOut â„‚) where
  coe := DFunLike.coe âˆ˜ toHPMap
  coe_injective' := DFunLike.coe_injective'.comp injective_toHPMap

set_option synthInstance.maxHeartbeats 40000 in
instance instLinearMapClass : LinearMapClass (PMap dIn dOut â„‚) â„ (HermitianMat dIn â„‚) (HermitianMat dOut â„‚) where
  map_add f x y := HermitianMat.ext <| LinearMap.map_add f.toLinearMap x y
  map_smulâ‚›â‚— f c x := HermitianMat.ext <| by simp [instFunLike]

instance instContinuousOrderHomClass : ContinuousOrderHomClass (PMap dIn dOut â„‚)
    (HermitianMat dIn â„‚) (HermitianMat dOut â„‚) where
  map_continuous f := ContinuousMapClass.map_continuous f.toHPMap
  map_monotone f x y h := by
    simpa using f.pos h

/-- Positive-presering maps also preserve positivity on, specifically, Hermitian matrices. -/
@[simp]
theorem pos_Hermitian (M : PMap dIn dOut â„‚) {x : HermitianMat dIn â„‚} (h : 0 â‰¤ x) : 0 â‰¤ M x := by
  simpa only [map_zero] using ContinuousOrderHomClass.map_monotone M h

end PMap

namespace CPMap

def of_kraus_CPMap {Îº : Type*} [Fintype Îº] [DecidableEq dIn] (M : Îº â†’ Matrix dOut dIn ğ•œ) : CPMap dIn dOut ğ•œ where
  toLinearMap := MatrixMap.of_kraus M M
  cp := MatrixMap.IsCompletelyPositive.of_kraus_isCompletelyPositive M

end CPMap

--Positive trace-preserving maps:
--  * Continuous linear order-preserving maps on HermitianMats.
--  * Continuous maps on MStates.
namespace PTPMap

@[ext]
theorem ext {Î›â‚ Î›â‚‚ : PTPMap dIn dOut ğ•œ} (h : Î›â‚.map = Î›â‚‚.map) : Î›â‚ = Î›â‚‚ := by
  rw [PTPMap.mk.injEq]
  exact PMap.ext h

theorem injective_toPMap : (PTPMap.toPMap (dIn := dIn) (dOut := dOut) (ğ•œ := ğ•œ)).Injective :=
  fun _ _ â†¦ (mk.injEq _ _ _ _).mpr

/-- Positive trace-preserving maps are functions from `HermitianMat`s to `HermitianMat`s. -/
instance instFunLike : FunLike (PTPMap dIn dOut â„‚) (HermitianMat dIn â„‚) (HermitianMat dOut â„‚) where
  coe := DFunLike.coe âˆ˜ toPMap
  coe_injective' := DFunLike.coe_injective'.comp injective_toPMap

instance instLinearMapClass : LinearMapClass (PTPMap dIn dOut â„‚) â„ (HermitianMat dIn â„‚) (HermitianMat dOut â„‚) where
  map_add f x y := by simp [instFunLike]
  map_smulâ‚›â‚— f c x := by simp [instFunLike]

instance instHContinuousOrderHomClass : ContinuousOrderHomClass (PTPMap dIn dOut â„‚)
    (HermitianMat dIn â„‚) (HermitianMat dOut â„‚) where
  map_continuous f := ContinuousMapClass.map_continuous f.toPMap
  map_monotone f x y h := by
    simpa using f.pos h

/-- PTP maps also preserve positivity on Hermitian matrices. -/
@[simp]
theorem pos_Hermitian (M : PTPMap dIn dOut â„‚) {x : HermitianMat dIn â„‚} (h : 0 â‰¤ x) : 0 â‰¤ M x := by
  simpa only [map_zero] using ContinuousOrderHomClass.map_monotone M h

/-- `PTPMap`s are functions from `MState`s to `MState`s. -/
instance instMFunLike [DecidableEq dIn] [DecidableEq dOut] :
    FunLike (PTPMap dIn dOut) (MState dIn) (MState dOut) where
  coe Î› Ï := MState.mk
    (Î›.toHPMap Ï.M) (HermitianMat.zero_le_iff.mpr (Î›.pos Ï.pos)) (by
      rw [HermitianMat.trace_eq_one_iff, â† Ï.tr']
      exact Î›.TP Ï)
  coe_injective' x y h := injective_toPMap <| PMap.injective_toHPMap <|
    HPMap.funext_mstate fun Ï â†¦ by
      have := congr($h Ï);
      rwa [MState.ext_iff, HermitianMat.ext_iff] at this

instance instMContinuousMapClass [DecidableEq dIn] [DecidableEq dOut] :
    ContinuousMapClass (PTPMap dIn dOut) (MState dIn) (MState dOut) where
  map_continuous f := by
    rw [continuous_induced_rng]
    exact (map_continuous f.toHPMap).comp MState.Continuous_HermitianMat

-- @[norm_cast]
theorem val_apply_MState [DecidableEq dIn] (M : PTPMap dIn dOut) (Ï : MState dIn) :
    (M Ï : HermitianMat dOut â„‚) = (instFunLike.coe M) Ï := by
  rfl

--If we have a PTPMap, the input and output dimensions are always both nonempty (otherwise
--we can't preserve trace) - or they're both empty. So `[Nonempty dIn]` will always suffice.
-- This would be nice as an `instance` but that would leave `dIn` as a metavariable.
theorem nonemptyOut (Î› : PTPMap dIn dOut) [hIn : Nonempty dIn] [DecidableEq dIn] : Nonempty dOut := by
  by_contra h
  simp only [not_nonempty_iff] at h
  let M := (1 : Matrix dIn dIn â„‚)
  have := calc (Finset.univ.card (Î± := dIn) : â„‚)
    _ = M.trace := by simp [Matrix.trace, M]
    _ = (Î›.map M).trace := (Î›.TP M).symm
    _ = 0 := by simp only [Matrix.trace_eq_zero_of_isEmpty]
  norm_num [Finset.univ_eq_empty_iff] at this

end PTPMap

namespace CPTPMap
variable [DecidableEq dIn]

/-- Two `CPTPMap`s are equal if their `MatrixMap`s are equal. -/
@[ext]
theorem ext {Î›â‚ Î›â‚‚ : CPTPMap dIn dOut ğ•œ} (h : Î›â‚.map = Î›â‚‚.map) : Î›â‚ = Î›â‚‚ := by
  rw [CPTPMap.mk.injEq]
  exact PTPMap.ext h

theorem injective_toPTPMap : (CPTPMap.toPTPMap (dIn := dIn) (dOut := dOut) (ğ•œ := ğ•œ)).Injective :=
  fun _ _ â†¦ (mk.injEq _ _ _ _).mpr

-- /-- Positive trace-preserving maps are functions from `HermitianMat`s to `HermitianMat`s. -/
-- instance instFunLike : FunLike (CPTPMap dIn dOut ğ•œ) (HermitianMat dIn ğ•œ) (HermitianMat dOut ğ•œ) where
--   coe :=  DFunLike.coe âˆ˜ toPTPMap
--   coe_injective' := DFunLike.coe_injective'.comp injective_toPTPMap

-- set_option synthInstance.maxHeartbeats 40000 in
-- instance instLinearMapClass : LinearMapClass (CPTPMap dIn dOut ğ•œ) â„ (HermitianMat dIn ğ•œ) (HermitianMat dOut ğ•œ) where
--   map_add f x y := by simp [instFunLike]
--   map_smulâ‚›â‚— f c x := by simp [instFunLike]

-- instance instContinuousOrderHomClass : ContinuousOrderHomClass (CPTPMap dIn dOut ğ•œ)
--     (HermitianMat dIn ğ•œ) (HermitianMat dOut ğ•œ) where
--   map_continuous f := ContinuousMapClass.map_continuous f.toPMap
--   map_monotone f x y h := by
    -- simpa using f.pos h

-- /-- PTP maps also preserve positivity on Hermitian matrices. -/
-- @[simp]
-- theorem pos_Hermitian (M : CPTPMap dIn dOut ğ•œ) {x : HermitianMat dIn ğ•œ} (h : 0 â‰¤ x) : 0 â‰¤ M x := by
--   simpa only [map_zero] using ContinuousOrderHomClass.map_monotone M h

/-- `CPTPMap`s are functions from `MState`s to `MState`s. -/
instance instMFunLike [DecidableEq dOut] : FunLike (CPTPMap dIn dOut) (MState dIn) (MState dOut) where
  coe := DFunLike.coe âˆ˜ toPTPMap
  coe_injective' := DFunLike.coe_injective'.comp injective_toPTPMap

-- @[norm_cast]
-- theorem val_apply_MState [DecidableEq dOut] (M : CPTPMap dIn dOut) (Ï : MState dIn) :
--     (M Ï : HermitianMat dOut â„‚) = (instFunLike.coe M) Ï := by
--   rfl

@[simp]
theorem IsTracePreserving (Î› : CPTPMap dIn dOut ğ•œ) : Î›.map.IsTracePreserving :=
  Î›.TP

def of_kraus_CPTPMap {Îº : Type*} [Fintype Îº] [DecidableEq dIn]
  (M : Îº â†’ Matrix dOut dIn ğ•œ)
  (hTP : (âˆ‘ k, (M k).conjTranspose * (M k)) = 1) : CPTPMap dIn dOut ğ•œ where
  toLinearMap := MatrixMap.of_kraus M M
  cp := MatrixMap.IsCompletelyPositive.of_kraus_isCompletelyPositive M
  TP := MatrixMap.IsTracePreserving.of_kraus_isTracePreserving M M hTP

end CPTPMap

namespace CPUMap
variable [DecidableEq dIn] [DecidableEq dOut]

@[ext]
theorem ext {Î›â‚ Î›â‚‚ : CPUMap dIn dOut ğ•œ} (h : Î›â‚.map = Î›â‚‚.map) : Î›â‚ = Î›â‚‚ := by
  rw [CPUMap.mk.injEq, CPMap.mk.injEq]
  exact PMap.ext h

theorem injective_toPMap : (CPMap.toPMap âˆ˜ CPUMap.toCPMap (dIn := dIn) (dOut := dOut) (ğ•œ := ğ•œ)).Injective := by
  intro _ _ _
  rwa [CPUMap.mk.injEq, CPMap.mk.injEq]

/-- `CPUMap`s are functions from `HermitianMat`s to `HermitianMat`s. -/
instance instFunLike : FunLike (CPUMap dIn dOut â„‚) (HermitianMat dIn â„‚) (HermitianMat dOut â„‚) where
  coe Î› := Î›.toPMap
  coe_injective' := (DFunLike.coe_injective' (F := PMap dIn dOut â„‚)).comp injective_toPMap

instance instLinearMapClass : LinearMapClass (CPUMap dIn dOut â„‚) â„ (HermitianMat dIn â„‚) (HermitianMat dOut â„‚) where
  map_add f x y := HermitianMat.ext <| LinearMap.map_add f.toLinearMap x y
  map_smulâ‚›â‚— f c x := HermitianMat.ext <| by simp [instFunLike]

instance instHContinuousOrderHomClass : ContinuousOrderHomClass (CPUMap dIn dOut â„‚)
    (HermitianMat dIn â„‚) (HermitianMat dOut â„‚) where
  map_continuous f := ContinuousMapClass.map_continuous f.toPMap
  map_monotone f x y h := by
    simpa using f.pos h

instance instOneHomClass : OneHomClass (CPUMap dIn dOut â„‚)
    (HermitianMat dIn â„‚) (HermitianMat dOut â„‚) where
  map_one f := HermitianMat.ext (f.unital)

/-- CPTP maps also preserve positivity on Hermitian matrices. -/
@[simp]
theorem pos_Hermitian (M : CPUMap dIn dOut â„‚) {x : HermitianMat dIn â„‚} (h : 0 â‰¤ x) : 0 â‰¤ M x := by
  simpa only [map_zero] using ContinuousOrderHomClass.map_monotone M h

end CPUMap


--Tests to make sure that our `simp`s and classe are all working like we want them too

section test
variable [DecidableEq dIn] [DecidableEq dOut]

#guard_msgs in
example (M : HPMap dIn dOut â„‚) : (M (Real.pi â€¢ 1)) = Real.pi â€¢ M 1 := by simp

#guard_msgs in
example (M : PTPMap dIn dOut â„‚) : (M.toHPMap (Real.pi â€¢ 1)) = Real.pi â€¢ M.toHPMap 1 := by simp

#guard_msgs in
example (M : CPTPMap dIn dOut ğ•œ) (Ï : Matrix dIn dIn ğ•œ) : (M.map Ï).trace = Ï.trace := by simp

#guard_msgs in
example (M : CPUMap dIn dOut â„‚) (T : HermitianMat dIn â„‚) : M (1 + 2 â€¢ T) = 1 + 2 â€¢ M T := by simp

end test
