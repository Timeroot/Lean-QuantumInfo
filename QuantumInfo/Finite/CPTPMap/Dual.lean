/-
Copyright (c) 2025 Alex Meiburg. All rights reserved.
Released under MIT license as described in the file LICENSE.
Authors: Alex Meiburg
-/
import QuantumInfo.Finite.CPTPMap.Bundled
import Mathlib.LinearAlgebra.Matrix.FiniteDimensional

/-! # Duals of matrix map

Definitions and theorems about the dual of a matrix map. -/

noncomputable section
open ComplexOrder

variable {dIn dOut : Type*} [Fintype dIn] [Fintype dOut]
variable {R : Type*} [CommRing R]
variable {ğ•œ : Type*} [RCLike ğ•œ]

--PULLOUT
theorem HermitianMat.toMat_add (x y : HermitianMat d â„‚) : (x + y).toMat = x.toMat + y.toMat := by
  rfl

namespace MatrixMap

variable [DecidableEq dIn] [DecidableEq dOut] {M : MatrixMap dIn dOut ğ•œ}

--This should be definable with LinearMap.adjoint, but that requires InnerProductSpace stuff
--that is currently causing issues and pains (tried `open scoped Frobenius`).

/-- The dual of a map between matrices, defined by `Tr[A M(B)] = Tr[(dual M)(A) B]`. Sometimes
 called the adjoint of the map instead. -/
@[irreducible]
def dual (M : MatrixMap dIn dOut R) : MatrixMap dOut dIn R :=
  let iso1 := (Module.Basis.toDualEquiv <| Matrix.stdBasis R dIn dIn).symm
  let iso2 := (Module.Basis.toDualEquiv <| Matrix.stdBasis R dOut dOut)
  iso1 âˆ˜â‚— LinearMap.dualMap M âˆ˜â‚— iso2

/-- The defining property of a dual map: inner products are preserved on the opposite argument. -/
theorem Dual.trace_eq (M : MatrixMap dIn dOut R) (A : Matrix dIn dIn R) (B : Matrix dOut dOut R) :
    (M A * B).trace = (A * M.dual B).trace := by
  unfold dual
  dsimp [Matrix.trace]
  rw [LinearMap.dualMap_apply']
  simp_rw [Matrix.mul_apply]
  sorry

--all properties below should provable just from `inner_eq`, since the definition of `dual` itself
-- is pretty hair (and maybe could be improved...)

/-- The dual of a `IsHermitianPreserving` map also `IsHermitianPreserving`. -/
theorem IsHermitianPreserving.dual (h : M.IsHermitianPreserving) : M.dual.IsHermitianPreserving := by
  sorry

/-- The dual of a `IsPositive` map also `IsPositive`. -/
theorem IsPositive.dual (h : M.IsPositive) : M.dual.IsPositive := by
  intro x hx
  use IsHermitianPreserving.dual h.IsHermitianPreserving hx.1
  sorry

/-- The dual of TracePreserving map is *not* trace-preserving, it's *unital*, that is, M*(I) = I. -/
theorem dual_Unital (h : M.IsTracePreserving) : M.dual.Unital := by
  sorry

alias IsTracePreserving.dual := dual_Unital

--The dual of a CompletelyPositive map is always CP, more generally it's k-positive
-- see Lemma 3.1 of https://www.math.uwaterloo.ca/~krdavids/Preprints/CDPRpositivereal.pdf
theorem IsCompletelyPositive.dual (h : M.IsCompletelyPositive) : M.dual.IsCompletelyPositive := by
  sorry

@[simp]
theorem dual_dual : M.dual.dual = M := by
  rw [dual, dual]
  simp only [â† LinearMap.dualMap_comp_dualMap]
  have hâ‚ : (Matrix.stdBasis ğ•œ dOut dOut).toDualEquiv.symm.toLinearMap âˆ˜â‚—
      ((Matrix.stdBasis ğ•œ dOut dOut).toDualEquiv).toLinearMap.dualMap =
      (Module.evalEquiv ğ•œ (Matrix dOut dOut ğ•œ)).symm.toLinearMap := by
    sorry
  have hâ‚‚ : (Matrix.stdBasis ğ•œ dIn dIn).toDualEquiv.symm.toLinearMap.dualMap âˆ˜â‚—
      (Matrix.stdBasis ğ•œ dIn dIn).toDualEquiv.toLinearMap =
      (Module.evalEquiv ğ•œ (Matrix dIn dIn ğ•œ)).toLinearMap := by
    ext x y
    simp
    generalize Matrix.stdBasis ğ•œ dIn dIn = L
    sorry
  rw [â† Module.Dual.eval_comp_comp_evalEquiv_eq]
  rw [â† Module.evalEquiv_toLinearMap]
  simp only [â† LinearMap.comp_assoc, LinearEquiv.comp_coe, LinearEquiv.self_trans_symm, LinearEquiv.refl_toLinearMap,
    LinearMap.id_comp, hâ‚]
  simp only [LinearMap.comp_assoc, LinearEquiv.comp_coe, LinearEquiv.self_trans_symm, LinearEquiv.refl_toLinearMap,
    LinearMap.comp_id, hâ‚‚]

end MatrixMap

namespace CPTPMap

variable [DecidableEq dIn] [DecidableEq dOut]

def dual (M : CPTPMap dIn dOut) : CPUMap dOut dIn where
  toLinearMap := M.map.dual
  unital := M.TP.dual
  cp := .dual M.cp

theorem dual_pos (M : CPTPMap dIn dOut) {T : HermitianMat dOut â„‚} (hT : 0 â‰¤ T) :
    0 â‰¤ M.dual T := by
  exact M.dual.pos_Hermitian hT

/-- The dual of a CPTP map preserves POVMs. Stated here just for two-element POVMs, that is, an
operator `T` between 0 and 1. -/
theorem dual.PTP_POVM (M : CPTPMap dIn dOut) {T : HermitianMat dOut â„‚} (hT : 0 â‰¤ T âˆ§ T â‰¤ 1) :
    (0 â‰¤ M.dual T âˆ§ M.dual T â‰¤ 1) := by
  rcases hT with âŸ¨hTâ‚, hTâ‚‚âŸ©
  have hT_psd := HermitianMat.zero_le_iff.mp hTâ‚
  use M.dual.pos_Hermitian hTâ‚
  simpa using ContinuousOrderHomClass.map_monotone M.dual hTâ‚‚

/-- The defining property of a dual channel, as specialized to `MState.exp_val`. -/
theorem exp_val_Dual (â„° : CPTPMap dIn dOut) (Ï : MState dIn) (T : HermitianMat dOut â„‚) :
    (â„° Ï).exp_val T  = Ï.exp_val (â„°.dual T) := by
  simp only [MState.exp_val, HermitianMat.inner_eq_re_trace,
    HermitianMat.val_eq_coe, RCLike.re_to_complex]
  congr 1
  apply MatrixMap.Dual.trace_eq

end CPTPMap

section hermDual

--PULLOUT to Bundled.lean. Also use this to improve the definitions in POVM.lean.
def HPMap.ofHermitianMat (f : HermitianMat dIn â„‚ â†’â‚—[â„] HermitianMat dOut â„‚) : HPMap dIn dOut where
  toFun x := f (realPart x) + Complex.I â€¢ f (imaginaryPart x)
  map_add' x y := by
    simp only [map_add, AddSubgroup.coe_add, smul_add]
    abel
  map_smul' c m := by
    have h_expand : realPart (c â€¢ m) = c.re â€¢ realPart m - c.im â€¢ imaginaryPart m âˆ§
      imaginaryPart (c â€¢ m) = c.re â€¢ imaginaryPart m + c.im â€¢ realPart m := by
      simp only [Subtype.ext_iff, AddSubgroupClass.coe_sub, selfAdjoint.val_smul,
        AddSubgroup.coe_add, realPart, selfAdjointPart_apply_coe, invOf_eq_inv, star_smul, RCLike.star_def,
        smul_add, imaginaryPart, LinearMap.coe_comp, Function.comp_apply,
        skewAdjoint.negISMul_apply_coe, skewAdjointPart_apply_coe,
        â† Matrix.ext_iff, Matrix.add_apply, Matrix.smul_apply, smul_eq_mul, Complex.real_smul,
        Complex.ofReal_inv, Complex.ofReal_ofNat, Matrix.star_apply, RCLike.star_def,
        Matrix.sub_apply, Complex.ext_iff, Complex.add_re, Complex.mul_re, Complex.inv_re,
        Complex.normSq_ofNat, Complex.mul_im, Complex.conj_re, Complex.conj_im, Complex.ofReal_re,
        Complex.sub_re, Complex.sub_im, Complex.add_im, Complex.neg_re, Complex.neg_im]
      ring_nf
      simp
    ext
    simp only [h_expand, map_sub, map_smul, AddSubgroupClass.coe_sub,
      selfAdjoint.val_smul, map_add, AddSubgroup.coe_add, smul_add, Matrix.add_apply,
      Matrix.sub_apply, Matrix.smul_apply, Complex.real_smul, smul_eq_mul, RingHom.id_apply,
      Complex.ext_iff, Complex.add_re, Complex.sub_re, Complex.mul_re, Complex.ofReal_re,
      Complex.ofReal_im, Complex.I, Complex.mul_im, Complex.add_im, Complex.sub_im]
    ring_nf
    simp
  HP _ h := by
    apply Matrix.IsHermitian.add
    Â· apply HermitianMat.H
    Â· simp [IsSelfAdjoint.imaginaryPart h]

--PULLOUT
@[simp]
theorem HPMap.linearMap_ofHermitianMat (f : HermitianMat dIn â„‚ â†’â‚—[â„] HermitianMat dOut â„‚) :
    LinearMapClass.linearMap (HPMap.ofHermitianMat f) = f := by
  ext1 âŸ¨x, hxâŸ©
  ext1
  simp only [ofHermitianMat, LinearMap.coe_coe]
  simp only [HPMap.instFunLike, HPMap.map, HermitianMat.val_eq_coe, HermitianMat.mk_toMat,
    LinearMap.coe_mk, AddHom.coe_mk]
  conv => enter [2, 1, 2, 1]; rw [â† realPart_add_I_smul_imaginaryPart x]
  suffices imaginaryPart x = 0 by simp [this]
  simp [imaginaryPart, skewAdjoint.negISMul, show star x = x from hx]

--PULLOUT
@[simp]
theorem HPMap.ofHermitianMat_linearMap (f : HPMap dIn dOut â„‚) :
    ofHermitianMat (LinearMapClass.linearMap f) = f := by
  ext : 2
  simp only [map, ofHermitianMat, instFunLike, LinearMap.coe_coe, HermitianMat.val_eq_coe,
    HermitianMat.mk_toMat, LinearMap.coe_mk, AddHom.coe_mk,
    â† map_smul, â† map_add, realPart_add_I_smul_imaginaryPart]

variable (f : HPMap dIn dOut) (A : HermitianMat dIn â„‚)

--Can define one for HPMap's that has 'easier' definitional properties, uses the inner product structure,
--doesn't go through Module.Basis the same way. Requires the equivalence between â„-linear maps of HermitianMats
--and â„‚-linear maps of matrices.
def HPMap.hermDual : HPMap dOut dIn :=
  HPMap.ofHermitianMat (LinearMapClass.linearMap f).adjoint

@[simp]
theorem HPMap.hermDual_hermDual : f.hermDual.hermDual = f := by
  simp [hermDual]

open RealInnerProductSpace in
/-- The defining property of a dual map: inner products are preserved on the opposite argument. -/
theorem HPMap.inner_hermDual (B : HermitianMat dOut â„‚) :
    âŸªf A, BâŸ« = âŸªA, f.hermDual BâŸ« := by
  change âŸª(LinearMapClass.linearMap f) A, BâŸ« = âŸªA, (LinearMapClass.linearMap f.hermDual) BâŸ«
  rw [hermDual, â† LinearMap.adjoint_inner_right, HPMap.linearMap_ofHermitianMat]

/-- Version of `HPMap.inner_hermDual` that uses HermitiaMat.inner directly. TODO cleanup -/
theorem HPMap.inner_hermDual' (B : HermitianMat dOut â„‚) :
    (f A).inner B = A.inner (f.hermDual B) :=
  HPMap.inner_hermDual f A B

open RealInnerProductSpace in
theorem HermitianMat.inner_negPart_nonpos [DecidableEq dIn] : âŸªA, Aâ»âŸ« â‰¤ 0 := by
  rw [â† neg_le_neg_iff, neg_zero, â† inner_neg_right]
  apply inner_mul_nonneg
  nth_rw 1 [â† A.cfc_id]
  rw [negPart_eq_cfc_ite]
  rw [â† cfc_neg]
  rw [â† coe_cfc_mul]
  change 0 â‰¤ A.cfc _
  rw [zero_le_cfc]
  intro i
  dsimp
  split_ifs with h
  Â· rw [neg_neg]
    exact mul_self_nonneg _
  Â· simp

theorem HermitianMat.cfc_eq_cfc_iff_eqOn [DecidableEq dIn] (A : HermitianMat dIn â„‚) (f g : â„ â†’ â„) :
    cfc A f = cfc A g â†” Set.EqOn f g (spectrum â„ A.toMat) := by
  rw [HermitianMat.ext_iff, cfc_toMat, cfc_toMat]
  exact _root_.cfc_eq_cfc_iff_eqOn A.H herm_cont herm_cont

open RealInnerProductSpace in
theorem HermitianMat.inner_negPart_zero_iff [DecidableEq dIn] : âŸªA, Aâ»âŸ« = 0 â†” 0 â‰¤ A := by
  constructor
  Â· intro h
    have h2 : âŸªAâº, Aâ»âŸ« = 0 := by
      --TODO own thm?
      have hi := inner_eq_trace_rc Aâº Aâ»
      rw [posPart_mul_negPart, Matrix.trace_zero] at hi
      exact congr(Complex.re ($hi))
    nth_rw 1 [â† posPart_add_negPart A] at h
    rw [inner_sub_left, sub_eq_zero, h2, eq_comm, inner_self_eq_zero] at h
    rw [â† zero_smul â„ 1, â† cfc_const A, negPart_eq_cfc_ite] at h --TODO cfc_zero
    rw [cfc_eq_cfc_iff_eqOn, A.H.spectrum_real_eq_range_eigenvalues] at h
    simp only [Set.eqOn_range] at h
    replace h (i) := congrFun h i
    simp only [Function.comp_apply, ite_eq_right_iff, neg_eq_zero] at h
    rw [zero_le_iff, A.H.posSemidef_iff_eigenvalues_nonneg]
    intro i
    contrapose! h
    use i, h.le, h.ne
  Â· intro h
    apply le_antisymm
    Â· exact inner_negPart_nonpos A
    Â· exact inner_ge_zero' _ _ h (negPart_le_zero A)

open RealInnerProductSpace in
theorem HermitianMat.inner_negPart_neg_iff [DecidableEq dIn] : âŸªA, Aâ»âŸ« < 0 â†” Â¬0 â‰¤ A := by
  simp [â† inner_negPart_zero_iff, lt_iff_le_and_ne, inner_negPart_nonpos A]

--PULLOUT
open RealInnerProductSpace in
theorem HermitianMat.zero_le_iff_inner_pos (A : HermitianMat dIn â„‚) :
    0 â‰¤ A â†” âˆ€ B, 0 â‰¤ B â†’ 0 â‰¤ âŸªA, BâŸ« := by
  use fun h _ â†¦ inner_ge_zero h
  intro h
  contrapose! h
  classical
  use Aâ», negPart_le_zero A
  rwa [inner_negPart_neg_iff]

/-- The dual of a `IsPositive` map also `IsPositive`. -/
theorem MatrixMap.IsPositive.hermDual (h : MatrixMap.IsPositive f.map) : f.hermDual.map.IsPositive := by
  unfold IsPositive at h âŠ¢
  intro x hx
  set xH : HermitianMat dOut â„‚ := âŸ¨x, hx.leftâŸ© with hxH
  have hx' : x = xH := rfl; clear_value xH; subst x; clear hxH
  change Matrix.PosSemidef (f.hermDual xH).toMat
  rw [â† HermitianMat.zero_le_iff] at hx âŠ¢
  rw [HermitianMat.zero_le_iff_inner_pos]
  intro y hy
  rw [HermitianMat.zero_le_iff] at hy
  specialize h hy
  change Matrix.PosSemidef (f y).toMat at h
  rw [â† HermitianMat.zero_le_iff] at h
  rw [HPMap.inner_hermDual, HPMap.hermDual_hermDual]
  apply HermitianMat.inner_ge_zero hx h

/-- The dual of TracePreserving map is *not* trace-preserving, it's *unital*, that is, M*(I) = I. -/
theorem HPMap.hermDual_Unital [DecidableEq dIn] [DecidableEq dOut] (h : MatrixMap.IsTracePreserving f.map) :
    f.hermDual.map.Unital := by
  suffices f.hermDual 1 = 1 by --todo: make this is an accessible 'constructor' for Unital
    rw [HermitianMat.ext_iff] at this
    exact this
  open RealInnerProductSpace in
  apply ext_inner_left â„
  intro v
  rw [â† HPMap.inner_hermDual]
  change HermitianMat.inner _ _ = HermitianMat.inner _ _
  rw [HermitianMat.inner_one, HermitianMat.inner_one] --TODO change to Inner.inner
  exact congr(Complex.re $(h v)) --TODO: HPMap with IsTracePreserving give the HermitianMat.trace version

alias MatrixMap.IsTracePreserving.hermDual := HPMap.hermDual_Unital

namespace PTPMap

variable [DecidableEq dIn] [DecidableEq dOut]

def hermDual (M : PTPMap dIn dOut) : PUMap dOut dIn where
  toHPMap := M.toHPMap.hermDual
  pos := M.pos.hermDual
  unital := M.TP.hermDual

theorem hermDual_pos (M : PTPMap dIn dOut) {T : HermitianMat dOut â„‚} (hT : 0 â‰¤ T) :
    0 â‰¤ M.hermDual T := by
  exact M.hermDual.pos_Hermitian hT

/-- The dual of a PTP map preserves POVMs. Stated here just for two-element POVMs, that is, an
operator `T` between 0 and 1. -/
theorem hermDual.PTP_POVM (M : PTPMap dIn dOut) {T : HermitianMat dOut â„‚} (hT : 0 â‰¤ T âˆ§ T â‰¤ 1) :
    (0 â‰¤ M.hermDual T âˆ§ M.hermDual T â‰¤ 1) := by
  rcases hT with âŸ¨hTâ‚, hTâ‚‚âŸ©
  have hT_psd := HermitianMat.zero_le_iff.mp hTâ‚
  use M.hermDual.pos_Hermitian hTâ‚
  simpa using ContinuousOrderHomClass.map_monotone M.hermDual hTâ‚‚

/-- The defining property of a dual channel, as specialized to `MState.exp_val`. -/
theorem exp_val_hermDual (â„° : PTPMap dIn dOut) (Ï : MState dIn) (T : HermitianMat dOut â„‚) :
    (â„° Ï).exp_val T  = Ï.exp_val (â„°.hermDual T) := by
  simp only [MState.exp_val]
  apply HPMap.inner_hermDual'

end PTPMap

end hermDual
