import QuantumInfo.Finite.CPTPMap

/-!
Quantum theory and operations specific to qubits.
 - Standard named (single-qubit) gates: Z, X, Y, H, S, T
 - Controlled versions of gates
 - Completeness of the PPT test: a state is separable iff it is PPT.
 - Fidelity for qubits: `F(Ï,Ïƒ) = 2âˆš(Ï.det * Ïƒ.det)`.
 - The singlet/triplet split.
-/

abbrev Qubit := Fin 2

section Mathlib
namespace Matrix

variable {Î± : Type*} [NonUnitalNonAssocSemiring Î±] [StarRing Î±]

variable {Î± Î² : Type*} [DecidableEq Î±] [Fintype Î±] [DecidableEq Î²] [Fintype Î²]

@[simp]
theorem neg_unitary_val (u : ğ”[Î±]) : (-u).val = -u := by
  rfl

omit [DecidableEq Î±] [Fintype Î±] [DecidableEq Î²] [Fintype Î²] in
open Kronecker in
@[simp]
theorem star_kron (a : Matrix Î± Î± â„‚) (b : Matrix Î² Î² â„‚) : star (a âŠ—â‚– b) = (star a) âŠ—â‚– (star b) := by
  ext _ _
  simp

open Kronecker in
theorem kron_unitary (a : ğ”[Î±]) (b : ğ”[Î²]) : a.val âŠ—â‚– b.val âˆˆ ğ”[Î± Ã— Î²] := by
  simp [Matrix.mem_unitaryGroup_iff, â† Matrix.mul_kronecker_mul]

open Kronecker in
def unitary_kron (a : ğ”[Î±]) (b : ğ”[Î²]) : ğ”[Î± Ã— Î²] :=
  âŸ¨_, kron_unitary a bâŸ©

scoped notation a:60 " âŠ—áµ¤ " b:60 => unitary_kron a b

@[simp]
theorem unitary_kron_apply (a : ğ”[Î±]) (b : ğ”[Î²]) (iâ‚ iâ‚‚ : Î±) (jâ‚ jâ‚‚ : Î²) :
    (a âŠ—áµ¤ b) (iâ‚, jâ‚) (iâ‚‚, jâ‚‚) = (a iâ‚ iâ‚‚) * (b jâ‚ jâ‚‚) := by
  rfl

@[simp]
theorem unitary_kron_one_one : (1 : ğ”[Î±]) âŠ—áµ¤ (1 : ğ”[Î²]) = (1 : ğ”[Î± Ã— Î²]) := by
  simp [Matrix.unitary_kron]

end Matrix
end Mathlib

open Lean.Parser.Tactic in
open Lean in
/--
Proves goals equating small matrices by expanding out products and simpliying standard Real arithmetic.
-/
syntax (name := matrix_expand) "matrix_expand"
  (" [" ((simpStar <|> simpErase <|> simpLemma),*,?) "]")?
  (" with " rcasesPat+)? : tactic

macro_rules
  | `(tactic| matrix_expand $[[$rules,*]]? $[with $withArg*]?) => do
    let id1 := (withArg.getD âŸ¨[]âŸ©).getD 0 (â† `(rcasesPat| _))
    let id2 := (withArg.getD âŸ¨[]âŸ©).getD 1 (â† `(rcasesPat| _))
    let rules' := rules.getD âŸ¨#[]âŸ©
    `(tactic| (
      ext i j
      repeat rcases (i : Prod _ _) with âŸ¨i, $id1âŸ©
      repeat rcases (j : Prod _ _) with âŸ¨j, $id2âŸ©
      fin_cases i
      <;> fin_cases j
      <;> simp [Complex.ext_iff,
        Matrix.mul_apply, Fintype.sum_prod_type, Matrix.one_apply,
        $rules',* ]
      <;> try field_simp
      <;> try ring_nf))

namespace Qubit
open Real
open Complex

variable {k : Type*} [Fintype k] [DecidableEq k]

/-- The Pauli Z gate on a qubit. -/
def Z : ğ”[Qubit] :=
  âŸ¨!![1, 0; 0, -1], by constructor <;> matrix_expandâŸ©

/-- The Pauli X gate on a qubit. -/
def X : ğ”[Qubit] :=
  âŸ¨!![0, 1; 1, 0], by constructor <;> matrix_expandâŸ©

/-- The Pauli Y gate on a qubit. -/
def Y : ğ”[Qubit] :=
  âŸ¨!![0, -I; I, 0], by constructor <;> matrix_expandâŸ©

/-- The H gate, a Hadamard gate, on a qubit. -/
noncomputable def H : ğ”[Qubit] :=
  âŸ¨âˆš(1/2) â€¢ (!![1, 1; 1, -1]), by constructor <;> matrix_expandâŸ©

/-- The S gate, or Rz(Ï€/2) rotation on a qubit. -/
def S : ğ”[Qubit] :=
  âŸ¨!![1, 0; 0, I], by constructor <;> matrix_expandâŸ©

/-- The T gate, or Rz(Ï€/4) rotation on a qubit. -/
noncomputable def T : ğ”[Qubit] :=
  âŸ¨!![1, 0; 0, (1 + I)/âˆš2], by constructor <;> matrix_expandâŸ©

@[simp]
theorem Z_sq : Z * Z = 1 := by
  matrix_expand [Z]

@[simp]
theorem X_sq : X * X = 1 := by
  matrix_expand [X]

@[simp]
theorem Y_sq : Y * Y = 1 := by
  matrix_expand [Y]

@[simp]
theorem H_sq : H * H = 1 := by
  matrix_expand [H]

@[simp]
theorem S_sq : S * S = Z := by
  matrix_expand [S, Z]

@[simp]
theorem T_sq : T * T = S := by
  matrix_expand [T, S]

/-- The anticommutator `{X,Y}` is zero. Marked simp as to put Pauli products in a canonical Y-X-Z order. -/
@[simp]
theorem X_Y_anticomm : X * Y = -Y * X := by
  matrix_expand [X, Y]

/-- The anticommutator `{Y,Z}` is zero. -/
theorem Y_Z_anticomm : Z * Y = -Y * Z := by
  matrix_expand [Z, Y]

/-- The anticommutator `{Z,X}` is zero. -/
theorem Z_X_anticomm : Z * X = -X * Z := by
  matrix_expand [Z, X]

@[simp]
theorem H_mul_X_eq_Z_mul_H : H * X = Z * H := by
  matrix_expand [H, X, Z]

@[simp]
theorem H_mul_Z_eq_X_mul_H : H * Z = X * H := by
  matrix_expand [H, X, Z]

@[simp]
theorem S_Z_comm : Z * S = S * Z := by
  simp [â† S_sq, mul_assoc]

@[simp]
theorem T_Z_comm : Z * T = T * Z := by
  simp [â† S_sq, â† T_sq, mul_assoc]

@[simp]
theorem S_T_comm : S * T = T * S := by
  simp [â† T_sq, mul_assoc]

/-- Given a unitary `U` on some Hilbert space `k`, we have the controllized version that acts on `Fin 2 âŠ— k`
where `U` is conditionally applied if the first qubit is `1`. -/
def controllize (g : ğ”[k]) : ğ”[Qubit Ã— k] :=
  âŸ¨Matrix.of fun (qâ‚,tâ‚) (qâ‚‚,tâ‚‚) â†¦
    if (qâ‚,qâ‚‚) = (0,0) then
      (if tâ‚ = tâ‚‚ then 1 else 0)
    else if (qâ‚,qâ‚‚) = (1,1) then
      g tâ‚ tâ‚‚
    else 0
    , by
      rw [Matrix.mem_unitaryGroup_iff]
      matrix_expand [-Complex.ext_iff] with ti tj;
      Â· congr 1
        exact propext eq_comm
      Â· exact congrFunâ‚‚ g.2.2 ti tj
    âŸ©

scoped notation "C[" g "]" => controllize g

variable (g : ğ”[k]) (jâ‚ jâ‚‚ : k)

@[simp]
theorem controllize_apply_zero_zero : C[g] (0, jâ‚) (0, jâ‚‚) = (1 : ğ”[k]) jâ‚ jâ‚‚ := by
  rfl

@[simp]
theorem controllize_apply_zero_one : C[g] (0, jâ‚) (1, jâ‚‚) = 0 := by
  rfl

@[simp]
theorem controllize_apply_one_zero : C[g] (1, jâ‚) (0, jâ‚‚) = 0 := by
  rfl

@[simp]
theorem controllize_apply_one_one : C[g] (1, jâ‚) (1, jâ‚‚) = g jâ‚ jâ‚‚ := by
  rfl

@[simp]
theorem controllize_mul (gâ‚ gâ‚‚ : ğ”[k]) : C[gâ‚] * C[gâ‚‚] = C[gâ‚ * gâ‚‚] := by
  matrix_expand

@[simp]
theorem controllize_one : C[(1 : ğ”[k])] = 1 := by
  matrix_expand

@[simp]
theorem controllize_mul_inv : C[g] * C[gâ»Â¹] = 1 := by
  simp

open scoped Matrix in
@[simp]
theorem X_controllize_X : (X âŠ—áµ¤ 1) * C[g] * (X âŠ—áµ¤ 1) = (1 âŠ—áµ¤ g) * C[gâ»Â¹] := by
  matrix_expand [X, -Complex.ext_iff] with ki kj;
  suffices (1 : Matrix k k â„‚) ki kj = (g * gâ»Â¹) ki kj by
    convert this
  simp

end Qubit
