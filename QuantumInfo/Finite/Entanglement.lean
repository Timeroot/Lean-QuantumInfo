/-
Copyright (c) 2025 Leonardo A Lessa. All rights reserved.
Released under MIT license as described in the file LICENSE.
Authors: Leonardo A Lessa
-/
import QuantumInfo.Finite.Braket
import QuantumInfo.Finite.Ensemble
import QuantumInfo.Finite.Entropy
import ClassicalInfo.Entropy

/-!
Entanglement measures. (Mixed) convex roof extensions. Definition of product / separable / entangled states
are in `Braket.lean` and/or `MState.lean`

Important definitions:
 * `convex_roof`: Convex roof extension of `g : Ket d ‚Üí ‚Ñù‚â•0`
 * `mixed_convex_roof`: Mixed convex roof extension of `f : MState d ‚Üí ‚Ñù‚â•0`
 * `EoF`: Entanglement of Formation

TODO:
 - Other entanglement measures (not necessarily based on convex roof extensions). In roughly increasing order of
   difficulty to implement: (Logarithmic) Negativity, Entanglement of Purification, Squashed Entanglement, Relative
   Entropy of Entanglement, Entanglement Cost, Distillable Entanglement.
   For a compendium on the zoo of entanglement measures, see
   [1] Christandl, Matthias. ‚ÄúThe Structure of Bipartite Quantum States - Insights from Group Theory and Cryptography.‚Äù
       https://doi.org/10.48550/arXiv.quant-ph/0604183.
 - Define classes of entanglement measures with good properties, including: monotonicity under LOCC (easier: just LO),
   monotonicity on average under LOCC, convexity (if the latter two are present, it is called an entanglement monotone
   by some), vanishing on separable states, normalized on the maximally entangled state, (sub)additivity, regularisible.
   For other properties, see [1] above and:
   [2] Szalay, Szil√°rd. ‚ÄúMultipartite Entanglement Measures.‚Äù (mainly Sec. IV)
       https://doi.org/10.1103/PhysRevA.92.042329.
   [3] Horodecki, Ryszard, Pawe≈Ç Horodecki, Micha≈Ç Horodecki, and Karol Horodecki. ‚ÄúQuantum Entanglement.‚Äù
       https://doi.org/10.1103/RevModPhys.81.865.
 - Useful properties of convex roof extensions:
   1. If f is monotonically non-increasing under LOCC, so is its convex roof.
   2. If f œà is zero if and only if œà is a product state, then its convex roof is faithful: zero if and only if
      the mixed state is separable
   For other properties, see Sec. IV.F of [2] above.
-/

noncomputable section

open ENNReal
open NNReal
open MState
open Ensemble

/-- Convex roof extension of a function `g : Ket d ‚Üí ‚Ñù‚â•0`, defined as the infimum of all pure-state
ensembles of a given `œÅ` of the average of `g` in that ensemble.

This is valued in the extended nonnegative real numbers `‚Ñù‚â•0‚àû` to have good properties of the infimum, which
come from the fact that `‚Ñù‚â•0‚àû` is a complete lattice. For example, it is necessary for `le_iInf` and `iInf_le_of_le`.
However, it is also proven in `convex_roof_ne_top` that the convex roof is never `‚àû`, so the definition `convex_roof` should
be used in most applications. -/
def convex_roof_ENNReal {d : Type _} [Fintype d] [DecidableEq d] (g : Ket d ‚Üí ‚Ñù‚â•0) : MState d ‚Üí ‚Ñù‚â•0‚àû := fun œÅ =>
  ‚®Ö (n : ‚Ñï+) (e : PEnsemble d (Fin n)) (_ : mix (toMEnsemble e) = œÅ), ‚Üë(pure_average_NNReal g e)

/-- Mixed convex roof extension of a function `f : MState d ‚Üí ‚Ñù‚â•0`, defined as the infimum of all mixed-state
ensembles of a given `œÅ` of the average of `f` on that ensemble.

This is valued in the extended nonnegative real numbers `‚Ñù‚â•0‚àû` to have good properties of the infimum, which
come from the fact that `‚Ñù‚â•0‚àû` is a complete lattice (see `ENNReal.instCompleteLinearOrder`). However,
it is also proven in `mixed_convex_roof_ne_top` that the mixed convex roof is never `‚àû`, so the definition `mixed_convex_roof` should
be used in most applications. -/
def mixed_convex_roof_ENNReal {d : Type _} [Fintype d] [DecidableEq d] (f : MState d ‚Üí ‚Ñù‚â•0) : MState d ‚Üí ‚Ñù‚â•0‚àû := fun œÅ =>
  ‚®Ö (n : ‚Ñï+) (e : MEnsemble d (Fin n)) (_ : mix e = œÅ), ‚Üë(average_NNReal f e)

variable {d d‚ÇÅ d‚ÇÇ : Type _} [Fintype d] [Fintype d‚ÇÅ] [Fintype d‚ÇÇ] [Nonempty d] [Nonempty d‚ÇÅ] [Nonempty d‚ÇÇ]
variable [DecidableEq d] [DecidableEq d‚ÇÅ] [DecidableEq d‚ÇÇ]
variable (f : MState d ‚Üí ‚Ñù‚â•0)
variable (g : Ket d ‚Üí ‚Ñù‚â•0)

/-- The convex roof extension `convex_roof_ENNReal` is never ‚àû. -/
theorem convex_roof_ne_top : ‚àÄ œÅ, convex_roof_ENNReal g œÅ ‚â† ‚àû := fun œÅ => by
  simp only [convex_roof_ENNReal, ne_eq, iInf_eq_top, coe_ne_top, imp_false, not_forall]
  use ‚ü®Fintype.card d, Fintype.card_pos‚ü©
  have ed : d ‚âÉ Fin ‚Üë(‚ü®Fintype.card d, Fintype.card_pos‚ü© : ‚Ñï+) := by
    simp only
    exact Fintype.equivFin d
  use (congrPEnsemble ed) <| spectral_ensemble œÅ
  rw [mix_congrPEnsemble_eq_mix ed]
  push_neg
  convert spectral_ensemble_mix

omit [Nonempty d] in
/-- The convex roof extension `mixed_convex_roof_ENNReal` is never ‚àû. -/
theorem mixed_convex_roof_ne_top : ‚àÄ œÅ, mixed_convex_roof_ENNReal f œÅ ‚â† ‚àû := fun œÅ => by
  simp only [mixed_convex_roof_ENNReal, ne_eq, iInf_eq_top, coe_ne_top, imp_false, not_forall]
  use 1, trivial_mEnsemble œÅ 0
  push_neg
  exact trivial_mEnsemble_mix œÅ 0

/-- Convex roof extension of a function `g : Ket d ‚Üí ‚Ñù‚â•0`, defined as the infimum of all pure-state
ensembles of a given `œÅ` of the average of `g` in that ensemble.

This is valued in the nonnegative real numbers `‚Ñù‚â•0` by applying `ENNReal.toNNReal` to `convex_roof_ENNReal`. Hence,
it should be used in proofs alongside `convex_roof_ne_top`. -/
def convex_roof : MState d ‚Üí ‚Ñù‚â•0 := fun x ‚Ü¶ (convex_roof_ENNReal g x).untop (convex_roof_ne_top g x)

/-- Mixed convex roof extension of a function `f : MState d ‚Üí ‚Ñù‚â•0`, defined as the infimum of all mixed-state
ensembles of a given `œÅ` of the average of `f` on that ensemble.

This is valued in the nonnegative real numbers `‚Ñù‚â•0` by applying `ENNReal.toNNReal` to `mixed_convex_roof_ENNReal`. Hence,
it should be used in proofs alongside `mixed_convex_roof_ne_top`. -/
def mixed_convex_roof : MState d ‚Üí ‚Ñù‚â•0 := fun x ‚Ü¶ (mixed_convex_roof_ENNReal f x).untop (mixed_convex_roof_ne_top f x)

/-- Auxiliary function. Convex roof of a function `f : MState d ‚Üí ‚Ñù‚â•0` defined over mixed states by resctricting `f` to pure states -/
def convex_roof_of_MState_fun : MState d ‚Üí ‚Ñù‚â•0 := convex_roof (f ‚àò pure)

-- TODO: make `le_convex_roof`, `convex_roof_le`, `le_mixed_convex_roof` and `mixed_convex_roof_le` if-and-only-if statements.

omit [Nonempty d] in
theorem le_mixed_convex_roof (œÅ : MState d) :
  (‚àÄ n > 0, ‚àÄ e : MEnsemble d (Fin n), mix e = œÅ ‚Üí c ‚â§ average_NNReal f e) ‚Üí (c ‚â§ mixed_convex_roof f œÅ) := fun h => by
  unfold mixed_convex_roof
  rw [WithTop.le_untop_iff]
  apply le_iInf; intro ‚ü®n, hnpos‚ü©; apply le_iInf; intro e; apply le_iInf; intro hmix
  rw [some_eq_coe', ENNReal.coe_le_coe]
  exact h n hnpos e hmix

theorem le_convex_roof (œÅ : MState d) :
  (‚àÄ n > 0, ‚àÄ e : PEnsemble d (Fin n), mix (toMEnsemble e) = œÅ ‚Üí c ‚â§ pure_average_NNReal g e) ‚Üí (c ‚â§ convex_roof g œÅ) := fun h => by
  unfold convex_roof
  rw [WithTop.le_untop_iff]
  apply le_iInf; intro ‚ü®n, hnpos‚ü©; apply le_iInf; intro e; apply le_iInf; intro hmix
  rw [some_eq_coe', ENNReal.coe_le_coe]
  exact h n hnpos e hmix

theorem convex_roof_le (œÅ : MState d):
(‚àÉ n > 0, ‚àÉ e : PEnsemble d (Fin n), mix (toMEnsemble e) = œÅ ‚àß pure_average_NNReal g e ‚â§ c) ‚Üí (convex_roof g œÅ ‚â§ c) := fun h => by
  obtain ‚ü®n, hnpos, e, hmix, h‚ü© := h
  unfold convex_roof
  rw [WithTop.untop_le_iff]
  apply iInf_le_of_le ‚ü®n, hnpos‚ü©; apply iInf_le_of_le e; apply iInf_le_of_le hmix
  rw [some_eq_coe', ENNReal.coe_le_coe]
  exact h

omit [Nonempty d] in
theorem mixed_convex_roof_le (œÅ : MState d):
(‚àÉ n > 0, ‚àÉ e : MEnsemble d (Fin n), mix e = œÅ ‚àß average_NNReal f e ‚â§ c) ‚Üí (mixed_convex_roof f œÅ ‚â§ c) := fun h => by
  obtain ‚ü®n, hnpos, e, hmix, h‚ü© := h
  unfold mixed_convex_roof
  rw [WithTop.untop_le_iff]
  apply iInf_le_of_le ‚ü®n, hnpos‚ü©; apply iInf_le_of_le e; apply iInf_le_of_le hmix
  rw [some_eq_coe', ENNReal.coe_le_coe]
  exact h

/-- The mixed convex roof extension of `f` is smaller than or equal to its convex roof extension, since
the former minimizes over a larger set of ensembles. -/
theorem mixed_convex_roof_le_convex_roof : mixed_convex_roof f ‚â§ convex_roof_of_MState_fun f := by
  intro œÅ
  apply le_convex_roof (f ‚àò pure) œÅ
  intro n hnpos e hmix
  apply mixed_convex_roof_le
  use n
  apply And.intro hnpos
  use ‚Üëe
  apply And.intro hmix
  exact le_of_eq <| NNReal.coe_inj.mp <| average_of_pure_ensemble (toReal ‚àò f) e

/-- The convex roof extension of `g : Ket d ‚Üí ‚Ñù‚â•0` applied to a pure state `œà` is `g œà`. -/
theorem convex_roof_of_pure (œà : Ket d) : convex_roof g (pure œà) = g œà := by
  rw [le_antisymm_iff]
  constructor
  ¬∑ apply convex_roof_le
    use 1; simp only [gt_iff_lt, zero_lt_one, true_and]; use trivial_pEnsemble œà 0
    constructor
    ¬∑ exact trivial_pEnsemble_mix œà 0
    ¬∑ simp only [pure_average_NNReal, Fin.isValue, ‚Üê NNReal.coe_le_coe, coe_mk]
      rw [trivial_pEnsemble_average œà _ 0]
      rfl
  ¬∑ apply le_convex_roof
    intro n hnpos e hmix
    replace hpure := mix_pEnsemble_pure_iff_pure.mp hmix
    apply le_of_eq
    simp only [pure_average_NNReal, ‚Üê NNReal.coe_inj, coe_mk]
    rw [mix_pEnsemble_pure_average (toReal ‚àò g) hmix, Function.comp_apply]

omit [Nonempty d] in
/-- The mixed convex roof extension of `f : MState d ‚Üí ‚Ñù‚â•0` applied to a pure state `œà` is `f (pure œà)`. -/
theorem mixed_convex_roof_of_pure (œà : Ket d) : mixed_convex_roof f (pure œà) = f (pure œà) := by
  rw [le_antisymm_iff]
  constructor
  ¬∑ apply mixed_convex_roof_le
    use 1; simp only [gt_iff_lt, zero_lt_one, true_and]; use trivial_mEnsemble (pure œà) 0
    constructor
    ¬∑ exact trivial_mEnsemble_mix (pure œà) 0
    ¬∑ simp only [average_NNReal, Fin.isValue, ‚Üê NNReal.coe_le_coe, coe_mk]
      rw [trivial_mEnsemble_average _ (pure œà) 0]
      rfl
  ¬∑ apply le_mixed_convex_roof
    intro n hnpos e hmix
    replace hpure := mix_mEnsemble_pure_iff_pure.mp hmix
    apply le_of_eq
    simp only [average_NNReal, ‚Üê NNReal.coe_inj, coe_mk]
    rw [mix_mEnsemble_pure_average (toReal ‚àò f) hmix, Function.comp_apply]

/-- Entanglement of Formation of bipartite systems. It is the convex roof extension of the
von Neumann entropy of one of the subsystems (here chosen to be the left one, but see `Entropy.S·µ•‚Çô_of_partial_eq`). -/
def EoF : MState (d‚ÇÅ √ó d‚ÇÇ) ‚Üí ‚Ñù‚â•0 := convex_roof (fun œà ‚Ü¶ ‚ü®S·µ•‚Çô (pure œà).traceRight, S·µ•‚Çô_nonneg (pure œà).traceRight‚ü©)

/-
The partial trace of the maximally entangled state is the maximally mixed state.
-/
theorem traceRight_pure_MES (d : Type*) [Fintype d] [DecidableEq d] [Nonempty d] :
    (MState.pure (Ket.MES d)).traceRight = MState.uniform := by
  -- By definition of partial trace, we sum over the second system.
  have h_partial_trace : ‚àÄ (i j : d), ‚àë k : d, (Ket.MES d).vec (i, k) * (star (Ket.MES d).vec (j, k)) = (1 / Fintype.card d : ‚Ñù) * (if i = j then 1 else 0) := by
    unfold Ket.MES
    intro i j
    simp only [one_div, Pi.star_apply, RCLike.star_def, ite_mul, zero_mul, Finset.sum_ite_eq,
      Finset.mem_univ, ‚ÜìreduceIte, Complex.ofReal_inv]
    split
    ¬∑ subst i
      simp only [map_inv‚ÇÄ, Complex.conj_ofReal]
      ring_nf; norm_cast; norm_num;
    ¬∑ grind [map_zero]
  unfold MState.pure MState.traceRight MState.uniform
  ext i j
  convert h_partial_trace i j
  simp_all only [Pi.star_apply, RCLike.star_def, one_div, Complex.ofReal_inv,
    Complex.ofReal_natCast, mul_ite, mul_one, mul_zero, HermitianMat.toMat_apply,
    coe_ofClassical, Distribution.uniform_def, Finset.card_univ]
  unfold HermitianMat.diagonal
  simp_all only [Complex.ofReal_inv, Complex.ofReal_natCast]
  rfl

/-
The von Neumann entropy of a state is equal to the trace of `œÅ log œÅ` (technically `cfc œÅ negMulLog`).
-/
theorem S·µ•‚Çô_eq_trace_cfc {d : Type*} [Fintype d] [DecidableEq d] (œÅ : MState d) :
    S·µ•‚Çô œÅ = (HermitianMat.cfc œÅ.M Real.negMulLog).trace := by
  -- By definition of von Neumann entropy, we have S·µ•‚Çô œÅ = Finset.sum Finset.univ (fun x ‚Ü¶ Real.negMulLog (œÅ.M.H.eigenvalues x)).
  have h_def : S·µ•‚Çô œÅ = Finset.sum Finset.univ (fun x ‚Ü¶ Real.negMulLog (œÅ.M.H.eigenvalues x)) := by
    rfl
  -- By definition of trace, the trace of `cfc œÅ.M Real.negMulLog` is the sum of its eigenvalues.
  have h_trace : (œÅ.M.cfc Real.negMulLog).trace =
      ‚àë x, (œÅ.M.cfc Real.negMulLog).H.eigenvalues x := by
    exact (HermitianMat.sum_eigenvalues_eq_trace _).symm
  obtain ‚ü®e, he‚ü© : ‚àÉ e : d ‚âÉ d, (œÅ.M.cfc Real.negMulLog).H.eigenvalues =
      Real.negMulLog ‚àò œÅ.M.H.eigenvalues ‚àò e := by
   exact Matrix.IsHermitian.cfc_eigenvalues _ _
  rw [h_def, h_trace, he]
  simp only [Function.comp_apply]
  conv_lhs => rw [ ‚Üê Equiv.sum_comp e ]

/-
The von Neumann entropy of a classical state (diagonal in the basis) is equal to the Shannon entropy of the corresponding distribution.
-/
theorem S·µ•‚Çô_ofClassical {d : Type*} [Fintype d] [DecidableEq d] (dist : Distribution d) :
    S·µ•‚Çô (MState.ofClassical dist) = H‚Çõ dist := by
  -- Let's unfold the definition of `S·µ•‚Çô` using `S·µ•‚Çô_eq_trace_cfc`.
  have h_def : S·µ•‚Çô (MState.ofClassical dist) = (HermitianMat.cfc (MState.ofClassical dist).M Real.negMulLog).trace := by
    exact S·µ•‚Çô_eq_trace_cfc (ofClassical dist);
  convert h_def using 1;
  -- By definition of $MState.ofClassical$, we know that $(MState.ofClassical dist).M$ is a diagonal matrix with entries $dist i$.
  have h_diag : (MState.ofClassical dist).M = HermitianMat.diagonal (fun x => dist x) := by
    exact rfl;
  rw [ h_diag, HermitianMat.cfc_diagonal, HermitianMat.trace_diagonal ] ; aesop

/-- The entanglement of formation of the maximally entangled state with on-site dimension ùïï is log(ùïï). -/
theorem EoF_of_MES : EoF (pure <| Ket.MES d) = Real.log (Finset.card Finset.univ (Œ± := d)) := by
  simp only [EoF, convex_roof_of_pure, coe_mk, Finset.card_univ]
  simp only [traceRight, Matrix.traceRight, MState.pure, Ket.MES, one_div]
  -- The von Neumann entropy of the maximally mixed state is log(d).
  have h_von_neumann : S·µ•‚Çô (MState.uniform : MState d) = Real.log (Fintype.card d) := by
    rw [MState.uniform, S·µ•‚Çô_ofClassical Distribution.uniform, H‚Çõ_uniform, Finset.card_univ]
  convert h_von_neumann using 2;
  convert traceRight_pure_MES d using 1;
  unfold Ket.MES;
  simp_all only [one_div]
  rfl
